module Lp_certificate = struct

(* inequality id, indices of inequalities, coefficients *)
type constraint_type = string * int list * int64 list;;

type terminal_case = {
  precision : int;
  constraints : constraint_type list;
  target_variables : constraint_type list;
  variable_bounds : constraint_type list;
};;

type split_type = Triangle_split | Quad_split | Pent_split | Hex_split;;

type split_case = {
  split_type : split_type;
  split_face : int list;
};;

type lp_certificate_case = 
    Lp_terminal of terminal_case
  | Lp_split of split_case * lp_certificate_case list;;

type lp_certificate = {
  hypermap_string : string;
  root_case : lp_certificate_case;
};;

(* Counts the number of terminal cases *)
let rec count_terminals lp_case =
  match lp_case with
    | Lp_terminal _ -> 1
    | Lp_split (_, cs) -> itlist (+) (map count_terminals cs) 0;;


(* Writes a certificate into a binary file *)
let write_lp_certificates fname (certificates : lp_certificate list) =
  let out = open_out_bin fname in
  let _ = Marshal.to_channel out certificates [] in
    close_out out;;

(* Reads a certificate from a binary file *)
let read_lp_certificates fname =
  let input = open_in_bin fname in
  let certificates = (Marshal.from_channel input : lp_certificate list) in
  let _ = close_in input in
    certificates;;

end;;
