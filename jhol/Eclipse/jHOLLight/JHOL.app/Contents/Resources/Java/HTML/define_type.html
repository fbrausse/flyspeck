<link rel = "stylesheet" href = "hollightref.css">
<DIV class=TYPE><PRE>define_type : string -&gt; thm * thm</PRE></DIV>
<DL>

<P><DT><SPAN CLASS=FIELD-NAME>SYNOPSIS</SPAN><DD>
Automatically define user-specified inductive data types.

<P><DT><SPAN CLASS=FIELD-NAME>DESCRIPTION</SPAN><DD>
The function <SPAN CLASS=BRKT>define_type</SPAN> automatically defines an inductive data type or a
mutually inductive family of them. These may optionally contain nested
instances of other inductive data types. The function returns two theorems that
together identify the type up to isomorphism. The input is just a string
indicating the desired pattern of recursion. The simplest case where we define
a single type is:
<DIV class=XMPL><PRE>
   "op = C1 ty ... ty | C2 ty ... ty | ... | Cn ty ... ty"
</PRE></DIV>
where <SPAN CLASS=BRKT>op</SPAN> is the name of the type constant or type operator to be
defined, <SPAN CLASS=BRKT>C1</SPAN>, ..., <SPAN CLASS=BRKT>Cn</SPAN> are identifiers, and each <SPAN CLASS=BRKT>ty</SPAN> is either a (logical)
type expression valid in the current theory (in which case <SPAN CLASS=BRKT>ty</SPAN> must not
contain <SPAN CLASS=BRKT>op</SPAN>) or just the identifier "<SPAN CLASS=BRKT>op</SPAN>' itself.

A string of this form describes an n-ary type operator <SPAN CLASS=BRKT>op</SPAN>, where n is the
number of distinct type variables in the types <SPAN CLASS=BRKT>ty</SPAN> on the right hand side of
the equation.  If n is zero then <SPAN CLASS=BRKT>op</SPAN> is a type constant; otherwise <SPAN CLASS=BRKT>op</SPAN> is an
n-ary type operator.  The type described by the specification has <SPAN CLASS=BRKT>n</SPAN> distinct
constructors <SPAN CLASS=BRKT>C1</SPAN>, ..., <SPAN CLASS=BRKT>Cn</SPAN>.  Each constructor <SPAN CLASS=BRKT>Ci</SPAN> is a function that takes
arguments whose types are given by the associated type expressions <SPAN CLASS=BRKT>ty</SPAN> in the
specification. If one or more of the type expressions <SPAN CLASS=BRKT>ty</SPAN> is the type <SPAN CLASS=BRKT>op</SPAN>
itself, then the equation specifies a recursive data type.  In any
specification, at least one constructor must be non-recursive, i.e. all its
arguments must have types which already exist in the current theory.

Each of the types <SPAN CLASS=BRKT>ty</SPAN> above may be built from the type being defined using
other inductive type operators already defined, e.g. <SPAN CLASS=BRKT>list</SPAN>. Moreover, one can
actually have a mutually recursive family of types, where the format is a
sequence of specifications in the above form separated by semicolons:
<DIV class=XMPL><PRE>
  "op1 = C1_1 ty ... ty | C1_2 ty ... ty | ... | C1_n1 ty ... ty;
   op2 = C2_1 ty ... ty | ... | C2_n2 ty ... ty;
   ...
   opk = Ck_1 ty ... ty | ... | ... | Ck_nk ty ... ty"
</PRE></DIV>

Given a type specification of the form described above, <SPAN CLASS=BRKT>define_type</SPAN> makes an
appropriate type definition for the type operator or type operators.  It then
makes appropriate definitions for the constants <SPAN CLASS=BRKT>Ci_j</SPAN> and automatically proves
and returns two theorems that characterize the type up to isomorphism. Roughly,
the first theorem allows one to prove properties over the new (family of) types
by (mutual) induction, while the latter allows one to defined functions by
recursion. Rather than presenting these in full generality, it is probably
easier to consider some simple examples.

<P><DT><SPAN CLASS=FIELD-NAME>FAILURE CONDITIONS</SPAN><DD>
The evaluation fails if one of the types or constructor constants is already
defined, or if there are certain improper kinds of recursion, e.g. involving
function spaces of one of the types being defined.

<P><DT><SPAN CLASS=FIELD-NAME>EXAMPLE</SPAN><DD>
The following call to <SPAN CLASS=BRKT>define_type</SPAN> defines <SPAN CLASS=BRKT>tri</SPAN> to be a simple enumerated
type with exactly three distinct values:
<DIV class=XMPL><PRE>
  # define_type "tri = ONE | TWO | THREE";;
  val it : thm * thm =
    (|- !P. P ONE /\ P TWO /\ P THREE ==> (!x. P x),
     |- !f0 f1 f2. ?fn. fn ONE = f0 /\ fn TWO = f1 /\ fn THREE = f2)
</PRE></DIV>
The theorem returned is a degenerate `primitive recursion' theorem
for the concrete type <SPAN CLASS=BRKT>tri</SPAN>.  An example of a recursive type that can be
defined using <SPAN CLASS=BRKT>define_type</SPAN> is a type of binary trees:
<DIV class=XMPL><PRE>
  # define_type "btree = LEAF A | NODE btree btree";;
  val it : thm * thm =
    (|- !P. (!a. P (LEAF a)) /\ (!a0 a1. P a0 /\ P a1 ==> P (NODE a0 a1))
            ==> (!x. P x),
     |- !f0 f1.
            ?fn. (!a. fn (LEAF a) = f0 a) /\
                 (!a0 a1. fn (NODE a0 a1) = f1 a0 a1 (fn a0) (fn a1)))
</PRE></DIV>
The theorem returned by <SPAN CLASS=BRKT>define_type</SPAN> in this case asserts the unique
existence of functions defined by primitive recursion over labelled binary
trees. For an example of nested recursion, here we use the type of lists in a
nested fashion to define a type of first-order terms:
<DIV class=XMPL><PRE>
  # define_type "term = Var num | Fn num (term list)";;
  val it : thm * thm =
    (|- !P0 P1.
            (!a. P0 (Var a)) /\
            (!a0 a1. P1 a1 ==> P0 (Fn a0 a1)) /\
            P1 [] /\
            (!a0 a1. P0 a0 /\ P1 a1 ==> P1 (CONS a0 a1))
            ==> (!x0. P0 x0) /\ (!x1. P1 x1),
     |- !f0 f1 f2 f3.
            ?fn0 fn1.
                (!a. fn1 (Var a) = f0 a) /\
                (!a0 a1. fn1 (Fn a0 a1) = f1 a0 a1 (fn0 a1)) /\
                fn0 [] = f2 /\
                (!a0 a1. fn0 (CONS a0 a1) = f3 a0 a1 (fn1 a0) (fn0 a1)))
</PRE></DIV>
and here we have an example of mutual recursion, defining syntax
trees for commands and expressions for a hypothetical programming language:
<DIV class=XMPL><PRE>
  # define_type "command = Assign num expression
                         | Ite expression command command;
                 expression = Variable num
                            | Constant num
                            | Plus expression expression
                            | Valof command";;
  val it : thm * thm =
    (|- !P0 P1.
            (!a0 a1. P1 a1 ==> P0 (Assign a0 a1)) /\
            (!a0 a1 a2. P1 a0 /\ P0 a1 /\ P0 a2 ==> P0 (Ite a0 a1 a2)) /\
            (!a. P1 (Variable a)) /\
            (!a. P1 (Constant a)) /\
            (!a0 a1. P1 a0 /\ P1 a1 ==> P1 (Plus a0 a1)) /\
            (!a. P0 a ==> P1 (Valof a))
            ==> (!x0. P0 x0) /\ (!x1. P1 x1),
     |- !f0 f1 f2 f3 f4 f5.
            ?fn0 fn1.
                (!a0 a1. fn0 (Assign a0 a1) = f0 a0 a1 (fn1 a1)) /\
                (!a0 a1 a2.
                     fn0 (Ite a0 a1 a2) =
                     f1 a0 a1 a2 (fn1 a0) (fn0 a1) (fn0 a2)) /\
                (!a. fn1 (Variable a) = f2 a) /\
                (!a. fn1 (Constant a) = f3 a) /\
                (!a0 a1. fn1 (Plus a0 a1) = f4 a0 a1 (fn1 a0) (fn1 a1)) /\
                (!a. fn1 (Valof a) = f5 a (fn0 a)))
</PRE></DIV>

<P><DT><SPAN CLASS=FIELD-NAME>SEE ALSO</SPAN><DD>
<A href="INDUCT_THEN.html">INDUCT_THEN</A>, <A href="new_recursive_definition.html">new_recursive_definition</A>, <A href="new_type_abbrev.html">new_type_abbrev</A>, <A href="prove_cases_thm.html">prove_cases_thm</A>,
<A href="prove_constructors_distinct.html">prove_constructors_distinct</A>, <A href="prove_constructors_one_one.html">prove_constructors_one_one</A>, <A href="prove_induction_thm.html">prove_induction_thm</A>,
<A href="prove_rec_fn_exists.html">prove_rec_fn_exists</A>.


</DL>
