<link rel = "stylesheet" href = "hollightref.css">
<DIV class=TYPE><PRE>prove_inductive_relations_exist : term -&gt; thm</PRE></DIV>
<DL>

<P><DT><SPAN CLASS=FIELD-NAME>SYNOPSIS</SPAN><DD>
Prove existence of inductively defined relations without defining them.

<P><DT><SPAN CLASS=FIELD-NAME>DESCRIPTION</SPAN><DD>
The function <SPAN CLASS=BRKT>prove_inductive_relations_exist</SPAN> should be given a specification
for an inductively defined relation <SPAN CLASS=BRKT>R</SPAN>, or more generally a family
<SPAN CLASS=BRKT>R1,...,Rn</SPAN> of mutually inductive relations; the required format is explained
further in the entry for <SPAN CLASS=BRKT>new_inductive_definition</SPAN>. It returns an existential
theorem <SPAN CLASS=BRKT>A |- ?R1 ... Rn. rules /\ induction /\ cases</SPAN>, where <SPAN CLASS=BRKT>rules</SPAN>,
<SPAN CLASS=BRKT>induction</SPAN> and <SPAN CLASS=BRKT>cases</SPAN> are the rule, induction and cases theorems, explained
further in the entry for <SPAN CLASS=BRKT>new_inductive_definition</SPAN>. In contrast with
<SPAN CLASS=BRKT>new_inductive_definition</SPAN>, no actual definitions are made. The assumption list
<SPAN CLASS=BRKT>A</SPAN> is normally empty, but will include any monotonicity hypotheses that were
not proven automatically.

<P><DT><SPAN CLASS=FIELD-NAME>FAILURE CONDITIONS</SPAN><DD>
Fails if the form of the rules is wrong.

<P><DT><SPAN CLASS=FIELD-NAME>EXAMPLE</SPAN><DD>
The traditional example of even and odd numbers:
<DIV class=XMPL><PRE>
  # prove_inductive_relations_exist
     `even(0) /\ odd(1) /\
      (!n. even(n) ==> odd(n + 1)) /\
      (!n. odd(n) ==> even(n + 1))`;;
  val it : thm =
    |- ?even odd.
           (even 0 /\
            odd 1 /\
            (!n. even n ==> odd (n + 1)) /\
            (!n. odd n ==> even (n + 1))) /\
           (!odd' even'.
                even' 0 /\
                odd' 1 /\
                (!n. even' n ==> odd' (n + 1)) /\
                (!n. odd' n ==> even' (n + 1))
                ==> (!a0. odd a0 ==> odd' a0) /\ (!a1. even a1 ==> even' a1)) /\
           (!a0. odd a0 <=> a0 = 1 \/ (?n. a0 = n + 1 /\ even n)) /\
           (!a1. even a1 <=> a1 = 0 \/ (?n. a1 = n + 1 /\ odd n))
</PRE></DIV>
Here is a example where we get a nonempty list of hypotheses because
HOL cannot prove monotonicity (and indeed, it doesn't hold).
<DIV class=XMPL><PRE>
  # prove_inductive_relations_exist `!x. ~P(x) ==> P(x+1)`;;
  val it : thm =
    !P P'.
        (!a. P a ==> P' a)
        ==> (!a. (?x. a = x + 1 /\ ~P x) ==> (?x. a = x + 1 /\ ~P' x))
    |- ?P. (!x. ~P x ==> P (x + 1)) /\
           (!P'. (!x. ~P' x ==> P' (x + 1)) ==> (!a. P a ==> P' a)) /\
           (!a. P a <=> (?x. a = x + 1 /\ ~P x))
</PRE></DIV>

<P><DT><SPAN CLASS=FIELD-NAME>USES</SPAN><DD>
Using existence of inductive relations as an auxiliary device inside a proof.

<P><DT><SPAN CLASS=FIELD-NAME>SEE ALSO</SPAN><DD>
<A href="derive_strong_induction.html">derive_strong_induction</A>, <A href="new_inductive_definition.html">new_inductive_definition</A>, <A href="prove_monotonicity_hyps.html">prove_monotonicity_hyps</A>.


</DL>
