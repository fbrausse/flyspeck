<link rel = "stylesheet" href = "hollightref.css">
<DIV class=TYPE><PRE>derive_strong_induction : thm * thm -&gt; thm</PRE></DIV>
<DL>

<P><DT><SPAN CLASS=FIELD-NAME>SYNOPSIS</SPAN><DD>
Derive stronger induction theorem from inductive definition.

<P><DT><SPAN CLASS=FIELD-NAME>DESCRIPTION</SPAN><DD>
The function <SPAN CLASS=BRKT>derive_strong_induction</SPAN> is applied to a pair of theorems as
returned by <SPAN CLASS=BRKT>new_inductive_definition</SPAN>. The first theorem is the `rule'
theorem, the second the `induction' theorem; the `case' theorem returned by
<SPAN CLASS=BRKT>new_inductive_definition</SPAN> is not needed. It returns a stronger induction
theorem where instances of each inductive predicate occurring in hypotheses
is conjoined with the corresponding inductive relation too.

<P><DT><SPAN CLASS=FIELD-NAME>FAILURE CONDITIONS</SPAN><DD>
Fails if the two input theorems are not of the correct form for rule and
induction theorems returned by <SPAN CLASS=BRKT>new_inductive_definition</SPAN>.

<P><DT><SPAN CLASS=FIELD-NAME>EXAMPLE</SPAN><DD>
A simple example of a mutually inductive definition is:
<DIV class=XMPL><PRE>
  # let eo_RULES,eo_INDUCT, eo_CASES = new_inductive_definition
     `even(0) /\ odd(1) /\
      (!n. even(n) ==> odd(n + 1)) /\
      (!n. odd(n) ==> even(n + 1))`;;
  val eo_RULES : thm =
    |- even 0 /\
       odd 1 /\
       (!n. even n ==> odd (n + 1)) /\
       (!n. odd n ==> even (n + 1))
  val eo_INDUCT : thm =
    |- !odd' even'.
           even' 0 /\
           odd' 1 /\
           (!n. even' n ==> odd' (n + 1)) /\
           (!n. odd' n ==> even' (n + 1))
           ==> (!a0. odd a0 ==> odd' a0) /\ (!a1. even a1 ==> even' a1)
  val eo_CASES : thm =
    |- (!a0. odd a0 <=> a0 = 1 \/ (?n. a0 = n + 1 /\ even n)) /\
       (!a1. even a1 <=> a1 = 0 \/ (?n. a1 = n + 1 /\ odd n))
</PRE></DIV>
The stronger induction theorem can be derived as follows. Note that it is
similar in form to <SPAN CLASS=BRKT>eo_INDUCT</SPAN> but has stronger hypotheses for two of the
conjuncts in the antecedent.
<DIV class=XMPL><PRE>
  # derive_strong_induction(eo_RULES,eo_INDUCT);;
  val it : thm =
    |- !odd' even'.
           even' 0 /\
           odd' 1 /\
           (!n. even n /\ even' n ==> odd' (n + 1)) /\
           (!n. odd n /\ odd' n ==> even' (n + 1))
           ==> (!a0. odd a0 ==> odd' a0) /\ (!a1. even a1 ==> even' a1)
</PRE></DIV>

<P><DT><SPAN CLASS=FIELD-NAME>COMMENTS</SPAN><DD>
This function needs to discharge monotonicity theorems as part of its internal
working, just as <SPAN CLASS=BRKT>new_inductive_definition</SPAN> does when the inductive definition
is made. Usually this is automatic and the user doesn't see it, but in
difficult cases, the theorem returned may have additional monotonicity
hypotheses that are unproven. In such cases, you can either try to prove them
manually or extend <SPAN CLASS=BRKT>monotonicity_theorems</SPAN> to make the built-in monotonicity
prover more powerful.

<P><DT><SPAN CLASS=FIELD-NAME>SEE ALSO</SPAN><DD>
<A href="new_inductive_definition.html">new_inductive_definition</A>, <A href="prove_inductive_relations_exist.html">prove_inductive_relations_exist</A>,
<A href="prove_monotonicity_hyps.html">prove_monotonicity_hyps</A>.


</DL>
