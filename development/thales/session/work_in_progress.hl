
module Work_in_progress = struct
end;;


open Hales_tactic;;


let LET_THM = CONJ LET_DEF LET_END_DEF;;

(* ineq "417..." has been added to Main_estimate, but until it gets reloaded, use this. *)

let get_work = 
  let is_main = function 
    | Main_estimate -> true
    | _ -> false in
  let has_main ind = 
    exists (is_main) ind.tags in
  let main_ineq_data1 = 
    filter has_main (!Ineq.ineqs) in
  let id = map (fun t-> t.idv) main_ineq_data1 in
    (* debug 
  let id = nonlinear_ineq_terminal in *)
  let main_ineq_data = map (fun t -> hd(Ineq.getexact t)) id in
  let ineql = map (fun ind -> ind.ineq) main_ineq_data in
  let sl = map (fun ind -> ind.idv) main_ineq_data in
  let main_ineq_conj = end_itlist (curry mk_conj) ineql in
  let th = new_definition (mk_eq (`main_work:bool`,main_ineq_conj)) in
  let th1 = UNDISCH (MATCH_MP (TAUT `(a <=> b) ==> (a ==> b)`) th) in
  let co1 thm = if (is_conj (concl thm)) then CONJUNCT1 thm else thm in
  let tryindex s sl = try index s sl with  _ -> report s; failwith s in
    fun s ->
      let i = tryindex s sl in
      let th2 = funpow i CONJUNCT2 th1 in
	co1 th2;;

let UNDISCH2 = repeat UNDISCH;;


(* *************************************************************************** *)
(* COMPLETED LEMMAS *)
(* *************************************************************************** *)

let cos_bounds_0_pi = prove_by_refinement(
  `!z. &0 < z /\ z < pi ==> -- &1 < cos z /\ cos z < &1`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  TYPIFY `(&0 < z /\ z < pi / &2) \/ z = pi / &2 \/ (&0 < pi - z /\ pi - z < pi / &2)` (C SUBGOAL_THEN ASSUME_TAC);
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  REPEAT (FIRST_X_ASSUM DISJ_CASES_TAC);
      BY(ASM_MESON_TAC[Counting_spheres.cos_bounds_0_Pi2;arith `&0 < x ==> -- &1 < x`]);
    ASM_REWRITE_TAC[COS_PI2];
    BY(REAL_ARITH_TAC);
  TYPIFY `-- &1 < --cos z /\ --cos z < &1` ENOUGH_TO_SHOW_TAC;
    BY(REAL_ARITH_TAC);
  ONCE_REWRITE_TAC[GSYM COS_NEG];
  REWRITE_TAC[GSYM COS_PERIODIC_PI;arith `--z + pi = pi - z`];
  BY(ASM_MESON_TAC[Counting_spheres.cos_bounds_0_Pi2;arith `&0 < x ==> -- &1 < x`])
  ]);;
  (* }}} *)


(* *************************************************************************** *)
(* WORK IN PROGRESS *)
(* *************************************************************************** *)




let ear_acute = prove_by_refinement(
  `main_nonlinear_terminal_v11 ==> (!y1 y2 y3 y4 y5 y6. 
    &2 <= y1 /\ y1 <= &2 * h0 /\
    &2 <= y2 /\ y2 <= &2 * h0 /\
    &2 <= y3 /\ y3 <= &2 * h0 /\
    &2 <= y4 /\ y4 <= &2 * h0 /\
    &2 <= y6 /\ y6 <= &2 * h0 /\
    &3 <= y5 /\
    &0 < ups_x (y1*y1) (y3*y3) (y5*y5) 
    ==> dih_y y1 y2 y3 y4 y5 y6 < pi / &2)
`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Sphere.h0;arith `&2 * #1.26  = #2.52`;Trigonometry.IHIQXLM];
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC (Terminal.get_main_nonlinear "2485876245a") [`y1`;`y2`;`y3`;`y4`;`y5`;`y6`];
  ASM_REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `#3.0 = &3`;];
  ANTS_TAC;
    REWRITE_TAC[arith `x <= y <=> ~(y < x)`];
    DISCH_TAC;
    FIRST_X_ASSUM_ST `&16` MP_TAC THEN REWRITE_TAC[arith `a * b * c = (a * b) * c`];
    REWRITE_TAC[arith `~(&0 < x * y) <=> &0 <= x * (-- y)`];
    REPEAT (GMATCH_SIMP_TAC REAL_LE_MUL_EQ);
    REPEAT (GMATCH_SIMP_TAC REAL_LT_MUL_EQ);
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  REWRITE_TAC[Nonlinear_lemma.dih_x_alt;Sphere.dih_y;LET_THM;GSYM Sphere.delta_y];
  REWRITE_TAC[GSYM Sphere.delta4_y;GSYM Sphere.y_of_x];
  REWRITE_TAC[arith `pi2 + a < pi2 <=> a < &0`];
  DISCH_TAC;
  GMATCH_SIMP_TAC Tskajxy.ATN2_Y_NEG;
  CONJ_TAC;
    BY(FIRST_X_ASSUM MP_TAC THEN REAL_ARITH_TAC);
  BY(REWRITE_TAC[arith `p - a = p + -- a`;GSYM ATN_NEG;arith `-- p + a < &0 <=> a < p`;ATN_BOUNDS])
  ]);;
  (* }}} *)

let quad_nonexist_849 = prove_by_refinement(
  `main_nonlinear_terminal_v11 ==> ~(?(v1:real^3) v2 v3 v4.
      dist(v1,v2) = &2 /\ dist(v2,v3) = &2 /\ dist(v3,v4) = &2 /\ dist(v1,v4)= &2 * h0 /\
      cstab <= dist(v1,v3) /\ cstab <= dist(v2,v4))`,
  (* {{{ proof *)
  [
  REWRITE_TAC[NOT_EXISTS_THM;Sphere.cstab];
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC (Terminal.get_main_nonlinear "8495326405") [`dist(v3,v4)`;`dist(v2,v3)`;`dist(v1,v3)`;`dist(v1,v2)`;`dist(v1,v4)`;`dist(v2,v4)`];
  REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
  DISCH_THEN MP_TAC THEN ANTS_TAC;
    ASM_REWRITE_TAC[arith `x <= x`];
    TYPIFY `!w1 w3 w2. dist(w1,w2) = &2 /\ dist(w2,w3) = &2 ==> dist((w1:real^3),w3) <= &6` ENOUGH_TO_SHOW_TAC;
      BY(DISCH_TAC THEN CONJ_TAC THEN FIRST_X_ASSUM MATCH_MP_TAC THEN ASM_MESON_TAC[DIST_SYM]);
    REPEAT GEN_TAC THEN (INTRO_TAC DIST_TRIANGLE[`w1`;`w2`;`w3`]);
    BY(REAL_ARITH_TAC);
  REWRITE_TAC[arith `~(x < &0) <=> &0 <= x`];
  BY(INTRO_TAC Terminal.DELTA_Y_POS_4POINTS [`v3`;`v4`;`v2`;`v1`] THEN REWRITE_TAC[DIST_SYM])
  ]);;
  (* }}} *)

(* shortest diag < 3.62 *)

let quad_diag_362 = prove_by_refinement(
  `main_nonlinear_terminal_v11 ==> (!(v1:real^3) v2 v3 v4.
      dist(v1,v2) = &2 /\ dist(v2,v3) = cstab /\ dist(v3,v4) = &2 /\ dist(v1,v4)= cstab ==>
      (dist(v1,v3) <= #3.62 \/ dist(v2,v4) <= #3.62))`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Sphere.cstab;arith `x <= y <=> ~(y < x)`;GSYM DE_MORGAN_THM];
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC (Terminal.get_main_nonlinear "2171548893") [`dist(v3,v4)`;`dist(v2,v3)`;`dist(v1,v3)`;`dist(v1,v2)`;`dist(v1,v4)`;`dist(v2,v4)`];
  REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
  DISCH_THEN MP_TAC THEN ANTS_TAC;
    ASM_REWRITE_TAC[arith `x <= x`];
    ASM_SIMP_TAC[arith `x < y ==> x <= y`];
    TYPIFY `!w1 w3 w2. dist(w1,w2) = &2 /\ dist(w2,w3) = #3.01 ==> dist((w1:real^3),w3) <= &6` ENOUGH_TO_SHOW_TAC;
      BY(DISCH_TAC THEN CONJ_TAC THENL [ALL_TAC;ONCE_REWRITE_TAC[DIST_SYM]] THEN FIRST_X_ASSUM MATCH_MP_TAC THEN ASM_MESON_TAC[DIST_SYM]);
    REPEAT GEN_TAC THEN (INTRO_TAC DIST_TRIANGLE[`w1`;`w2`;`w3`]);
    BY(REAL_ARITH_TAC);
  REWRITE_TAC[arith `~(x < &0) <=> &0 <= x`];
  BY(INTRO_TAC Terminal.DELTA_Y_POS_4POINTS [`v3`;`v4`;`v2`;`v1`] THEN REWRITE_TAC[DIST_SYM])
  ]);;
  (* }}} *)


let INV_ARCLENGTH = prove_by_refinement(
  `!y1 y3 z. 
   &0 < z /\ z < pi /\ &0 < y1 /\ &0 < y3 ==>
    &0 < y1 pow 2 + y3 pow 2 - &2 * y1  *y3 * cos z /\
    z = arclength y1 y3 (sqrt (y1 pow 2 + y3 pow 2 - &2 * y1 *y3 * cos z))`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC cos_bounds_0_pi [`z`];
  ASM_REWRITE_TAC[] THEN REPEAT WEAKER_STRIP_TAC;
  SUBCONJ_TAC;
    REWRITE_TAC[arith `y1 pow 2 + y3 pow 2 - &2 * y1 * y3 * cos z = (y1 - y3) pow 2 + &2 * y1 *y3 * (&1 - cos z)`];
    MATCH_MP_TAC (arith `&0 <= a /\ &0 < b ==> &0 < a + b`);
    REWRITE_TAC[ REAL_LE_POW_2];
    REPEAT (GMATCH_SIMP_TAC REAL_LT_MUL_EQ);
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  DISCH_TAC;
  TYPED_ABBREV_TAC `c = sqrt (y1 pow 2 + y3 pow 2 - &2 * y1 * y3 * cos z)`;
  GMATCH_SIMP_TAC Trigonometry1.ACS_ARCLENGTH;
  TYPIFY `c*c = y1 pow 2 + y3 pow 2 - &2 * y1 * y3 * cos z` (C SUBGOAL_THEN ASSUME_TAC);
    EXPAND_TAC "c";
    GMATCH_SIMP_TAC Merge_ineq.sqrtpow2;
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  TYPED_ABBREV_TAC `r = (y1 * y1 + y3 * y3 - c * c) / (&2 * y1 * y3)`;
  CONJ2_TAC;
    TYPIFY `r = cos z` ENOUGH_TO_SHOW_TAC;
      DISCH_THEN SUBST1_TAC;
      GMATCH_SIMP_TAC ACS_COS;
      BY(ASM_TAC THEN REAL_ARITH_TAC);
    EXPAND_TAC "r";
    FIRST_X_ASSUM_ST `c * c = s` (SUBST1_TAC);
    Calc_derivative.CALC_ID_TAC;
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  ASM_REWRITE_TAC[];
  SUBCONJ_TAC;
    EXPAND_TAC "c";
    GMATCH_SIMP_TAC SQRT_POS_LE;
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  DISCH_TAC;
  TYPIFY `c pow 2 <= (y1 + y3) pow 2 /\ (abs(y1 - y3)) pow 2 <= c pow 2` ENOUGH_TO_SHOW_TAC;
    GMATCH_SIMP_TAC (GSYM Trigonometry2.POW2_COND);
    GMATCH_SIMP_TAC (GSYM Trigonometry2.POW2_COND);
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  REWRITE_TAC[REAL_POW2_ABS];
  ASM_REWRITE_TAC[arith `c pow 2  = c * c`];
  TYPIFY `-- &2 * y1 * y3 * cos z <= &2 * y1 * y3 /\ -- &2 * y1 * y3 <= -- &2 * y1 * y3 * cos z` ENOUGH_TO_SHOW_TAC;
    BY(REAL_ARITH_TAC);
  REWRITE_TAC[arith ` -- &2 * y1 * y3 * cos z <= &2 * y1 *y3 <=> &0 <= &2 * y1 * y3 * (&1 + cos z)`;arith `-- &2 * y1 * y3 <= -- &2 * y1 * y3 * cos z <=> &0 <= &2 * y1 * y3 * (&1 - cos z)`];
  GMATCH_SIMP_TAC REAL_LE_MUL;
  REPEAT (GMATCH_SIMP_TAC REAL_LE_MUL);
  BY(ASM_TAC THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let taum_dih_y = prove_by_refinement(
  `!y1 y2 y3 y4 y5 y6.
    taum y1 y2 y3 y4 y5 y6 = 
    rho y1 * dih_y y1 y2 y3 y4 y5 y6 +
      rho y2 * dih_y y2 y3 y1 y5 y6 y4 +
      rho y3 * dih_y y3 y1 y2 y6 y4 y5 - (pi + sol0)`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Sphere.taum;Sphere.sol_y;Sphere.lnazim;Sphere.rho;Nonlinear_lemma.sol0_const1];
  BY(REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let real_continuous_dih_y_wrt4 = prove_by_refinement(
 `!y1 y2 y3 y4 y5 y6.
     &0 < delta_y y1 y2 y3 y4 y5 y6 /\
     &0 < y1 /\
     &0 < ups_x (y1 * y1) (y2 * y2) (y6 * y6) /\
     &0 < ups_x (y1 * y1) (y3 * y3) (y5 * y5)
     ==> (\q. dih_y y1 y2 y3 q y5 y6) real_continuous atreal y4`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC Ocbicby.derived_form_dih_x_wrt_x4 [`y1*y1`;`y2*y2`;`y3*y3`;`y4*y4`;`y5*y5`;`y6*y6`];
  REWRITE_TAC[Calc_derivative.derived_form;Sphere.dih_y;LET_THM];
  REWRITE_TAC[WITHINREAL_UNIV];
  ANTS_TAC;
    ASM_REWRITE_TAC[];
    GMATCH_SIMP_TAC REAL_LT_MUL_EQ;
    BY(ASM_REWRITE_TAC[GSYM Sphere.delta_y]);
  DISCH_TAC;
  TYPIFY `(\q. dih_x (y1 * y1) (y2 * y2) (y3 * y3) (q * q) (y5 * y5) (y6 * y6)) = (\q. dih_x (y1 * y1) (y2 * y2) (y3 * y3) (q) (y5 * y5) (y6 * y6)) o (\t. t pow 2)` (C SUBGOAL_THEN SUBST1_TAC);
    BY(REWRITE_TAC[FUN_EQ_THM;o_THM;arith `x*x = x pow 2`]);
  MATCH_MP_TAC REAL_CONTINUOUS_ATREAL_COMPOSE;
  CONJ_TAC;
    MATCH_MP_TAC REAL_CONTINUOUS_POW;
    BY(REWRITE_TAC[REAL_CONTINUOUS_AT_ID]);
  REWRITE_TAC[arith `y4 pow 2 = y4 * y4`];
  MATCH_MP_TAC HAS_REAL_DERIVATIVE_IMP_CONTINUOUS_ATREAL;
  BY(ASM_MESON_TAC[])
  ]);;
  (* }}} *)

let real_continuous_dih_y_wrt5 = prove_by_refinement(
 `!y1 y2 y3 y4 y5 y6.
     &0 < delta_y y1 y2 y3 y4 y5 y6 /\
     &0 < y1 /\
     &0 < ups_x (y1 * y1) (y2 * y2) (y6 * y6) /\
     &0 < ups_x (y1 * y1) (y3 * y3) (y5 * y5)
     ==> (\q. dih_y y1 y2 y3 y4 q y6) real_continuous atreal y5`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC Ocbicby.derived_form_dih_x_wrt_x5 [`y1*y1`;`y2*y2`;`y3*y3`;`y4*y4`;`y5*y5`;`y6*y6`];
  REWRITE_TAC[Calc_derivative.derived_form;Sphere.dih_y;LET_THM];
  REWRITE_TAC[WITHINREAL_UNIV];
  ANTS_TAC;
    ASM_REWRITE_TAC[];
    GMATCH_SIMP_TAC REAL_LT_MUL_EQ;
    BY(ASM_REWRITE_TAC[GSYM Sphere.delta_y]);
  DISCH_TAC;
  TYPIFY `(\q. dih_x (y1 * y1) (y2 * y2) (y3 * y3) (y4 * y4) (q * q) (y6 * y6)) = (\q. dih_x (y1 * y1) (y2 * y2) (y3 * y3) (y4*y4) (q) (y6 * y6)) o (\t. t pow 2)` (C SUBGOAL_THEN SUBST1_TAC);
    BY(REWRITE_TAC[FUN_EQ_THM;o_THM;arith `x*x = x pow 2`]);
  MATCH_MP_TAC REAL_CONTINUOUS_ATREAL_COMPOSE;
  CONJ_TAC;
    MATCH_MP_TAC REAL_CONTINUOUS_POW;
    BY(REWRITE_TAC[REAL_CONTINUOUS_AT_ID]);
  REWRITE_TAC[arith `y5 pow 2 = y5 * y5`];
  MATCH_MP_TAC HAS_REAL_DERIVATIVE_IMP_CONTINUOUS_ATREAL;
  BY(ASM_MESON_TAC[])
  ]);;
  (* }}} *)

let real_continuous_dih_y_wrt6 = prove_by_refinement(
 `!y1 y2 y3 y4 y5 y6.
     &0 < delta_y y1 y2 y3 y4 y5 y6 /\
     &0 < y1 /\
     &0 < ups_x (y1 * y1) (y2 * y2) (y6 * y6) /\
     &0 < ups_x (y1 * y1) (y3 * y3) (y5 * y5)
     ==> (\q. dih_y y1 y2 y3 y4 y5 q) real_continuous atreal y6`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC Ocbicby.derived_form_dih_x_wrt_x6 [`y1*y1`;`y2*y2`;`y3*y3`;`y4*y4`;`y5*y5`;`y6*y6`];
  REWRITE_TAC[Calc_derivative.derived_form;Sphere.dih_y;LET_THM];
  REWRITE_TAC[WITHINREAL_UNIV];
  ANTS_TAC;
    ASM_REWRITE_TAC[];
    GMATCH_SIMP_TAC REAL_LT_MUL_EQ;
    BY(ASM_REWRITE_TAC[GSYM Sphere.delta_y]);
  DISCH_TAC;
  TYPIFY `(\q. dih_x (y1 * y1) (y2 * y2) (y3 * y3) (y4 * y4) (y5 * y5) (q*q)) = (\q. dih_x (y1 * y1) (y2 * y2) (y3 * y3) (y4*y4) (y5 * y5) (q)) o (\t. t pow 2)` (C SUBGOAL_THEN SUBST1_TAC);
    BY(REWRITE_TAC[FUN_EQ_THM;o_THM;arith `x*x = x pow 2`]);
  MATCH_MP_TAC REAL_CONTINUOUS_ATREAL_COMPOSE;
  CONJ_TAC;
    MATCH_MP_TAC REAL_CONTINUOUS_POW;
    BY(REWRITE_TAC[REAL_CONTINUOUS_AT_ID]);
  REWRITE_TAC[arith `y6 pow 2 = y6 * y6`];
  MATCH_MP_TAC HAS_REAL_DERIVATIVE_IMP_CONTINUOUS_ATREAL;
  BY(ASM_MESON_TAC[])
  ]);;
  (* }}} *)


let real_continuous_taum = prove_by_refinement(
  `!y1 y2 y3 y4 y5 y6. 
         &0 < delta_y y1 y2 y3 y4 y5 y6 /\
         &0 < y1 /\ &0 < y2 /\ &0 < y3 /\
         &0 < ups_x (y1 * y1) (y2 * y2) (y6 * y6) /\
         &0 < ups_x (y2 * y2) (y3 * y3) (y4 * y4) /\
         &0 < ups_x (y1 * y1) (y3 * y3) (y5 * y5) ==>
     (\q. taum y1 y2 y3 q y5 y6) real_continuous atreal y4`,
  (* {{{ proof *)
  [
  REWRITE_TAC[taum_dih_y];
  REPEAT WEAKER_STRIP_TAC;
  TYPIFY `delta_y y3 y1 y2 y6 y4 y5 = delta_y y1 y2 y3 y4 y5 y6 /\ delta_y y2 y3 y1 y5 y6 y4 = delta_y y1 y2 y3 y4 y5 y6` (C SUBGOAL_THEN ASSUME_TAC);
    BY(MESON_TAC[Merge_ineq.delta_y_sym]);
  TYPIFY `ups_x (y3 * y3) (y1 * y1) (y5 * y5) = ups_x (y1 * y1) (y3 * y3) (y5 * y5) /\ ups_x (y3 * y3) (y2 * y2) (y4 * y4) = ups_x (y2 * y2) (y3 * y3) (y4 * y4) /\ ups_x (y2 * y2) (y1 * y1) (y6 * y6) = ups_x (y1 * y1) (y2 * y2) (y6 * y6)` (C SUBGOAL_THEN ASSUME_TAC);
    BY(REWRITE_TAC[Merge_ineq.ups_x_sym]);
  BY((REPEAT (REPEAT CONJ_TAC THEN (MATCH_MP_TAC REAL_CONTINUOUS_ADD ORELSE MATCH_MP_TAC REAL_CONTINUOUS_MUL ORELSE MATCH_MP_TAC REAL_CONTINUOUS_POW ORELSE MATCH_MP_TAC REAL_CONTINUOUS_SUB ORELSE MATCH_MP_TAC real_continuous_dih_y_wrt6 ORELSE MATCH_MP_TAC real_continuous_dih_y_wrt5 ORELSE MATCH_MP_TAC real_continuous_dih_y_wrt4 THEN ASM_REWRITE_TAC[REAL_CONTINUOUS_CONST]))))
  ]);;
  (* }}} *)

let taum_attains_inf = prove_by_refinement(
  `main_work ==> main_nonlinear_terminal_v11 ==> (!y1 y2 y3 y4 y5 y6. &2 <= y1 /\ y1 <= &2 * h0 /\
	&2 <= y2 /\ y2 <= &2 * h0 /\
	&2 <= y3 /\ y3 <= &2 * h0 /\
	&2 <= y4 /\ y4 <= &2 * h0 /\
	&2 <= y5 /\ y5 <= &2 * h0 /\
	&2 <= y6 /\ y6 <= &2 * h0 
	==>
	(?y4'. &2 <= y4' /\ y4' <= &2 * h0 /\ (!y4. &2 <= y4 /\ y4 <= &2 * h0 ==>
	   taum y1 y2 y3 y4' y5 y6 <= taum y1 y2 y3 y4 y5 y6)))`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC REAL_CONTINUOUS_ATTAINS_INF [`\q. taum y1 y2 y3 q y5 y6`;`real_interval [&2, &2 * h0]`];
  ANTS_TAC;
    REWRITE_TAC[REAL_COMPACT_INTERVAL];
    CONJ_TAC;
      REWRITE_TAC[EXTENSION;NOT_IN_EMPTY;IN_REAL_INTERVAL;NOT_FORALL_THM;Sphere.h0];
      TYPIFY `&2` EXISTS_TAC;
      BY(REAL_ARITH_TAC);
    REWRITE_TAC[REAL_CONTINUOUS_ON_EQ_CONTINUOUS_WITHIN];
    REWRITE_TAC[IN_REAL_INTERVAL];
    REPEAT WEAKER_STRIP_TAC;
    MATCH_MP_TAC REAL_CONTINUOUS_ATREAL_WITHINREAL;
    MATCH_MP_TAC real_continuous_taum;
    ASM_SIMP_TAC[arith `&2 <= y ==> &0 < y`];
    CONJ_TAC;
      INTRO_TAC (get_work "4717061266") [`y1`;`y2`;`y3`;`x`;`y5`;`y6`];
      REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
      REWRITE_TAC[arith `x > &0 <=> &0 < x`] THEN DISCH_THEN MATCH_MP_TAC;
      BY(ASM_REWRITE_TAC[]);
    BY(REPEAT CONJ_TAC THEN MATCH_MP_TAC Ysskqoy.TRI_UPS_X_STRICT_POS THEN ASM_TAC THEN REWRITE_TAC[Sphere.h0] THEN REAL_ARITH_TAC);
  REWRITE_TAC[IN_REAL_INTERVAL];
  REPEAT WEAKER_STRIP_TAC;
  TYPIFY `x` EXISTS_TAC;
  BY(ASM_MESON_TAC[])
  ]);;
  (* }}} *)

let UPS_X_STD_POS = prove_by_refinement(
  `!y1 y2 y3. 	&2 <= y1 /\ y1 <= &2 * h0 /\
	&2 <= y2 /\ y2 <= &2 * h0 /\
	&2 <= y3 /\ y3 <= &2 * h0 ==> &0 < ups_x (y1 * y1) (y2 * y2) (y3 * y3)`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  BY(MATCH_MP_TAC Ysskqoy.TRI_UPS_X_STRICT_POS THEN ASM_TAC THEN REWRITE_TAC[Sphere.h0] THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let TAUM_EXTREMAL = prove_by_refinement(
  `main_work ==> main_nonlinear_terminal_v11 ==> (!y1 y2 y3 y4 y5 y6.
	&2 <= y1 /\ y1 <= &2 * h0 /\
	&2 <= y2 /\ y2 <= &2 * h0 /\
	&2 <= y3 /\ y3 <= &2 * h0 /\
	&2 <= y4 /\ y4 <= &2 * h0 /\
	&2 <= y5 /\ y5 <= &2 * h0 /\
	&2 <= y6 /\ y6 <= &2 * h0 
	==>
	(?y4'. (y4' = &2 * h0 \/ y4' = &2) /\
	   taum y1 y2 y3 y4' y5 y6 <= taum y1 y2 y3 y4 y5 y6)
				  )`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC (UNDISCH2 taum_attains_inf) [`y1`;`y2`;`y3`;`y4`;`y5`;`y6`];
  ASM_REWRITE_TAC[];
  REPEAT WEAKER_STRIP_TAC;
  TYPIFY `y4'` EXISTS_TAC;
  CONJ2_TAC;
    FIRST_X_ASSUM MATCH_MP_TAC;
    BY(ASM_REWRITE_TAC[]);
  PROOF_BY_CONTR_TAC;
  TYPIFY `&2 < y4' /\ y4' < &2 * h0` (C SUBGOAL_THEN ASSUME_TAC);
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  INTRO_TAC (UNDISCH Ocbicby.LEMMA_1834976363) [`y1`;`y2`;`y3`;`y4'`;`y5`;`y6`;`&2`;`&2 * h0`];
  ANTS_TAC;
    ASM_REWRITE_TAC[SUBSET;IN_REAL_INTERVAL;IN_ELIM_THM];
    ASM_SIMP_TAC [arith `&2 <= y ==> &0 < y`];
    INTRO_TAC Ocbicby.xrr_simple_lower_bound [`y2`;`y3`;`y4'`;`&2`];
    INTRO_TAC Ocbicby.xrr_simple_lower_bound [`y1`;`y3`;`y5`;`&2`];
    INTRO_TAC Ocbicby.xrr_simple_lower_bound [`y1`;`y2`;`y6`;`&2`];
    ASM_REWRITE_TAC[arith `x <= x`];
    REPEAT WEAKER_STRIP_TAC THEN ASM_REWRITE_TAC[];
    CONJ_TAC;
      INTRO_TAC Ocbicby.xrr_simple_upper_bound [`y2`;`y3`;`y4'`;`&2 * h0`];
      ANTS_TAC;
        BY(ASM_REWRITE_TAC[] THEN REWRITE_TAC[Sphere.h0] THEN REAL_ARITH_TAC);
      BY(REWRITE_TAC[Sphere.h0] THEN REAL_ARITH_TAC);
    CONJ2_TAC;
      BY(CONJ_TAC THEN MATCH_MP_TAC UPS_X_STD_POS THEN ASM_REWRITE_TAC[]);
    REPEAT WEAKER_STRIP_TAC;
    ASM_SIMP_TAC[arith `&2 < x ==> &0 < x`];
    CONJ2_TAC;
      MATCH_MP_TAC UPS_X_STD_POS THEN ASM_REWRITE_TAC[];
      BY(ASM_TAC THEN REAL_ARITH_TAC);
    INTRO_TAC (get_work "4717061266") [`y1`;`y2`;`y3`;`x`;`y5`;`y6`];
    REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
    REWRITE_TAC[arith `x > &0 <=> &0 < x`] THEN DISCH_THEN MATCH_MP_TAC;
    (ASM_REWRITE_TAC[]);
    BY(REPLICATE_TAC 2 (POP_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  REWRITE_TAC[IN_REAL_INTERVAL];
  REPEAT WEAKER_STRIP_TAC;
  FIRST_X_ASSUM (C INTRO_TAC [`y4''`]);
  ANTS_TAC;
    BY(ASM_TAC THEN REAL_ARITH_TAC);
  BY(ASM_REWRITE_TAC[arith `~(x <= y) <=> (y < x)`])
  ]);;
  (* }}} *)

let TAUM_STD_POS = prove_by_refinement(
  `main_work ==> main_nonlinear_terminal_v11 ==> (!y1 y2 y3 y4 y5 y6.
	&2 <= y1 /\ y1 <= &2 * h0 /\
	&2 <= y2 /\ y2 <= &2 * h0 /\
	&2 <= y3 /\ y3 <= &2 * h0 /\
	&2 <= y4 /\ y4 <= &2 * h0 /\
	&2 <= y5 /\ y5 <= &2 * h0 /\
	&2 <= y6 /\ y6 <= &2 * h0 
	==>
	(&0 <= taum y1 y2 y3 y4 y5 y6))`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  REWRITE_TAC[arith `x <= y <=> ~(y < x)`];
  DISCH_TAC;
  TYPIFY `&2 * h0 <= sqrt8 /\ #2.52 = &2 * h0 /\ &2 <= &2 * h0` (C SUBGOAL_THEN ASSUME_TAC);
    BY(REWRITE_TAC[Sphere.h0] THEN MP_TAC Flyspeck_constants.bounds THEN REAL_ARITH_TAC);
  COMMENT "y4";
  INTRO_TAC (UNDISCH2 TAUM_EXTREMAL) [`y1`;`y2`;`y3`;`y4`;`y5`;`y6`];
  ASM_REWRITE_TAC[];
  REPEAT WEAKER_STRIP_TAC;
  FIRST_X_ASSUM (DISJ_CASES_TAC);
    REPEAT (FIRST_X_ASSUM_ST `taum` MP_TAC) THEN ASM_REWRITE_TAC[] THEN REPEAT WEAKER_STRIP_TAC;
    INTRO_TAC ( Terminal.get_main_nonlinear "5541487347") [`y2`;`y3`;`y1`;`y5`;`y6`;`y4'`];
    REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
    DISCH_THEN MP_TAC THEN ANTS_TAC;
      BY(ASM_REWRITE_TAC[arith `x <= x`]);
    TYPIFY `taum y2 y3 y1 y5 y6 y4' = taum y1 y2 y3 y4' y5 y6` (C SUBGOAL_THEN SUBST1_TAC);
      BY(MESON_TAC[Terminal.taum_sym]);
    ASM_REWRITE_TAC[];
    BY(REPLICATE_TAC 3 (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  TYPIFY `taum y1 y2 y3 (&2) y5 y6 < &0` (C SUBGOAL_THEN ASSUME_TAC);
    BY(FIRST_X_ASSUM_ST `taum` MP_TAC THEN ASM_REWRITE_TAC[] THEN ASM_TAC THEN REAL_ARITH_TAC);
  FIRST_X_ASSUM MP_TAC;
  POP_ASSUM kill;
  REPEAT (FIRST_X_ASSUM_ST `taum` kill);
  DISCH_TAC;
  COMMENT "y5";
  INTRO_TAC (UNDISCH2 TAUM_EXTREMAL) [`y2`;`y3`;`y1`;`y5`;`y6`;`(&2)`];
  ASM_REWRITE_TAC[arith `x <= x`];
  REPEAT WEAKER_STRIP_TAC;
  TYPIFY `taum y1 y2 y3 (&2) y5 y6 = taum y2 y3 y1 y5 y6 (&2) /\ taum y3 y1 y2 y6 (&2) y4' = taum y2 y3 y1 y4' y6 (&2)` (C SUBGOAL_THEN ASSUME_TAC);
    BY(MESON_TAC[Terminal.taum_sym]);
  FIRST_X_ASSUM (DISJ_CASES_TAC);
    REPEAT (FIRST_X_ASSUM_ST `taum` MP_TAC) THEN ASM_REWRITE_TAC[] THEN REPEAT WEAKER_STRIP_TAC;
    INTRO_TAC ( Terminal.get_main_nonlinear "5541487347") [`y3`;`y1`;`y2`;`y6`;`&2`;`y4'`];
    REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
    DISCH_THEN MP_TAC THEN ANTS_TAC;
      BY(ASM_REWRITE_TAC[arith `x <= x`]);
    ASM_REWRITE_TAC[];
    BY(REPLICATE_TAC 4 (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  TYPIFY `taum y1 y2 y3 (&2) (&2) y6 < &0` (C SUBGOAL_THEN ASSUME_TAC);
    TYPIFY `taum y1 y2 y3 (&2) (&2) y6 = taum  y2 y3 y1 (&2) y6 (&2)` (C SUBGOAL_THEN SUBST1_TAC);
      BY(MESON_TAC[Terminal.taum_sym]);
    BY(REPEAT (FIRST_X_ASSUM_ST `taum` MP_TAC) THEN ASM_REWRITE_TAC[] THEN ASM_TAC THEN REAL_ARITH_TAC);
  FIRST_X_ASSUM MP_TAC;
  POP_ASSUM kill;
  REPEAT (FIRST_X_ASSUM_ST `taum` kill);
  DISCH_TAC;
  COMMENT "y6";
  INTRO_TAC (UNDISCH2 TAUM_EXTREMAL) [`y3`;`y1`;`y2`;`y6`;`(&2)`;`(&2)`];
  ASM_REWRITE_TAC[arith `x <= x`];
  REPEAT WEAKER_STRIP_TAC;
  POP_ASSUM MP_TAC;
  TYPIFY_GOAL_THEN `!z1 z2 z3. taum y3 y1 y2 z3 z1 z2 = taum y1 y2 y3 z1 z2 z3` (unlist REWRITE_TAC);
    BY(MESON_TAC[Terminal.taum_sym]);
  DISCH_TAC;
  FIRST_X_ASSUM (DISJ_CASES_TAC);
    REPEAT (FIRST_X_ASSUM_ST `taum` MP_TAC) THEN ASM_REWRITE_TAC[] THEN REPEAT WEAKER_STRIP_TAC;
    INTRO_TAC ( Terminal.get_main_nonlinear "5541487347") [`y1`;`y2`;`y3`;`&2`;`&2`;`&2 * h0`];
    REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
    DISCH_THEN MP_TAC THEN ANTS_TAC;
      BY(ASM_REWRITE_TAC[arith `x <= x`]);
    BY(REPLICATE_TAC 4 (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  TYPIFY `taum y1 y2 y3 (&2) (&2) (&2) < &0` (C SUBGOAL_THEN ASSUME_TAC);
    BY(REPEAT (FIRST_X_ASSUM_ST `taum` MP_TAC) THEN ASM_REWRITE_TAC[] THEN ASM_TAC THEN REAL_ARITH_TAC);
  INTRO_TAC ( Terminal.get_main_nonlinear "OMKYNLT 3336871894") [`y1`;`y2`;`y3`;`&2`;`&2`;`&2`];
  REWRITE_TAC[Sphere.ineq;TAUT `a ==> b ==> c <=> a /\ b ==> c`;arith `x <= x`];
  ASM_REWRITE_TAC[arith `#2.0 = &2`];
  BY(POP_ASSUM MP_TAC THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)



let LFLACKU = prove_by_refinement(
  `main_work ==> main_nonlinear_terminal_v11 ==>
   scs_arrow_v39 {scs_3I1} {}`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  REPEAT (GMATCH_SIMP_TAC Ocbicby.pos_imp_scs_arrow_empty);
  REWRITE_TAC[Appendix.scs_3I1;Appendix.mk_unadorned_v39];
  MATCH_MP_TAC (REWRITE_RULE[LET_DEF;LET_END_DEF] Terminal.taustar_taum_dfun);
  REWRITE_TAC[Terminal.FUNLIST_EXPLICIT];
  REWRITE_TAC[Appendix.is_ear_v39;Appendix.scs_v39_explicit;EMPTY_GSPEC;NOT_INSERT_EMPTY];
  REWRITE_TAC[Appendix.d_tame];
  REWRITE_TAC[Appendix.cs_adj;Terminal.FUNLIST_EXPLICIT];
  NUM_REDUCE_TAC;
  REWRITE_TAC[arith `x <= x`];
  TYPIFY_GOAL_THEN `&2 * h0 <= #3.62` (unlist REWRITE_TAC);
    BY(REWRITE_TAC[Sphere.h0] THEN REAL_ARITH_TAC);
  REPEAT WEAKER_STRIP_TAC;
  REWRITE_TAC[arith `&0 +  #0.1 * -- &1 * (&0 + &0 + &0 + &0) = &0`];
  MATCH_MP_TAC (UNDISCH2 TAUM_STD_POS);
  BY(ASM_REWRITE_TAC[])
  ]);;
  (* }}} *)

let SCS_ARROW_TRANS_SING = prove_by_refinement(
  `!a S. scs_arrow_v39 {a} S /\ (!x. x IN S ==> scs_arrow_v39 {x} {}) ==> scs_arrow_v39 {a} {}`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Appendix.scs_arrow_v39;NOT_IN_EMPTY;IN_SING];
  BY(MESON_TAC[])
  ]);;
  (* }}} *)

let s_init_list_alt = prove_by_refinement(
  `s_init_list_v39 = [scs_6I1;scs_5I1;scs_4I1;scs_3I1;scs_5I2;scs_4I2;scs_5I3;scs_4I3]`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Appendix.s_init_list_v39];
  REPEAT LET_TAC;
  ASM_REWRITE_TAC[CONS_11;Appendix.scs_6I1;Appendix.scs_5I1;Appendix.scs_4I1;Appendix.scs_3T1;Appendix.scs_5I2;Appendix.scs_5I3;Appendix.scs_4I3;Appendix.scs_3I1;Appendix.scs_4I2];
  TYPIFY `(!a1 a2 a3. (a_pro 5 a1 a2 a3 = (funlist_v39   [(0,1),a1; (0,2),a3; (0,3),a3; (1,3),a3; (1,4),a3;   (2,4),  a3]  a2 5))) /\ (!a1 a2 a3. a_pro 4 a1 a2 a3  = (funlist_v39 [(0,1),a1; (0,2),a3; (1,3),a3] a2 4))` ENOUGH_TO_SHOW_TAC;
    BY(DISCH_THEN (unlist REWRITE_TAC));
  CONJ_TAC;
    REPEAT WEAKER_STRIP_TAC;
    REWRITE_TAC[FUN_EQ_THM];
    MATCH_MP_TAC Terminal.periodic2_mod_reduce;
    TYPIFY `5` EXISTS_TAC;
    REWRITE_TAC[arith `~(5 =0)`];
    CONJ_TAC;
      REWRITE_TAC[Appendix.periodic2];
      EXPAND_TAC "a_pro";
      ASM_REWRITE_TAC[Oxl_2012.MOD_ADD_CANCEL;arith `SUC (j+5) = (SUC j) + 5`];
      BY(MESON_TAC[Terminal.periodic2_funlist;Appendix.periodic2]);
    REWRITE_TAC[arith `x < 5 <=> x = 0 \/ x = 1 \/ x = 2 \/ x = 3 \/ x = 4`];
    REWRITE_TAC[TAUT ` ((a \/ b) /\ c) <=> ((a /\ c) \/ (b /\ c))`;TAUT `(a /\ (b \/ c)) <=> ((a /\ b) \/ (a /\ c))`];
    EXPAND_TAC "a_pro";
    REWRITE_TAC[Geomdetail.PAIR_EQ_EXPAND];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[] THEN NUM_REDUCE_TAC THEN ASM_REWRITE_TAC[Terminal.FUNLIST_EXPLICIT;Hexagons.PSORT_5_EXPLICIT] THEN NUM_REDUCE_TAC);
  REPEAT WEAKER_STRIP_TAC;
  REWRITE_TAC[FUN_EQ_THM];
  MATCH_MP_TAC Terminal.periodic2_mod_reduce;
  TYPIFY `4` EXISTS_TAC;
  REWRITE_TAC[arith `~(4 =0)`];
  CONJ_TAC;
    REWRITE_TAC[Appendix.periodic2];
    EXPAND_TAC "a_pro";
    ASM_REWRITE_TAC[Oxl_2012.MOD_ADD_CANCEL;arith `SUC (j+4) = (SUC j) + 4`];
    BY(MESON_TAC[Terminal.periodic2_funlist;Appendix.periodic2]);
  REWRITE_TAC[arith `x < 4 <=> x = 0 \/ x = 1 \/ x = 2 \/ x = 3`];
  REWRITE_TAC[TAUT ` ((a \/ b) /\ c) <=> ((a /\ c) \/ (b /\ c))`;TAUT `(a /\ (b \/ c)) <=> ((a /\ b) \/ (a /\ c))`];
  EXPAND_TAC "a_pro";
  REWRITE_TAC[Geomdetail.PAIR_EQ_EXPAND];
  BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[] THEN NUM_REDUCE_TAC THEN ASM_REWRITE_TAC[Terminal.FUNLIST_EXPLICIT] THEN NUM_REDUCE_TAC)
  ]);;
  (* }}} *)


let JEJTVGB_case_breakdown = prove_by_refinement(
  `main_work ==> main_nonlinear_terminal_v11 ==> (
  scs_arrow_v39 { scs_6I1 } { scs_6T1, scs_5M1, scs_4M2, scs_3T1 } /\ // OEHDBEN
    scs_arrow_v39 { scs_5I1 } { scs_stab_diag_v39 scs_5I1 0 2 , scs_5M2 } /\ // OTMTOTJ1
scs_arrow_v39 { scs_5I2 } { scs_stab_diag_v39 scs_5I2 0 2 , scs_5M2 } /\ // 

scs_arrow_v39 { scs_5I3 } { scs_stab_diag_v39 scs_5M1 0 2 , 
						    scs_stab_diag_v39 scs_5M1 0 3, scs_stab_diag_v39 scs_5M1 2 4, scs_5M2 } /\ //  OTMTOTJ3

scs_arrow_v39 { scs_5M1 } { scs_stab_diag_v39 scs_5M1 0 2 , 
						    scs_stab_diag_v39 scs_5M1 0 3, scs_stab_diag_v39 scs_5M1 2 4, scs_5M2 } /\ // OTMTOTJ4

scs_arrow_v39 { scs_5M2 } { scs_3T1,scs_3T4,scs_4M6',scs_4M7,scs_4M8,
						   scs_5T1, scs_stab_diag_v39 scs_5I2 0 2 , 
						   scs_stab_diag_v39 scs_5M1 0 2 ,     scs_stab_diag_v39 scs_5M1 0 3, scs_stab_diag_v39 scs_5M1 2 4 } /\ // HIJQAHA

scs_arrow_v39 { scs_stab_diag_v39 scs_5I1 0 2 }
    {scs_4M2, scs_3M1 } /\ //  CNICGSF1 ..

scs_arrow_v39 { scs_stab_diag_v39 scs_5I2 0 2 }
    {scs_4M3', scs_3T1 } /\ //  CNICGSF2

scs_arrow_v39 { scs_stab_diag_v39 scs_5M1 0 2 }
    {scs_4M2, scs_3T4 } /\ //  CNICGSF3

scs_arrow_v39 { scs_stab_diag_v39 scs_5M1 0 3 }
    {scs_4M4', scs_3T5 } /\ //  CNICGSF4

scs_arrow_v39 { scs_stab_diag_v39 scs_5M1 2 4 }
    {scs_4M5', scs_3T5 } /\ //  CNICGSF5 ..

  scs_arrow_v39 { scs_4I1 } {scs_4I2, scs_stab_diag_v39 scs_4I1 0 2 } /\ // FYSSVEV


  scs_arrow_v39 { scs_4I2 } { scs_4T1, scs_4T2 } /\ // ARDBZYE


  scs_arrow_v39 { scs_stab_diag_v39 scs_4I1 0 2 } { scs_3T1 } /\ // AUEAHEH

  // scs_arrow_v39 { scs_stab_diag_v39 scs_4I3 0 2 } { scs_4T4 } /\ // ZNLLLDL (internal)

  scs_arrow_v39 { scs_4I3 } { scs_4T4, scs_4M6' } /\ // VQFYMZY ..

//  scs_arrow_v39 { scs_4M1 } { scs_4M2, scs_4M6' } /\ // unused.

  scs_arrow_v39 { scs_4M2 } { scs_3T5, scs_3T4, scs_4M6' } /\ //  BNAWVNH

  scs_arrow_v39 { scs_4M3' } { scs_3T1, scs_3T6', scs_4M6' } /\ // RAWZDIB

  scs_arrow_v39 { scs_4M4' } { scs_3T5, scs_3T3, scs_4M7 } /\ //  MFKLVDK

  scs_arrow_v39 { scs_4M5' } { scs_3T4, scs_4M8 } /\ // RYPDIXT

scs_arrow_v39 {scs_4M6'} {scs_4T3,scs_4T5} /\ // NWDGKXH

scs_arrow_v39 {scs_4M7} {scs_3T1,scs_3T3,scs_3T4} /\ // YOBIMPP

scs_arrow_v39 {scs_4M8} {scs_4M6',scs_3T7,scs_3T4} /\ // MIQMCSN

    scs_arrow_v39 {scs_3M1} {scs_3T1,scs_3T5} // BKOSSGE

==> 
JEJTVGB_assume_v39)
`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  INTRO_TAC (UNDISCH Ocbicby.OCBICBY) [];
  REPEAT WEAKER_STRIP_TAC;
  TYPIFY `scs_arrow_v39 {scs_3M1} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T1, scs_3T5}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M6'} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4T3, scs_4T5}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M7} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T1, scs_3T3, scs_3T4}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M8} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4M6', scs_3T7, scs_3T4}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M5'} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T4, scs_4M8}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M4'} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T5, scs_3T3, scs_4M7}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M3'} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T1, scs_3T6', scs_4M6'}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4M2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T5, scs_3T4, scs_4M6'}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4I3} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4T4, scs_4M6'}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_stab_diag_v39 scs_4I1 0 2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_3T1}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4I2}  {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4T1, scs_4T2}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_4I1}  {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4I2, scs_stab_diag_v39 scs_4I1 0 2}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_stab_diag_v39 scs_5M1 2 4}   {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4M5', scs_3T5}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_stab_diag_v39 scs_5M1 0 3}   {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4M4', scs_3T5}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_stab_diag_v39 scs_5M1 0 2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY ` {scs_4M2, scs_3T4}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_stab_diag_v39 scs_5I2 0 2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_4M3', scs_3T1}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_stab_diag_v39 scs_5I1 0 2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY ` {scs_4M2, scs_3M1}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_5M2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `      {scs_3T1, scs_3T4, scs_4M6', scs_4M7, scs_4M8, scs_5T1, scs_stab_diag_v39                                                              scs_5I2                                                              0                                                              2, scs_stab_diag_v39                                                                 scs_5M1                                                                 0                                                                 2, scs_stab_diag_v39                                                                    scs_5M1                                                                    0                                                                    3,       scs_stab_diag_v39 scs_5M1 2      4}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_5M1} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_stab_diag_v39 scs_5M1 0 2, scs_stab_diag_v39 scs_5M1 0 3,       scs_stab_diag_v39 scs_5M1 2      4, scs_5M2}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_5I3} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_stab_diag_v39 scs_5M1 0 2, scs_stab_diag_v39 scs_5M1 0 3,       scs_stab_diag_v39 scs_5M1 2      4, scs_5M2}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_5I2} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_stab_diag_v39 scs_5I2 0 2, scs_5M2}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_5I1} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY ` {scs_stab_diag_v39 scs_5I1 0 2, scs_5M2}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  TYPIFY `scs_arrow_v39 {scs_6I1} {}` (C SUBGOAL_THEN ASSUME_TAC);
    MATCH_MP_TAC SCS_ARROW_TRANS_SING;
    TYPIFY `{scs_6T1, scs_5M1, scs_4M2, scs_3T1}` EXISTS_TAC;
    ASM_REWRITE_TAC[IN_INSERT;NOT_IN_EMPTY];
    BY(REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[]);
  COMMENT "final kill";
  MATCH_MP_TAC Ayqjtmd.EAPGLE;
  REWRITE_TAC[GSYM Ocbicby.scs_arrow_sing_empty];
  REWRITE_TAC[s_init_list_alt;MEM];
  REPEAT STRIP_TAC THEN ASM_REWRITE_TAC[];
  BY(REWRITE_TAC[UNDISCH2 LFLACKU])
  ]);;
  (* }}} *)
