open Counting_spheres;;
open Hales_tactic;;

let DLWCHEM_concl = `!V. packing V /\ packing_ineq_def_a /\
  V SUBSET ball_annulus /\ ~(lmfun_ineq_center V) ==>
   (CARD V = 13 \/ CARD V = 14 \/ CARD V = 15)`;;


let XULJEPR_concl = `!V. packing V /\ V SUBSET ball_annulus /\ packing_ineq_def_a /\
 (?v.  v IN V /\ norm (v) = &2 /\ (!u.  (u IN V) /\ ~(u = v) ==> &2 * h0 <= dist(u,v) ))
==> (lmfun_ineq_center V)`;;


(* ========================================================================== *)
(* COMPLETED LEMMAS  *)
(* ========================================================================== *)


let AZIM_SUM_LE = prove_by_refinement(
  `!x y z w1 w2 w3.
    ~(collinear {x,y,z}) /\ ~(collinear {x,y,w1}) /\ ~(collinear {x,y,w2}) /\
   ~(collinear {x,y,w3}) /\
    azim x y z w1 <= azim x y z w2 /\
    azim x y z w2 <= azim x y z w3 ==>
   (azim x y w1 w3 = azim x y w1 w2 + azim x y w2 w3)`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  SUBGOAL_THEN `azim x y z w3 = azim x y z w1 + azim x y w1 w3` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    ASM_REWRITE_TAC[];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  SUBGOAL_THEN `azim x y z w2 = azim x y z w1 + azim x y w1 w2` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    BY(ASM_REWRITE_TAC[]);
  SUBGOAL_THEN `azim x y z w3 = azim x y z w2 + azim x y w2 w3` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    BY(ASM_REWRITE_TAC[]);
  BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let AZIM_NN = prove_by_refinement(
  `!x y z u. &0 <= azim x y z u`,
  (* {{{ proof *)
  [
  MESON_TAC[azim]
  ]);;
  (* }}} *)


let AZIM_BASE_SHIFT_LT = prove_by_refinement(
  `!x y z z' w1 w2 w3.
    ~(collinear {x,y,z}) /\ ~(collinear {x,y,z'}) /\ ~(collinear {x,y,w1}) /\
    ~(collinear {x,y,w2}) /\ ~(collinear {x,y,w3}) /\
    azim x y z w1 < azim x y z w2 /\
    azim x y z w2 < azim x y z w3 /\
    azim x y z' w1 < azim x y z' w3 ==>
   (azim x y z' w1 < azim x y z' w2 /\ azim x y z' w2 < azim x y z' w3)
`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  SUBGOAL_THEN `azim x y w1 w3 = azim x y w1 w2 + azim x y w2 w3` ASSUME_TAC;
    MATCH_MP_TAC AZIM_SUM_LE;
    EXISTS_TAC `z:real^3`;
    ASM_REWRITE_TAC[];
    BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  REWRITE_TAC[arith `a < b <=> ~(b <= a)`];
  CONJ_TAC THEN WEAK_STRIP_TAC;
    SUBGOAL_THEN `azim x y w2 w3 = azim x y w2 w1 + azim x y w1 w3` ASSUME_TAC;
      MATCH_MP_TAC AZIM_SUM_LE;
      EXISTS_TAC `z':real^3`;
      ASM_REWRITE_TAC[];
      BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
    SUBGOAL_THEN `azim x y w1 w2 = &0 /\ azim x y w2 w1 = &0` (MP_TAC);
      BY(ASM_MESON_TAC[AZIM_NN;arith `a = b + c /\ c = e + a /\ &0 <= b /\ &0 <= e ==> (b = &0 /\ e = &0)`]);
    STRIP_TAC;
    SUBGOAL_THEN `azim x y z w2 = azim x y z w1 + azim x y w1 w2` ASSUME_TAC;
      MATCH_MP_TAC Fan.sum4_azim_fan;
      ASM_REWRITE_TAC[];
      BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  COMMENT "1 left";
  SUBGOAL_THEN `azim x y w1 w2 = azim x y w1 w3 + azim x y w3 w2` ASSUME_TAC;
    MATCH_MP_TAC AZIM_SUM_LE;
    EXISTS_TAC `z':real^3`;
    ASM_REWRITE_TAC[];
    BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  SUBGOAL_THEN `azim x y w2 w3 = &0 /\ azim x y w3 w2 = &0` (MP_TAC);
    BY(ASM_MESON_TAC[AZIM_NN;arith `a = b + c /\ b = a + c' /\ &0 <= c /\ &0 <= c' ==> (c = &0 /\ c' = &0)`]);
  STRIP_TAC;
  SUBGOAL_THEN `azim x y z w3 = azim x y z w2 + azim x y w2 w3` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    ASM_REWRITE_TAC[];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)


let AZIM_COMP_LT = prove_by_refinement(
  `!x y z u v. &0 < azim x y z u /\  azim x y z u < azim x y z v ==>
     azim x y v z < azim x y u z `,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  ONCE_REWRITE_TAC[Rogers.AZIM_COMPL_EXT];
  BY(REPEAT COND_CASES_TAC THEN (REPEAT (FIRST_X_ASSUM MP_TAC)) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let AZIM_COMP_LE = prove_by_refinement(
  `!x y z u v. &0 < azim x y z u /\  azim x y z u <= azim x y z v ==>
     azim x y v z <= azim x y u z `,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  ONCE_REWRITE_TAC[Rogers.AZIM_COMPL_EXT];
  BY(REPEAT COND_CASES_TAC THEN (REPEAT (FIRST_X_ASSUM MP_TAC)) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let AZIM_COMP2_LE = prove_by_refinement(
  `!x y z u v. &0 < azim x y u z /\  &0 < azim x y v z /\ azim x y u z <= azim x y v z ==>
     azim x y z v <= azim x y z u `,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  ONCE_REWRITE_TAC[Rogers.AZIM_COMPL_EXT];
  BY(REPEAT COND_CASES_TAC THEN (REPEAT (FIRST_X_ASSUM MP_TAC)) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let AZIM_COMP2_LT = prove_by_refinement(
  `!x y z u v. &0 < azim x y u z /\  &0 < azim x y v z /\ azim x y u z < azim x y v z ==>
     azim x y z v < azim x y z u `,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  ONCE_REWRITE_TAC[Rogers.AZIM_COMPL_EXT];
  BY(REPEAT COND_CASES_TAC THEN (REPEAT (FIRST_X_ASSUM MP_TAC)) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let WEDGE_ORDER_DISJOINT = prove_by_refinement(
  `!x y z n g.
     ~(collinear {x,y,z}) /\
    (!i. i IN 1..n ==> ~(collinear {x,y, g i})) /\
     (g (n+1) = g 1) /\
        (!j k. j IN 1..n /\ k IN 1..n /\ (j < k) ==>  
	  azim x y z (g j) < azim x y z (g k))
    ==>
    (!j k. j IN 1..n /\ k IN 1..n /\ ~(j = k) ==>
       wedge x y (g j) (g (j+1)) INTER wedge x y (g k) (g (k+1)) = {})
	 `,
  (* {{{ proof *)
  [
  REPEAT GEN_TAC;
  DISCH_TAC;
  MATCH_MP_TAC WLOG_LT;
  REWRITE_TAC[];
  CONJ_TAC;
    BY(SET_TAC[]);
  GEN_TAC;
  X_GEN_TAC `k:num`;
  FIRST_X_ASSUM MP_TAC;
  REPEAT WEAK_STRIP_TAC;
  REWRITE_TAC[FUN_EQ_THM];
  X_GEN_TAC `p:real^3`;
  REWRITE_TAC[INTER;IN_ELIM_THM;wedge;X_IN NOT_IN_EMPTY];
  REPEAT WEAK_STRIP_TAC;
  SUBGOAL_THEN `j + 1 IN 1..n` ASSUME_TAC;
    REPEAT (FIRST_X_ASSUM MP_TAC) THEN REWRITE_TAC[IN_NUMSEG];
    BY(ARITH_TAC);
  SUBGOAL_THEN `(azim x y z (g (j:num)) < azim x y z p)` ASSUME_TAC;
    REWRITE_TAC [arith `a <  b <=> ~(b <= a)`];
    WEAK_STRIP_TAC;
    (fun (asl,w) -> (MP_TAC (SPECL ( envl (asl,w)[`x`;`y`;`g j`;`z`;`g j`;`p`;`g (j+1)` ]) AZIM_BASE_SHIFT_LT)) (asl,w));
    ASM_SIMP_TAC[AZIM_REFL;arith `j < j+1`];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  COMMENT "A";
  SUBGOAL_THEN `azim x y z p < azim x y z (g (j+1))` ASSUME_TAC;
    REWRITE_TAC[arith `a < b <=> ~(b <= a)`];
    WEAK_STRIP_TAC;
    (fun (asl,w) -> (MP_TAC (SPECL ( envl (asl,w)[`x`;`y`;`g j`;`z`;`g j`;`p`;`g (j+1)` ]) AZIM_BASE_SHIFT_LT)) (asl,w));
    ASM_SIMP_TAC[AZIM_REFL;arith `j < j+1`];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  COMMENT "B";
  SUBGOAL_THEN `k = (n:num) /\ 1 IN 1..n` ASSUME_TAC;
    MATCH_MP_TAC (prove (`k IN 1..n /\ ~(k+1 IN 1..n) ==> ((k = n) /\ 1 IN 1..n)`, REWRITE_TAC [ IN_NUMSEG ] THEN ARITH_TAC ));
    ASM_SIMP_TAC[];
    WEAK_STRIP_TAC;
    FIRST_ASSUM (MP_TAC o (SPECL[`k:num`;`k+1`]));
    FIRST_X_ASSUM (MP_TAC o (SPECL[`j+1`;`k:num`]));
    ASM_SIMP_TAC[arith `k < k+1`];
    REPEAT WEAK_STRIP_TAC;
    (fun (asl,w) -> (MP_TAC (SPECL ( envl (asl,w)[`x`;`y`;`g k`;`z`;`g k`;`p`;`g (k+1)`]) AZIM_BASE_SHIFT_LT)) (asl,w));
    ASM_SIMP_TAC[AZIM_REFL];
    SUBGOAL_THEN `azim x y z (g (j+1)) <= azim x y z (g (k:num))` ASSUME_TAC;
      BY(ASM_MESON_TAC[arith `a <= b <=> (a<b \/ a = b)`;arith `j<k ==> (j+1=k)\/ (j+1<k)`]);
    REPEAT (FIRST_X_ASSUM MP_TAC);
    BY(REAL_ARITH_TAC);
  COMMENT "C";
  SUBGOAL_THEN `azim x y (g 1) (g n) < azim x y p (g (n:num))` ASSUME_TAC;
    MATCH_MP_TAC AZIM_COMP_LT;
    BY(ASM_MESON_TAC[]);
  SUBGOAL_THEN `1 < n` ASSUME_TAC;
    BY(ASM_MESON_TAC[IN_NUMSEG;arith `1 <= j /\ j < k /\ k <= n ==> 1 < n`]);
  SUBGOAL_THEN `azim x y z (g n) = azim x y z (g 1) + azim x y (g 1) (g n)` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    BY(ASM_MESON_TAC[arith `a<b ==> a <= b`]);
  SUBGOAL_THEN `azim x y z p = azim x y z (g 1) + azim x y (g 1) p` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    ASM_SIMP_TAC[];
    MATCH_MP_TAC (arith `a<b ==> a <= b`);
    ASM_CASES_TAC `(1=j)`;
      BY(ASM_SIMP_TAC[]);
    MATCH_MP_TAC (arith `a < azim x y z (g (j:num)) /\ azim x y z (g j) < c ==> a < c`);
    ASM_SIMP_TAC[];
    FIRST_X_ASSUM MATCH_MP_TAC;
    ASM_SIMP_TAC[];
    BY(ASM_MESON_TAC[IN_NUMSEG; arith `1 <= j ==> ((1=j) \/ (1 < j))`]);
  FIRST_X_ASSUM MP_TAC;
  SUBGOAL_THEN `azim x y z (g n) = azim x y z p + azim x y p (g (n:num))` ASSUME_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    ASM_SIMP_TAC[];
    MATCH_MP_TAC (TAUT `a /\ b ==> b /\ a`);
    CONJ_TAC;
      BY(ASM_MESON_TAC[]);
    MATCH_MP_TAC (arith `a<b ==> a <= b`);
    ASM_CASES_TAC `(j+1=n)`;
      BY(ASM_MESON_TAC[]);
    MATCH_MP_TAC (arith `a < azim x y z (g (j+1)) /\ azim x y z (g (j+1)) < c ==> a < c`);
    ASM_SIMP_TAC[];
    BY(ASM_MESON_TAC[IN_NUMSEG; arith `~(j+1=n) /\ ~(j=n) /\ (j<=n) ==> (j+1 < n)`]);
  DISCH_TAC;
  SUBGOAL_THEN `azim x y (g 1) p < &0` ASSUME_TAC;
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  BY(ASM_MESON_TAC[azim;arith `x < &0 ==> ~(&0 <= x)`])
  ]);;
  (* }}} *)

let ORDER_AZIM_SUM2Pi = prove_by_refinement(
   `!x y z n g.
     ~(collinear {x,y,z}) /\
    (!i. i IN 1..n ==> ~(collinear {x,y, g i})) /\
     (g (n+1) = g 1) /\ (1 < n) /\
        (!j k. j IN 1..n /\ k IN 1..n /\ (j < k) ==>  
	  azim x y z (g j) < azim x y z (g k))
    ==>
     sum (1..n) (\i. azim x y (g i) (g (i+1))) = &2 * pi`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  SUBGOAL_THEN `!i. i IN 1..(n-1) /\ 1 < n ==> (i < i+1 /\ i IN 1..n /\ (i+1) IN 1..n)` MP_TAC;
    REWRITE_TAC[IN_NUMSEG];
    BY(ARITH_TAC);
  REPEAT WEAK_STRIP_TAC;
  SUBGOAL_THEN `!i. i IN 1..(n-1) ==> azim x y (g i) (g(i+1)) = azim x y z (g(i+1)) - azim x y z (g i)` ASSUME_TAC;
    REPEAT WEAK_STRIP_TAC;
    REWRITE_TAC[arith `a = b - c <=> b = c + a`];
    MATCH_MP_TAC Fan.sum4_azim_fan;
    ASM_SIMP_TAC[];
    MATCH_MP_TAC (arith `a < b ==> a <= b`);
    BY(ASM_MESON_TAC[arith `i < i+1`]);
  SUBGOAL_THEN `sum (1..n) (\i. azim x y (g i) (g (i+1))) = sum (1..(n-1)) (\i. azim x y (g i) (g (i+1))) + sum (n..n) (\i. azim x y (g i) (g (i+1)))` SUBST1_TAC;
    BY(ASM_MESON_TAC[arith `1 <= (n-1)+1 /\ ((1<n) ==>(n-1)+1 = n)`;SUM_ADD_SPLIT]);
  REWRITE_TAC[SUM_SING_NUMSEG];
  SUBGOAL_THEN `sum (1..(n-1)) (\i. azim x y (g i) (g(i+1))) = sum(1..(n-1)) (\i. azim x y z (g (i+1)) - azim x y z (g i))` SUBST1_TAC;
    BY(ASM_MESON_TAC[SUM_EQ]);
  SIMP_TAC[SUM_DIFFS_ALT];
  ASM_SIMP_TAC[arith `1< n ==> 1 <= n-1`;arith `1 < n==> (n-1)+1 = n`];
  MATCH_MP_TAC (arith `  a = b + azim x y (g 1) (g n)  /\ c = &2 * pi - azim x y (g 1) (g n) ==> a - b + c = &2 * pi`);
  SUBGOAL_THEN `1 IN 1..n /\ n IN 1..n` MP_TAC;
    REWRITE_TAC[IN_NUMSEG];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN ARITH_TAC);
  REPEAT WEAK_STRIP_TAC;
  SUBCONJ_TAC;
    MATCH_MP_TAC Fan.sum4_azim_fan;
    BY(ASM_SIMP_TAC[arith `a< b ==> a<=b`]);
  DISCH_TAC;
  (fun (asl,w) -> (REWRITE_TAC[SPECL ( envl (asl,w)[`x`;`y`;`g (1)`;`g(n:num)`]) Rogers.AZIM_COMPL_EXT]) (asl,w));
  COND_CASES_TAC;
    BY(ASM_MESON_TAC[arith `x = y + &0 ==> ~(y<x)`]);
  BY(REWRITE_TAC[])
  ]);;
  (* }}} *)


let AFFINE_VEC0 = prove_by_refinement(
  `!(u:real^A) t.  ~(t= &1) ==> vec 0 IN affine hull {u, t % u}`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  REWRITE_TAC[ AFFINE_HULL_2_ALT ; IN_ELIM_THM ];
  ASM_CASES_TAC (`(u:real^A) = vec 0`);
    ASM_REWRITE_TAC[];
    REWRITE_TAC[VECTOR_ADD_RID;VECTOR_SUB_RZERO;VECTOR_ADD_LID;VECTOR_SUB_LZERO];
    REWRITE_TAC[VECTOR_MUL_RZERO];
    BY(REWRITE_TAC[IN_UNIV]);
  REWRITE_TAC[IN_UNIV];
  EXISTS_TAC `&1 / (&1 - t)`;
  REWRITE_TAC[ VECTOR_ARITH `(u + s % (t % u - (u:real^A))) = (&1 + s * t - s) % u`];
  MATCH_MP_TAC (VECTOR_ARITH (`(a:real^A) = b ==> b = a`));
  ASM_REWRITE_TAC [ VECTOR_MUL_EQ_0 ];
  MATCH_MP_TAC (Calc_derivative.rational_identity `&1 + &1 / (&1 - t) * t - &1 / (&1 - t) = &0`);
  BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC)
  ]);;
  (* }}} *)


let RELATIVE_INTERIOR_AFFINE_FACE = prove_by_refinement(
  `!C (p:real^N) f. convex C /\ f face_of C /\ p IN affine hull f /\ (p IN relative_interior C) ==> (f = C) `,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  (fun (asl,w) -> (ENOUGH_TO_SHOW_TAC ( env (asl,w) `~(f INTER relative_interior C = {})`)) (asl,w));
    BY(ASM_MESON_TAC[ FACE_OF_DISJOINT_RELATIVE_INTERIOR ]);
  REWRITE_TAC[Local_lemmas.EXISTS_IN];
  (fun (asl,w) -> (EXISTS_TAC ( env (asl,w) `p`)) (asl,w));
  ASM_REWRITE_TAC[ IN_INTER ];
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w) `f = affine hull f INTER C`) SUBST1_TAC) (asl,w));
    BY(ASM_MESON_TAC [ FACE_OF_STILLCONVEX ]);
  ASM_REWRITE_TAC[ IN_INTER ];
  FIRST_X_ASSUM (MP_TAC);
  BY(ASM_MESON_TAC[ RELATIVE_INTERIOR_SUBSET ;SUBSET; IN ])
  ]);;
  (* }}} *)

let SUBSET_P_HULL = prove(` (S:A -> bool) SUBSET P hull S`,
REWRITE_TAC[HULL_SUBSET]);;


let FCHANGED_AFFINE = prove_by_refinement(
  `!p (f:real^3->bool).
    polyhedron p /\ bounded p /\ vec 0 IN interior p /\ f facet_of p ==>
    (fchanged f INTER affine hull f = relative_interior f)`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  MATCH_MP_TAC SUBSET_ANTISYM;
  ROT_TAC;
  CONJ_TAC;
    REWRITE_TAC[SUBSET_INTER];
    CONJ_TAC;
      BY(BY(ASM_MESON_TAC[ Polyhedron.RELATIVE_SUBSET_FCHANGE ]));
    BY(BY(ASM_MESON_TAC[ Qzksykg.SET_SUBSET_AFFINE_HULL ; RELATIVE_INTERIOR_SUBSET ; SUBSET]));
  REWRITE_TAC[ SUBSET ; IN_INTER ; Polyhedron.fchanged ; IN_ELIM_THM ];
  (REPEAT WEAK_STRIP_TAC);
  (fun (asl,w) -> (ASM_CASES_TAC ( env (asl,w)`x = v1` )) (asl,w));
    BY(ASM_MESON_TAC[]);
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w) `vec 0 IN affine hull f`) ASSUME_TAC) (asl,w));
    (fun (asl,w) -> (ENOUGH_TO_SHOW_TAC ( env (asl,w) `vec 0 IN affine hull {v1, t % v1 } /\ {v1 , t % v1 } SUBSET affine hull f`)) (asl,w));
      BY(ASM_MESON_TAC[ SUBSET; IN; Marchal_cells_2.AFFINE_SUBSET_KY_LEMMA ; HULL_MONO; HULL_HULL ]);
    CONJ_TAC;
      MATCH_MP_TAC AFFINE_VEC0;
      BY(ASM_MESON_TAC [ VECTOR_MUL_LID ]);
    REWRITE_TAC[ SUBSET ];
    GEN_TAC;
    REWRITE_TAC[Collect_geom.IN_SET2];
    REPEAT WEAK_STRIP_TAC THEN ASM_REWRITE_TAC[];
      BY(ASM_MESON_TAC [ RELATIVE_INTERIOR_SUBSET; SUBSET_P_HULL ; SUBSET; IN]);
    BY(ASM_MESON_TAC[]);
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w)`f = p`) ASSUME_TAC) (asl,w));
    MATCH_MP_TAC (INST_TYPE [(`:3`,`:N`)] RELATIVE_INTERIOR_AFFINE_FACE);
    EXISTS_TAC `(vec 0):real^3`;
    ASM_REWRITE_TAC[];
    BY(ASM_MESON_TAC[ POLYHEDRON_IMP_CONVEX ; facet_of; INTERIOR_SUBSET_RELATIVE_INTERIOR ; SUBSET; IN]);
  HASH_UNDISCH_TAC 8736;
  ASM_REWRITE_TAC[ facet_of ];
  BY(MESON_TAC[ ( arith `T ==> ~((x:int) = x - &1)`)])
  ]);;
  (* }}} *)

let RCONE_PREP = prove_by_refinement(
  `!p (v:real^3) u0 b.
    &0 < b /\ ~(v = vec 0) /\ (&0 < v dot v)  /\
    u0= (b / (v dot v)) % v /\ 
    (&0 < t ) /\ (t < &1) /\
    p dot v = b ==>
   ( (u0 dot u0 = (b * b) / (v dot v)) /\
       (p dot u0 = (b * b )/ (v dot v)) /\ 
     (dist (p,u0) pow 2 = p dot p -  (b * b)/(v dot v)   ))`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  SUBCONJ_TAC;
    ASM_REWRITE_TAC[];
    REWRITE_TAC[DOT_LMUL];
    REWRITE_TAC[DOT_RMUL];
    CALC_ID_TAC;
    BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  DISCH_TAC;
  SUBCONJ_TAC;
    ASM_REWRITE_TAC[];
    REWRITE_TAC[DOT_RMUL];
    ASM_REWRITE_TAC[];
    CALC_ID_TAC;
    BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  DISCH_TAC;
  REWRITE_TAC[ Collect_geom.DIST_POW2_DOT ];
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w) `(p - u0) dot (p - u0) = (p dot p) - (b * b)/(v dot v)`) SUBST1_TAC) (asl,w));
    (fun (asl,w) -> (REWRITE_TAC [ VECTOR_ARITH ( env (asl,w) `(p - u0) dot (p - u0) = p dot p - &2 * (p dot u0) + u0 dot u0`)]) (asl,w));
    HASH_KILL_TAC 9721;
    ASM_REWRITE_TAC[];
    BY(BY(REAL_ARITH_TAC));
  BY(REAL_ARITH_TAC)
  ]);;
  (* }}} *)

(*
let RCONE_DISK = prove_by_refinement(
  `!p (v:real^3) u0 b r t.
    &0 < b /\ ~(v = vec 0) /\ (&0 < v dot v) /\ dist(p,u0) < r /\
    u0= (b / (v dot v)) % v /\ 
    (&0 < t ) /\ (t < &1) /\
    p dot v = b /\ (r = b * sqrt(&1 - t pow 2)/(t * norm v)) ==>
    (p IN rcone_gt (vec 0) v t)`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  REWRITE_TAC[rcone_gt;rconesgn;IN_ELIM_THM];
  REWRITE_TAC[VECTOR_ADD_RID;VECTOR_SUB_RZERO;VECTOR_ADD_LID;VECTOR_SUB_LZERO];
  REWRITE_TAC[DIST_0];
  ASM_REWRITE_TAC[];
  (* done above *)
  SUBGOAL_THEN `(u0:real^3) dot u0 = (b * b)/((v:real^3) dot v)` ASSUME_TAC;
    ASM_REWRITE_TAC[];
    REWRITE_TAC[DOT_LMUL];
    REWRITE_TAC[DOT_RMUL];
    CALC_ID_TAC;
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
    (* done above *)
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w)`(p dot u0 = (b * b)/(v dot v))`) ASSUME_TAC) (asl,w));
    ASM_REWRITE_TAC[];
    REWRITE_TAC[DOT_RMUL];
    CALC_ID_TAC;
    ASM_REWRITE_TAC[];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  REWRITE_TAC[arith `a > b <=> b < a`];
  MATCH_MP_TAC Tactics_jordan.REAL_POW_2_LT;
  REWRITE_TAC[ Trigonometry2.MUL_POW2 ];
  REWRITE_TAC[ NORM_POW_2 ];
  CONJ_TAC;
    REPEAT (MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2) THEN REWRITE_TAC[ NORM_POS_LE ];
    REPEAT (MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2) THEN REWRITE_TAC[ NORM_POS_LE ];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  CONJ_TAC;
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
    (* some above *)
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w) `(p - u0) dot (p - u0) = (p dot p) - (b * b)/(v dot v)`) MP_TAC) (asl,w));
    (fun (asl,w) -> (REWRITE_TAC [ VECTOR_ARITH ( env (asl,w) `(p - u0) dot (p - u0) = p dot p - &2 * (p dot u0) + u0 dot u0`)]) (asl,w));
    ASM_REWRITE_TAC[];
    BY(REAL_ARITH_TAC);
  DISCH_TAC;
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w) `p dot p < (b * b)/ ((t pow 2) * (v dot v))`) ASSUME_TAC) (asl,w));
    FIRST_X_ASSUM (fun t -> MP_TAC (MATCH_MP Tarjjuw.CHANGE_TARJJUW_4 t));
    ASM_REWRITE_TAC[ Collect_geom.DIST_POW2_DOT ];
    MATCH_MP_TAC (arith `(c + u = v) ==> (a - c < u ==> a< v)`);
    CALC_ID_TAC;
    REWRITE_TAC[ NORM_EQ_0 ];
    ASM_SIMP_TAC[arith `&0 < k ==> ~(k = &0)`];
    REWRITE_TAC[ Trigonometry2.MUL_POW2 ; NORM_POW_2 ];
    SUBGOAL_THEN `&0 <= &1 - t pow 2` ASSUME_TAC;
      MATCH_MP_TAC Trigonometry2.UNIT_BOUNDED_IN_TOW_FORMS;
      BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
    ASM_SIMP_TAC [ SQRT_POW_2 ];
    BY(REAL_ARITH_TAC);
  COMMENT "1";
  FIRST_X_ASSUM (fun t -> ASSUME_TAC (MATCH_MP ( REWRITE_RULE[ TAUT `(a /\ b ==> c) <=> (a ==> (b ==> c))`] REAL_LT_RMUL) t));
  (fun (asl,w) -> (FIRST_X_ASSUM (fun t -> MP_TAC (ISPEC ( env (asl,w) `(v dot v) * t pow 2`) t))) (asl,w));
  ANTS_TAC;
    MATCH_MP_TAC REAL_LT_MUL THEN ASM_REWRITE_TAC[];
    REWRITE_TAC[ GSYM Trigonometry2.NOT_ZERO_EQ_POW2_LT ];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  MATCH_MP_TAC (arith `(b = c) ==> (a < b ==> (a < c))`);
  CALC_ID_TAC;
  BY(REPEAT CONJ_TAC THEN REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC)
 ]);;
  (* }}} *)
*)

let RCONE_DISK = prove_by_refinement(
  `!p (v:real^3) u0 b r t.
    &0 < b /\ ~(v = vec 0) /\ (&0 < v dot v) /\ dist(p,u0) < r /\
    u0= (b / (v dot v)) % v /\ 
    (&0 < t ) /\ (t < &1) /\
    p dot v = b /\ (r = b * sqrt(&1 - t pow 2)/(t * norm v)) ==>
    (p IN rcone_gt (vec 0) v t)`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  REWRITE_TAC[rcone_gt;rconesgn;IN_ELIM_THM];
  REWRITE_TAC[VECTOR_ADD_RID;VECTOR_SUB_RZERO;VECTOR_ADD_LID;VECTOR_SUB_LZERO];
  REWRITE_TAC[DIST_0];
  GOAL_TERM (fun w -> (MP_TAC (ISPECL ( envl w [`p`;`v`;`u0`;`b`]) RCONE_PREP)));
  ANTS_TAC;
    BY(ASM_MESON_TAC[]);
  REPEAT WEAK_STRIP_TAC;
  HASH_KILL_TAC 9721;
  REWRITE_TAC[arith `a > b <=> b < a`];
  MATCH_MP_TAC Tactics_jordan.REAL_POW_2_LT;
  REWRITE_TAC[ Trigonometry2.MUL_POW2 ];
  REWRITE_TAC[ NORM_POW_2 ];
  CONJ_TAC;
    REPEAT (MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2) THEN REWRITE_TAC[ NORM_POS_LE ];
    REPEAT (MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2) THEN REWRITE_TAC[ NORM_POS_LE ];
    BY(BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  CONJ_TAC;
    BY(BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w) `p dot p < (b * b)/ ((t pow 2) * (v dot v))`) ASSUME_TAC) (asl,w));
    FIRST_X_ASSUM (fun t -> MP_TAC (MATCH_MP Tarjjuw.CHANGE_TARJJUW_4 t));
    ASM_REWRITE_TAC[];
    MATCH_MP_TAC (arith `(c + u = v) ==> (a - c < u ==> a< v)`);
    CALC_ID_TAC;
    REWRITE_TAC[ NORM_EQ_0 ];
    ASM_SIMP_TAC[arith `&0 < k ==> ~(k = &0)`];
    REWRITE_TAC[ Trigonometry2.MUL_POW2 ; NORM_POW_2 ];
    SUBGOAL_THEN `&0 <= &1 - t pow 2` ASSUME_TAC;
      MATCH_MP_TAC Trigonometry2.UNIT_BOUNDED_IN_TOW_FORMS;
      BY(BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
    ASM_SIMP_TAC [ SQRT_POW_2 ];
    BY(BY(REAL_ARITH_TAC));
  COMMENT "1";
  FIRST_X_ASSUM (fun t -> ASSUME_TAC (MATCH_MP ( REWRITE_RULE[ TAUT `(a /\ b ==> c) <=> (a ==> (b ==> c))`] REAL_LT_RMUL) t));
  (fun (asl,w) -> (FIRST_X_ASSUM (fun t -> MP_TAC (ISPEC ( env (asl,w) `(v dot v) * t pow 2`) t))) (asl,w));
  ANTS_TAC;
    MATCH_MP_TAC REAL_LT_MUL THEN ASM_REWRITE_TAC[];
    REWRITE_TAC[ GSYM Trigonometry2.NOT_ZERO_EQ_POW2_LT ];
    BY(BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  MATCH_MP_TAC (arith `(b = c) ==> (a < b ==> (a < c))`);
  ASM_REWRITE_TAC[];
  CALC_ID_TAC;
  BY(BY(REPEAT CONJ_TAC THEN REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC))
 ]);;
  (* }}} *)



(* ========================================================================== *)
(* WORK IN PROGRESS *)
(* ========================================================================== *)

let RDISK_R = prove_by_refinement(
    `! (v:real^3) u0 b  t.
    &0 < b /\ ~(v = vec 0) /\ (&0 < v dot v) /\     (&0 < t ) /\ (t < &1) /\ 
    u0= (b / (v dot v)) % v ==>
    (?r. (&0 < r) /\ (!p.  dist(p,u0) < r /\  p dot v = b ==>  (p IN rcone_gt (vec 0) v t)) /\
       (!w. dist(w,u0) = r /\ w dot v = b ==> cos (arcV(vec 0) u0 w) = t))`,
  (* {{{ proof *)
  [
  REPEAT WEAK_STRIP_TAC;
  GOAL_TERM (fun w -> (ABBREV_TAC ( env w `r = b * sqrt(&1 - t pow 2)/(t * norm v)`)));
  SUBGOAL_THEN `&0 < &1 - t pow 2` ASSUME_TAC;
    REWRITE_TAC[ arith `&0 < &1 - x <=> x < &1` ];
    REWRITE_TAC[ ABS_SQUARE_LT_1 ];
    BY(BY(BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC)));
  EXISTS_TAC `r:real`;
  SUBCONJ_TAC;
    EXPAND_TAC "r";
    MATCH_MP_TAC REAL_LT_MUL;
    CONJ_TAC THEN ASM_REWRITE_TAC[ Calc_derivative.invert_den_lt ];
    MATCH_MP_TAC REAL_LT_MUL;
    ASM_SIMP_TAC [ SQRT_POS_LT ];
    MATCH_MP_TAC REAL_LT_MUL THEN ASM_REWRITE_TAC[];
    BY(ASM_REWRITE_TAC[ NORM_POS_LT ]);
  DISCH_TAC;
  CONJ_TAC;
    REPEAT WEAK_STRIP_TAC;
    BY(ASM_MESON_TAC[ RCONE_DISK]);
  REPEAT WEAK_STRIP_TAC;
  GOAL_TERM (fun REWRITE_TAC -> (MP_TAC (ISPECL ( envl REWRITE_TAC [`w`;`v`;`u0`;`b`]) RCONE_PREP)));
  ANTS_TAC;
    BY(BY(ASM_MESON_TAC[]));
  REPEAT WEAK_STRIP_TAC;
  GOAL_TERM (fun w -> (ENOUGH_TO_SHOW_TAC ( env w `norm u0 * norm w * cos (arcV (vec 0) u0 w) = norm u0 * norm w * t`)));
    REWRITE_TAC[ REAL_EQ_MUL_LCANCEL ; NORM_EQ_0 ];
    MATCH_MP_TAC (TAUT `~a /\ ~b ==> (a \/ b \/ c ==> c)`);
    CONJ_TAC;
      ASM_REWRITE_TAC[];
      REWRITE_TAC [ VECTOR_MUL_EQ_0 ];
      ASM_REWRITE_TAC[];
      REWRITE_TAC [ Calc_derivative.invert_den_eq ];
      REWRITE_TAC[ REAL_ENTIRE];
      BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
    DISCH_TAC;
    HASH_UNDISCH_TAC 287;
    ASM_REWRITE_TAC[];
    REWRITE_TAC[ DOT_LZERO ];
    BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC);
  REWRITE_TAC[ GSYM Trigonometry1.DOT_COS ];
  FIRST_X_ASSUM (MP_TAC);
  HASH_KILL_TAC 9721;
  ASM_REWRITE_TAC[];
  HASH_KILL_TAC 1350;
  EXPAND_TAC "r";
  REWRITE_TAC[ Trigonometry2.MUL_POW2 ; Trigonometry2.DIV_POW2 ];
  REWRITE_TAC[ arith `a = b - c <=> b = a + c`];
  ASM_SIMP_TAC [ SQRT_POW_2 ; arith `&0 < u ==> &0 <= u`];
  DISCH_TAC;
  GOAL_TERM (fun w -> (ENOUGH_TO_SHOW_TAC ( env w `&0 <= u0 dot w /\ &0 <= norm u0 * norm w * t /\ ((u0 dot w) pow 2 = (norm u0 * norm w * t) pow 2)`)));
    BY(ASM_MESON_TAC[ Collect_geom.EQ_POW2_COND ]);
  ONCE_REWRITE_TAC[ DOT_SYM ];
  ASM_REWRITE_TAC[];
  REWRITE_TAC[ Trigonometry2.MUL_POW2 ; Trigonometry2.DIV_POW2 ];
  REWRITE_TAC[ NORM_POW_2 ];
  ASM_REWRITE_TAC[];
  CONJ_TAC;
    REWRITE_TAC[ Calc_derivative.invert_den_le ];
    BY(MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2 THEN CONJ_TAC THEN TRY (MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2 ) THEN (REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  CONJ_TAC;
    REPEAT (MATCH_MP_TAC Real_ext.REAL_PROP_NN_MUL2 THEN REWRITE_TAC[ NORM_POS_LE ] THEN TRY CONJ_TAC);
    BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  CALC_ID_TAC;
  ASM_REWRITE_TAC[ DOT_EQ_0 ; NORM_EQ_0 ];
  CONJ_TAC;
    BY((REPEAT (FIRST_X_ASSUM MP_TAC) THEN REAL_ARITH_TAC));
  REWRITE_TAC[ NORM_POW_2 ];
  BY(REAL_ARITH_TAC)
  ]);;
  (* }}} *)



(*
 (!p. norm p < r ==> p IN P) /\ 
*)

let GOTCJAH_concl = `!c v b P  WF t n. 
    polyhedron P /\ bounded P /\  (&0 < b) /\
    (vec 0 IN interior P) /\ 
    c facet_of P /\ 
    fchanged c = WF /\
    (&0 < t /\ t < &1) /\
    (c = P INTER { p | p dot v = b } /\ rcone_gt (vec 0) v t SUBSET WF) /\
    ( {u | u facet_of c } HAS_SIZE n) 
   ==> &2 * pi - &2* &n * asn (t* sin(pi/ &n)) <= sol (vec 0)  WF`;;

let GOTCJAH = prove_by_refinement(
  GOTCJAH_concl,
  (* {{{ proof *)
  [
  X_GENv_TAC "c3";
  REPEAT WEAK_STRIP_TAC;
  ABBREV_TAC (`A = { (p:real^3) | p dot v = b}`);
  ABBREV_TAC `u0 = (b / (v dot v)) % (v:real^3)`;
  SUBGOAL_THEN `~((v:real^3) = vec 0)` ASSUME_TAC;
    DISCH_TAC;
    HASH_UNDISCH_TAC 2896;
    ASM_REWRITE_TAC[DOT_RZERO];
    REWRITE_TAC[ FUN_EQ_THM ;IN_ELIM_THM];
    DISCH_TAC;
    (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w)`A = {}`) ASSUME_TAC) (asl,w));
      BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN SET_TAC[arith `&0 < b ==> ~(&0 = b)`]);
    (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w)`c3 = {}`) ASSUME_TAC) (asl,w));
      BY(REPEAT (FIRST_X_ASSUM MP_TAC) THEN SET_TAC[]);
    BY(ASM_MESON_TAC[ facet_of ]);
  COMMENT "1";
  COMMENT "v dot v";
  SUBGOAL_THEN `&0 < (v:real^3) dot v` ASSUME_TAC;
    BY(ASM_REWRITE_TAC[DOT_POS_LT]);
  COMMENT "subgoal";
  SUBGOAL_THEN `(u0:real^3) IN rcone_gt (vec 0) v t` ASSUME_TAC;
    REWRITE_TAC[rcone_gt ; rconesgn ; IN_ELIM_THM ; VECTOR_SUB_RZERO ; DIST_0 ];
    EXPAND_TAC "u0";
    REWRITE_TAC[ DOT_LMUL ];
    REWRITE_TAC[ NORM_MUL ];
    REWRITE_TAC[ GSYM NORM_POW_2 ];
    REWRITE_TAC[ arith `x pow 2 = x * x`];
    REWRITE_TAC[ arith `x > y <=> y < x`];
    (fun (asl,w) -> (SUBGOAL_THEN ( env (asl,w)`abs (b / (norm v * norm v)) = b / (norm v * norm v)`) SUBST1_TAC) (asl,w));
      MATCH_MP_TAC Trigonometry2.LT_IMP_ABS_REFL;
      MATCH_MP_TAC REAL_LT_DIV;
      BY(ASM_MESON_TAC [ NORM_POW_2 ; arith `x pow 2 = x * x` ]);
    REWRITE_TAC[ arith `(a * b) * c = a * (b * c)`];
    REWRITE_TAC[ arith `x * x = x pow 2`; NORM_POW_2 ; arith `a * b * c * d = a * (b * c) * d`];
    MATCH_MP_TAC REAL_LT_LMUL;
    CONJ_TAC;
      BY(ASM_MESON_TAC [ REAL_LT_DIV ]);
    REWRITE_TAC[ arith `a * t < a <=> &0 < a * (&1 - t)`];
    BY(ASM_MESON_TAC [ REAL_LT_MUL ; arith `t < &1 <=> &0 < &1 - t`])
(* to here *)
comment "u0"
sgth `(u0:real^3) IN c3` assume
ets `(u0:real^3) IN fchanged c3 /\ (u0 IN affine hull c3)` 
rt[ GSYM IN_INTER]
amt[FCHANGED_AFFINE; SUBSET; IN;  ?44 ] 
conj
amt[SUBSET; IN]
mptac (specl (%%[`P`;`c3`;`v`;`b`]) ?45 )
ants
art[]
conj
mmp ?46
amt[]
AP_TERM_TAC
expand "A"
?-2
rt[ ?elim ]
mt[ ?47 ]
dthen subst1
rt[ ?elim ]
expand "u0"
rt [ DOT_RMUL ]
fun (asl,t) -> (mmt (Calc_derivative.rational_identity t) (asl,t))
repeat (fxa mptac ) then rat 
(* XXD to here *)



  ]);;
  (* }}} *)

