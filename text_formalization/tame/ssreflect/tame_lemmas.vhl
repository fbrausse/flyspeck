"flyspeck_needs \"../formal_lp/hypermap/ssreflect/add_triangle-compiled.hl\"".

Module Tame_lemmas.

"let reflection = new_definition `reflection n x = x - (&2 * (x dot n) / (n dot n)) % n`".

"let kcblrqc_ineq_tml = 
  let has_id = (fun t -> not(intersect [\"KCBLRQC\"] (Ineq.flypaper_ids t) = [])) in
  let extra_ids = [
	\"3287695934\"; 
	\"6988401556\"; 
	\"3862621143 back\"; 
	\"3862621143 front\";
	\"3862621143 side\";
	\"4240815464 a reduced\";
  ] in
  let idl = (filter (fun t -> has_id t or mem t.idv extra_ids) !Ineq.ineqs) in
  let tml = map (fun t -> t.ineq) idl in
    end_itlist (curry mk_conj) tml".

"let kcblrqc_ineq_def = new_definition
	(mk_eq (`kcblrqc_ineq_def:bool`, kcblrqc_ineq_tml))".

"open Tame_general".
"open Add_triangle".
"open Fan_defs".
"open Pack_defs".
"open Hypermap".
"open Hypermap_and_fan".
"open Hypermap_iso".
"open Ssrbool".
"open Ssrnat".

(* Reflection *)
Section Reflection.

"let VECTOR_MUL_RZERO = GEN_ALL VECTOR_MUL_RZERO".
"let VECTOR_SUB_RZERO = GEN_ALL VECTOR_SUB_RZERO".
"let VECTOR_SUB_RDISTRIB = GEN_ALL VECTOR_SUB_RDISTRIB".
"let VECTOR_ADD_RDISTRIB = GEN_ALL VECTOR_ADD_RDISTRIB".
"let VECTOR_SUB_LDISTRIB = GEN_ALL VECTOR_SUB_LDISTRIB".

Implicit Type x y d: `:real^N`.


Lemma reflection_at0 x: `reflection (vec 0) x = x`.
Proof. by rewrite reflection VECTOR_MUL_RZERO VECTOR_SUB_RZERO. Qed.

Lemma reflection_eq n x: `x dot n = &0 ==> reflection n x = x`.
Proof. 
rewrite reflection => ->.
by rewrite real_div REAL_MUL_LZERO REAL_MUL_RZERO VECTOR_MUL_LZERO VECTOR_SUB_RZERO.
Qed.

Lemma reflection_eq2 n x: `reflection n x = x <=> x dot n = &0`.
Proof.
case n0: `n = vec 0` => /=; first by rewrite n0 reflection_at0 DOT_RZERO.
split; last by move/reflection_eq.
rewrite reflection "VECTOR_ARITH `!x y. x - y = x <=> y = vec 0`" VECTOR_MUL_EQ_0.
by rewrite n0 orbF real_div !REAL_ENTIRE REAL_INV_EQ_0 DOT_EQ_0 n0; arith.
Qed.

Lemma reflection0 n: `reflection n (vec 0) = vec 0`.
Proof. by rewrite reflection_eq // DOT_LZERO. Qed.

Lemma reflection_sub n x y: `reflection n (x - y) = reflection n x - reflection n y`.
Proof.
rewrite !reflection DOT_LSUB !real_div !REAL_MUL_ASSOC REAL_SUB_LDISTRIB REAL_SUB_RDISTRIB.
by rewrite VECTOR_SUB_RDISTRIB; "VECTOR_ARITH_TAC".
Qed.

Lemma reflection_add n x y: `reflection n (x + y) = reflection n x + reflection n y`.
Proof.
rewrite !reflection DOT_LADD !real_div !REAL_MUL_ASSOC REAL_ADD_LDISTRIB REAL_ADD_RDISTRIB.
by rewrite VECTOR_ADD_RDISTRIB; "VECTOR_ARITH_TAC".
Qed.

Lemma reflection_cmul n c x: `reflection n (c % x) = c % reflection n x`.
Proof.
rewrite !reflection DOT_LMUL !real_div VECTOR_SUB_LDISTRIB VECTOR_MUL_ASSOC.
by rewrite !REAL_MUL_ASSOC (REAL_MUL_SYM `&2`).
Qed.

Lemma norm_reflection n x: `norm (reflection n x) = norm x`.
Proof.
case n0: `n = vec 0` => /=; first by rewrite n0 reflection_at0.
rewrite reflection NORM_EQ DOT_LSUB !DOT_RSUB !DOT_RMUL !DOT_LMUL (DOT_SYM n x).
suff: `~(n dot n = &0)` by "CONV_TAC REAL_FIELD".
by rewrite DOT_EQ_0.
Qed.

Lemma dist_reflection n x y: `dist (reflection n x, reflection n y) = dist (x, y)`.
Proof. by rewrite !dist -reflection_sub norm_reflection. Qed.

Lemma dist_reflection_special n x y: `y dot n = &0 ==> dist (reflection n x, y) = dist (x, y)`.
Proof. by move/reflection_eq => {1}<-; rewrite dist_reflection. Qed.

Lemma reflection_dot n x: `(reflection n x) dot n = --(x dot n)`.
Proof.
case n0: `n = vec 0` => /=; first by rewrite n0 !DOT_RZERO REAL_NEG_0.
rewrite reflection DOT_LSUB DOT_LMUL.
by move: n0; rewrite -DOT_EQ_0; "CONV_TAC REAL_FIELD".
Qed.

Lemma dist_reflection_lemma n x y: `dist (reflection n x, y) pow 2
	= dist (x, y) pow 2 + &4 * ((y dot n) * (x dot n)) / (n dot n)`.
Proof.
case n0: `n = vec 0` => /=.
  by rewrite n0 reflection_at0 DOT_RZERO real_div !REAL_MUL_LZERO REAL_MUL_RZERO REAL_ADD_RID.
rewrite Collect_geom.DIST_POW2_DOT.
have {1 2}->: `y = reflection n y + (&2 * (y dot n) / (n dot n)) % n`.
  by rewrite reflection; "VECTOR_ARITH_TAC".
set a := `&2 * _`.
have -> b c d: `b - (c + d) = (b - c) - d:real^N` by "VECTOR_ARITH_TAC".
have -> b c: `(b - c) dot (b - c) = b dot b + c dot c - &2 * (b dot c:real^N)`.
  by rewrite DOT_LSUB !DOT_RSUB (DOT_SYM b c); "VECTOR_ARITH_TAC".
rewrite -Collect_geom.DIST_POW2_DOT dist_reflection.
rewrite DOT_LSUB DOT_LMUL !DOT_RMUL !reflection_dot.
rewrite -a_def; move: n0; rewrite -DOT_EQ_0.
by "CONV_TAC REAL_FIELD".
Qed.


End Reflection.


(* Misc *)
Section Misc.

Implicit Type v: `:real^3`.


Lemma delta_y_pos v w w': 
	`let y1 = norm v in
	 let y2 = norm w in
	 let y3 = norm w' in
	 let y4 = dist (w,w') in
	 let y5 = dist (v,w') in
	 let y6 = dist (v,w) in
		&0 <= delta_y y1 y2 y3 y4 y5 y6`.
Proof.
do !"CONV_TAC let_CONV".
rewrite Sphere.delta_y -DELTA_EQ_DELTA_X -!DIST_0 -!REAL_POW_2.
have := Collect_geom.DELTA_POS_4POINTS `vec 0:real^3` v w w'.
by rewrite !(DIST_SYM `vec 0`).
Qed.


Lemma quadratic_root_plus_works a b c: `~(a = &0) /\ &0 <= b pow 2 - &4 * a * c ==>
	let x = quadratic_root_plus (a,b,c) in
		a * x pow 2 + b * x + c = &0`.
Proof.
move => ineqs; "CONV_TAC let_CONV".
rewrite (Collect_geom2.FACTOR_OF_QUADRARTIC ineqs).
by rewrite Sphere.quadratic_root_plus; arith.
Qed.


Lemma quadratic_root_plus_eq a b c x: `&0 < a /\ &0 <= b pow 2 - &4 * a * c 
	/\ &0 <= b /\ (a * x pow 2 + b * x + c = &0) /\ &0 <= x
	==> quadratic_root_plus (a,b,c) = x`.
Proof.
move => [a_gt0] [disc_ge0] [b_ge0] [x_root] x_ge0.
have a_neq: `~(a = &0)` by move: a_gt0; arith.
have := Collect_geom2.FACTOR_OF_QUADRARTIC a_neq disc_ge0 x.
rewrite x_root eq_sym !REAL_ENTIRE -Sphere.quadratic_root_plus a_neq orFb.
rewrite !REAL_SUB_0 => [] [-> | ] // x_eq.
have: `x <= &0`.
  rewrite x_eq.
  suff -> u: `&0 <= u ==> (--b - u) / (&2 * a) <= &0` by rewrite SQRT_POS_LE.
  move => u_ge0; rewrite "REAL_ARITH `!x. x <= &0 <=> ~(&0 < x)`".
  rewrite real_div REAL_MUL_POS_LT negb_or.
  have h1: `&0 < inv (&2 * a)` by rewrite REAL_LT_INV REAL_LT_MUL a_gt0; arith.
  rewrite h1 andbT !(real_lt `&0`) !REAL_LE_LT h1 /=.
  by move: u_ge0 b_ge0; arith.
rewrite REAL_LE_LT; case; first by move: x_ge0; arith.
move => x_eq0.
have c_eq0: `c = &0` by move: x_root; rewrite x_eq0 REAL_POW_2 !REAL_MUL_RZERO !REAL_ADD_LID.
rewrite Sphere.quadratic_root_plus c_eq0 !REAL_MUL_RZERO REAL_SUB_RZERO.
by rewrite POW_2_SQRT // REAL_ADD_LINV real_div REAL_MUL_LZERO x_eq0.
Qed.


Lemma quadratic_root_plus_gt_eq a b c x y: `&0 < a /\ &0 <= b pow 2 - &4 * a * c
	/\ a * x pow 2 + b * x + c = &0 /\ a * y pow 2 + b * y + c = &0 /\ y < x
	==> quadratic_root_plus (a,b,c) = x`.
Proof.
move => [a_gt0] [disc_ge0] [x_root] [y_root] y_lt_x.
have a_neq: `~(a = &0)` by move: a_gt0; arith.
have h := Collect_geom2.FACTOR_OF_QUADRARTIC a_neq disc_ge0.
move: (h x) (h y); rewrite x_root y_root !(eq_sym `&0`) !REAL_ENTIRE a_neq !orFb.
rewrite !REAL_SUB_0 -{1}Sphere.quadratic_root_plus => [] [-> | ] // x_eq.
case => y_eq; move: y_lt_x; rewrite x_eq y_eq; last by arith.
rewrite !real_div REAL_LT_RMUL_EQ ?(REAL_LT_INV, REAL_LT_MUL, a_gt0); first by arith.
by move: (SQRT_POS_LE disc_ge0); arith.
Qed.


Lemma quadratic_root_plus_disc0_eq a b c x: `~(a = &0) /\ b pow 2 - &4 * a * c = &0 
	/\ a * x pow 2 + b * x + c = &0
	==> quadratic_root_plus (a,b,c) = x`.
Proof.
move => [a_neq0] [disc0] x_root.
have := Collect_geom2.FACTOR_OF_QUADRARTIC a_neq0 b c x; "ANTS_TAC".
  by rewrite disc0 REAL_LE_REFL.
rewrite x_root eq_sym !REAL_ENTIRE a_neq0 orFb.
rewrite !REAL_SUB_0 Sphere.quadratic_root_plus => [] [-> | ] //.
by rewrite disc0 SQRT_0 REAL_SUB_RZERO REAL_ADD_RID => ->.
Qed.


End Misc.


(* FullySurrounded *)
Section FullySurrounded.

Variable V: `:real^3->bool`.
Variable E: `:(real^3->bool)->bool`.

Hypothesis fanV: `FAN (vec 0,V,E)`.


Hypothesis f_surr: `fully_surrounded (V,E)`.

Let dart1_eq: `dart_of_fan (V,E) = dart1_of_fan (V,E)`.
Proof. by rewrite fully_surrounded_dart_of_fan_eq. Qed.

Let confV: `conforming_fan (vec 0,V,E)`.
Proof. by rewrite fully_surrounded_imp_conforming. Qed.

Let dartH: `dart (hypermap_of_fan (V,E)) = dart_of_fan (V,E)`.
Proof. by rewrite COMPONENTS_HYPERMAP_OF_FAN. Qed.


Lemma dart_leads_into_eq1 v w: `v,w IN dart_of_fan (V,E)
	==> dart_leads_into (vec 0) V E v w 
		= dartset_leads_into_fan (vec 0) V E 
			(IMAGE (ext_dart (V,E)) (face (hypermap_of_fan (V,E)) (v,w)))`.
Proof.
move => vw_in.
have iso := fan_hypermaps_iso_explicit2 fanV.
set h := `ext_dart _`.
set H := `hypermap_of_fan _`.
rewrite -(iso_components iso) ?dartH //.
set f1 := `face _1 _2`.
rewrite (Planarity.DARTSET_LEADS_INTO_FAN fanV f1 `h (v,w)`); last first.
  by rewrite -h_def ext_dart Fan.pr2 Fan.pr3.
rewrite fully_surrounded_imp_fan80 // -f1_def -lemma_in_face_set.
rewrite face_refl (hyp_iso_dart iso) ?dartH // => /= u uV.
by rewrite fully_surrounded_card_set_of_edge1.
Qed.


Lemma dart_leads_into_same f v w x y: `f IN face_set (hypermap_of_fan (V,E)) 
	/\ (v,w) IN f /\ (x,y) IN f
	==> dart_leads_into (vec 0) V E v w = dart_leads_into (vec 0) V E x y`.
Proof.
move => [/lemma_face_representation] [[a b]] [ab_in] -> [vw_in xy_in].
have all_in: `x,y IN dart_of_fan (V,E) /\ v,w IN dart_of_fan (V,E)`.
  by rewrite -dartH (IN_TRANS xy_in) ?(IN_TRANS vw_in) // lemma_face_subset.
rewrite !dart_leads_into_eq1 //.
by rewrite -(lemma_face_identity `a,b` `v,w`) // -(lemma_face_identity `a,b` `x,y`).
Qed.


Lemma fully_surrounded_sol v w: `v,w IN dart_of_fan (V,E)
   ==> sol (vec 0) (dart_leads_into (vec 0) V E v w)
	= &2 * pi + sum (face (hypermap_of_fan (V,E)) (v,w)) (\x. azim_dart (V,E) x - pi)`.
Proof.
move => vw_in.
have iso := fan_hypermaps_iso_explicit2 fanV.
move: confV; rewrite Conforming.conforming_fan "let_RULE Conforming.conforming_solid_angle_fan".
move => [_] [_] [_] [_] [f_sol] _.
rewrite dart_leads_into_eq1 // f_sol.
  by rewrite -(iso_components iso) -?lemma_in_face_set ?(hyp_iso_dart iso) // dartH.
congr `_1 + _2:real` => //.
have in_face x: `x IN face (hypermap_of_fan (V,E)) (v,w) ==> x IN dart_of_fan (V,E)`.
  by move/IN_TRANS => ->; rewrite -dartH lemma_face_subset dartH.
rewrite SUM_IMAGE.
  move => x y [/in_face] x_in [/in_face] y_in /(hyp_iso_inj iso).
  by rewrite dartH => ->.
apply SUM_EQ; move => [a b] /in_face d_in.
by rewrite o_THM /= ext_dart Fan.pr2 Fan.pr3 /= azim_dart_eq_azim_fan.
Qed.


Lemma fully_surrounded_sum_sol:
   `sum (face_set (hypermap_of_fan (V,E)))
	(\f. sol (vec 0) (dartset_leads_into_fan (vec 0) V E  (IMAGE (ext_dart (V,E)) f))) 
	= &4 * pi`.
Proof.
have iso := fan_hypermaps_iso_explicit2 fanV.
have := Conforming.SUM_SOL_IN_FACE_SET_EQ_4PI fanV confV.
rewrite (iso_face_set iso) SUM_IMAGE; last first.
  by move => <-; apply SUM_EQ => f _ /=; rewrite o_THM.
move => f1 f2.
move => [/lemma_face_representation [x] [x_in] ->] [/lemma_face_representation [y] [y_in] ->].
move/(image_inj_gen `dart (hypermap_of_fan (V,E))`) => -> //.
rewrite !lemma_face_subset // !andbT.
exact: (hyp_iso_inj iso).
Qed.


Lemma fully_surrounded_not_coplanar v w: `v,w IN dart_of_fan (V,E)
	==> ~(coplanar {vec 0, v, w, sigma_fan (vec 0) V E v w})`.
Proof.
move => vw_in; move: (vw_in); rewrite dart1_eq => vw_in1.
have vw'_in := sigma_in_dart1 fanV vw_in1.
set w' := `sigma_fan _1 V _2 v w`.
have ->: `{vec 0, v, w, w'} = {vec 0, w', v, w}`.
  by rewrite ("(GEN_ALL o CONJUNCT2 o CONJUNCT2) Collect_geom.PER_SET3" w' w v).
rewrite (Planarity.properties_fully_surrounded fanV) //.
rewrite -w'_def -(fully_surrounded_azim_dart_eq_azim fanV f_surr) // w'_def.
rewrite !(inE_eq_in_dart1 V) vw_in1 dart1_switch vw'_in (AZIM_DART_POS fanV vw_in).
by move: f_surr; rewrite fully_surrounded => ->.
Qed.


Lemma fully_surrounded_delta_pos v w: `v,w IN dart_of_fan (V,E) ==>
		let w' = sigma_fan (vec 0) V E v w in
		let y1 = norm v in
		let y2 = norm w in
		let y3 = norm w' in
		let y4 = dist (w,w') in
		let y5 = dist (v,w') in
		let y6 = dist (v,w) in
			&0 < delta_y y1 y2 y3 y4 y5 y6`.
Proof.
rewrite Sphere.delta_y => vw_in; do !"CONV_TAC let_CONV".
rewrite -DELTA_EQ_DELTA_X -!Trigonometry1.DIST_L_ZERO -!REAL_POW_2.
rewrite "GEN_ALL Collect_geom2.POS_EQ_NOT_COPLANANR".
rewrite -Ckqowsa_3_points.coplanar_eq_coplanar_alt ?DIMINDEX_3; first by arith.
exact: fully_surrounded_not_coplanar.
Qed.


Lemma fully_surrounded_azim_eq_dih_y v w: `v,w IN dart_of_fan (V,E) ==>
	 let w' = sigma_fan (vec 0) V E v w in
	 let y1 = norm v in
	 let y2 = norm w in
	 let y3 = norm w' in
	 let y4 = dist (w,w') in
	 let y5 = dist (v,w') in
	 let y6 = dist (v,w) in
		azim_dart (V,E) (v,w) = dih_y y1 y2 y3 y4 y5 y6`.
Proof.
move => vw_in; do !"CONV_TAC let_CONV"; move: (vw_in); rewrite dart1_eq => vw_in1.
have vw'_in := sigma_in_dart1 vw_in1 fanV.
rewrite fully_surrounded_azim_dart_eq_azim // AZIM_DIVH ?(DART1_NOT_COLLINEAR fanV) //.
rewrite -fully_surrounded_azim_dart_eq_azim //.
move: f_surr; rewrite fully_surrounded => -> //=.
rewrite "let_RULE DIHV_EQ_DIH_Y" ?(DART1_NOT_COLLINEAR fanV) //.
by rewrite !Trigonometry1.DIST_L_ZERO.
Qed.


Lemma fully_surrounded_sol_eq_sol_y v w: `v,w IN dart_of_fan (V,E) /\
	CARD (face (hypermap_of_fan (V,E)) (v,w)) = 3 ==>
		 let w' = sigma_fan (vec 0) V E v w in
		 let y1 = norm v in
		 let y2 = norm w in
		 let y3 = norm w' in
		 let y4 = dist (w,w') in
		 let y5 = dist (v,w') in
		 let y6 = dist (v,w) in
	sol (vec 0) (dart_leads_into (vec 0) V E v w) = sol_y y1 y2 y3 y4 y5 y6`.
Proof.
move => [vw_in card3]; do !"CONV_TAC let_CONV"; move: (vw_in); rewrite dart1_eq => vw_in1.
rewrite fully_surrounded_sol //.
have [f_eq eqs] := "let_RULE TRIANGULAR_FACE" fanV vw_in1 card3.
set w' := `sigma_fan _ V E v w`.
set H := `hypermap_of_fan _`.
have all_in: `v,w IN dart_of_fan (V,E) /\ w,w' IN dart_of_fan (V,E) /\ w',v IN dart_of_fan (V,E)`.
  by rewrite -dartH; do !split; apply IN_TRANS; exists `face H (v,w)`;
	rewrite lemma_face_subset ?dartH // f_eq !IN_INSERT.
have neqs: `~(v = w) /\ ~(w' = v) /\ ~(w = w')`.
  by rewrite !(PAIR_IN_DART1_OF_FAN_IMP_NOT_EQ fanV) -?dart1_eq.
rewrite f_eq !(SUM_SING, SUM_CLAUSES) ?(FINITE_INSERT, FINITE_EMPTY) //.
rewrite !(IN_SING, IN_INSERT) !PAIR_EQ !neqs /= -neqs /=.
rewrite !"let_RULE fully_surrounded_azim_eq_dih_y" // w'_def !eqs.
by rewrite Sphere.sol_y !(DIST_SYM w') !(DIST_SYM w); arith.
Qed.


Lemma tauVEF_alt1 v w: `v,w IN dart_of_fan (V,E)
	==> let f = face (hypermap_of_fan (V,E)) (v,w) in
		tauVEF (V,E,f) = sol (vec 0) (dart_leads_into (vec 0) V E v w)
		  + (&2 - &(CARD f)) * sol0
		  - sol0 / pi * sum f (\x. azim_dart (V,E) x * (lmfun (h_dart x) - &1))`.
Proof.
move => vw_in; "CONV_TAC let_CONV".
rewrite Tame_defs.tauVEF fully_surrounded_sol //.
set H := `hypermap_of_fan _`.
have fin_f := FACE_FINITE H `v,w`.
rewrite "REAL_ARITH `!a b c. a * (&1 + b * (&1 - c)) = a - b * (a * (c - &1))`".
by rewrite !SUM_SUB // !SUM_LMUL (SUM_CONST `pi`) //; arith.
Qed.


Lemma tauVEF_alt2 v w: `v,w IN dart_of_fan (V,E)
	==> let f = face (hypermap_of_fan (V,E)) (v,w) in
	  tauVEF (V,E,f) = sol (vec 0) (dart_leads_into (vec 0) V E v w) * (&1 + sol0 / pi) 
			- sol0 / pi * sum f (\x. azim_dart (V,E) x * lmfun (h_dart x))`.
Proof.
move => vw_in; "CONV_TAC let_CONV".
rewrite "let_RULE tauVEF_alt1" // fully_surrounded_sol //.
set H := `hypermap_of_fan _`.
have fin_f := FACE_FINITE H `v,w`.
have -> a b: `a * (b - &1) = a * b - a` by arith.
rewrite !SUM_SUB // (SUM_CONST `pi`) //.
by move: PI_POS; "CONV_TAC REAL_FIELD".
Qed.


Lemma tauVEF_alt2_alt f: `f IN face_set (hypermap_of_fan (V,E))
   ==> tauVEF (V,E,f) 
	= sol (vec 0) (dartset_leads_into_fan (vec 0) V E  (IMAGE (ext_dart (V,E)) f)) 
		* (&1 + sol0 / pi) 
		- sol0 / pi * sum f (\x. azim_dart (V,E) x * lmfun (h_dart x))`.
Proof.
move/lemma_face_representation => [[v w]]; rewrite dartH => [] [vw_in] f_eq.
by rewrite {1}f_eq "let_RULE tauVEF_alt2" // dart_leads_into_eq1.
Qed.


Lemma tauVEF_alt3 v w: `v,w IN dart_of_fan (V,E)
	==> let f = face (hypermap_of_fan (V,E)) (v,w) in
	tauVEF (V,E,f) = sum f (\x. (&1 + sol0 / pi * (&1 - lmfun (h_dart x))) 
	* azim_dart (V,E) x - pi - sol0) + &2 * (pi + sol0)`.
Proof.
move => vw_in; "CONV_TAC let_CONV".
have -> a x b c: `a * x - b - c = x * a - (b + c):real` by arith.
set H := `hypermap_of_fan _`.
have fin_f := FACE_FINITE H `v,w`.
rewrite SUM_SUB // (SUM_CONST `pi + sol0`) //.
by rewrite Tame_defs.tauVEF; arith.
Qed.


Lemma sum_tauVEF_upper_bound: `&12 <= scriptL V ==>
	 sum (face_set (hypermap_of_fan (V,E))) (\f. tauVEF (V,E,f)) 
		<= &4 * pi - &20 * sol0`.
Proof.
rewrite (SUM_EQ tauVEF_alt2_alt) => ineq.
rewrite SUM_SUB ?FINITE_HYPERMAP_ORBITS // (SUM_LMUL `sol0 / pi`) (SUM_RMUL `&1 + sol0 / pi`).
rewrite fully_surrounded_sum_sol.
rewrite -Tame_defs.face_set_of_fan Hrxefdm_tame.HRXEFDM_lemma1 //.
have -> v s: `(&4 * pi) * (&1 + s / pi) - s / pi * &2 * pi * v = &4 * pi - (&2 * v - &4) * s`.
  by move: PI_NZ; "CONV_TAC REAL_FIELD".
rewrite !real_sub REAL_LE_LADD REAL_LE_NEG REAL_LE_RMUL.
rewrite (REAL_LT_IMP_LE sol0_POS).
by move: ineq; arith.
Qed.


Lemma fully_surrounded_dot_cross v w: `v,w IN dart_of_fan (V,E)
	==> &0 < v dot (w cross sigma_fan (vec 0) V E v w)`.
Proof.
move => vw_in.
set w' := `sigma_fan _ V E v w`.
have := Trigonometry.JBDNJJB v w w'.
rewrite Trigonometry2.re_eqvl => [] [t] [t_gt0].
rewrite -{1}w'_def -fully_surrounded_azim_dart_eq_azim // => sin_eq.
suff: `&0 < sin (azim_dart (V,E) (v,w))`.
  rewrite sin_eq Trigonometry1.cross_triple DOT_SYM.
  by rewrite REAL_MUL_POS_LT; move: t_gt0; arith.
rewrite SIN_POS_PI AZIM_DART_POS //.
by move: f_surr; rewrite fully_surrounded => ->.
Qed.


Section BallAnnulus.

Hypothesis subV: `V SUBSET ball_annulus`.

Let v_norm v: `v IN V ==> norm v <= &2 * h0 /\ &2 <= norm v`.
Proof. by rewrite SUBSET in subV; move/subV; rewrite Ckqowsa_3_points.in_ball_annulus /=. Qed.


Lemma fully_surrounded_lnazim v w: `v,w IN dart_of_fan (V,E) ==>
	 let w' = sigma_fan (vec 0) V E v w in
	 let y1 = norm v in
	 let y2 = norm w in
	 let y3 = norm w' in
	 let y4 = dist (w,w') in
	 let y5 = dist (v,w') in
	 let y6 = dist (v,w) in
		azim_dart (V,E) (v,w) * lmfun (h_dart (v,w)) = lnazim y1 y2 y3 y4 y5 y6`.
Proof.
move => vw_in; do !"CONV_TAC let_CONV".
rewrite Sphere.lnazim -("let_RULE fully_surrounded_azim_eq_dih_y" vw_in).
by rewrite REAL_MUL_SYM ly_EQ_lmfun FST // v_norm (PAIR_IN_DART_OF_FAN fanV vw_in).
Qed.


Lemma fully_surrounded_tau_eq_taum v w: `v,w IN dart_of_fan (V,E)
	/\ CARD (face (hypermap_of_fan (V,E)) (v,w)) = 3
	==>	let w' = sigma_fan (vec 0) V E v w in
		let y1 = norm v in
		let y2 = norm w in
		let y3 = norm w' in
		let y4 = dist (w,w') in
		let y5 = dist (v,w') in
		let y6 = dist (v,w) in
		tauVEF (V,E,face (hypermap_of_fan (V,E)) (v,w)) = taum y1 y2 y3 y4 y5 y6`.
Proof.
move => [vw_in card3]; do !"CONV_TAC let_CONV"; move: (vw_in); rewrite dart1_eq => vw_in1.
have [f_eq eqs] := "let_RULE TRIANGULAR_FACE" fanV vw_in1 card3.
have [dartH _] := COMPONENTS_HYPERMAP_OF_FAN fanV.
set w' := `sigma_fan _ V E v w`.
set H := `hypermap_of_fan _`.
have all_in: `v,w IN dart_of_fan (V,E) /\ w,w' IN dart_of_fan (V,E) /\ w',v IN dart_of_fan (V,E)`.
  by rewrite -dartH; do !split; apply IN_TRANS; exists `face H (v,w)`;
	rewrite lemma_face_subset ?dartH // f_eq !IN_INSERT.
have neqs: `~(v = w) /\ ~(w' = v) /\ ~(w = w')`.
  by rewrite !(PAIR_IN_DART1_OF_FAN_IMP_NOT_EQ fanV) -?dart1_eq.
rewrite -H_def "let_RULE tauVEF_alt2" // H_def.
rewrite f_eq !(SUM_SING, SUM_CLAUSES) ?(FINITE_INSERT, FINITE_EMPTY) //.
rewrite !(IN_SING, IN_INSERT) !PAIR_EQ !neqs /= -neqs /=.
rewrite !"let_RULE fully_surrounded_lnazim" // !eqs.
rewrite "let_RULE fully_surrounded_sol_eq_sol_y" ?H_def // w'_def.
by rewrite Sphere.taum sol0_over_pi_EQ_const1 !(DIST_SYM w') !(DIST_SYM w).
Qed.


End BallAnnulus.

End FullySurrounded.


(* enclosed and tauq *)
Section EnclosedTauq.

Lemma taum_sym y1 y2 y3 y4 y5 y6: `taum y1 y2 y3 y4 y5 y6 = taum y3 y2 y1 y6 y5 y4`.
Proof.
have sym := Nonlinear_lemma.dih_y_sym.
rewrite eq_sym Sphere.taum Sphere.sol_y !Sphere.lnazim.
rewrite (sym y3 y2) (sym y2 y1) (sym y1 y3).
have -> a b c d: `a + b + c - d = c + b + a - d:real` by arith.
rewrite -Sphere.sol_y.
have -> a b c: `a + b + c = c + b + a:real` by arith.
by rewrite -!Sphere.lnazim -Sphere.taum.
Qed.


Variable V: `:real^3->bool`.
Variable E: `:(real^3->bool)->bool`.

Hypothesis fanV: `FAN (vec 0,V,E)`.
Hypothesis f_surr: `fully_surrounded (V,E)`.

Let dart1_eq: `dart_of_fan (V,E) = dart1_of_fan (V,E)`.
Proof. by rewrite fully_surrounded_dart_of_fan_eq. Qed.

Let dartH: `dart (hypermap_of_fan (V,E)) = dart_of_fan (V,E)`.
Proof. by rewrite COMPONENTS_HYPERMAP_OF_FAN. Qed.


Lemma fully_surrounded_enclosed v w: `v,w IN dart_of_fan (V,E)
	==>	let w' = sigma_fan (vec 0) V E v w in
		let u = sigma_fan (vec 0) V E w' v in
		let y1 = norm v in
		let y2 = norm w in
		let y3 = norm w' in
		let y4 = dist(w,w') in
		let y5 = dist(v,w') in
		let y6 = dist(v,w) in
		let y7 = norm u in
		let y8 = dist(w',u) in
		let y9 = dist(w,u) in
	dist (v,u) = enclosed y1 y5 y6 y4 y2 y3 y7 y8 y9`.
Proof.
move => vw_in; do 11!"CONV_TAC let_CONV".
set w' := `sigma_fan _ V E v w`.
set u := `sigma_fan _ V E w' v`.
rewrite Enclosed.enclosed.
have := "let_RULE Collect_geom2.CAYLEYR_5POINTS" `vec 0:real^3` w' w v u.
rewrite !(DIST_SYM `vec 0`) !DIST_0 !REAL_POW_2 -Mur.muR !(DIST_SYM v) !(DIST_SYM w').
set p := `muR _1 _2 _3 _4 _5 _6 _7 _8 _9`.
move => p0.
suff ->: `quadratic_root_plus (abc_of_quadratic p) = dist (u,v) pow 2`.
  by rewrite POW_2_SQRT // DIST_POS_LE.
suff: `?a b c x. (p = \x. a * x pow 2 + b * x + c) 
	/\ &0 < a /\ &0 <= b pow 2 - &4 * a * c 
	/\ (p (dist (x, v) * dist (x, v)) = &0 /\ dist (x, v) < dist (u, v) 
		\/ b pow 2 - &4 * a * c = &0)`.
  move => [a] [b] [c] [x] [p_eq] [a_gt0] [disc_ge0] h.
  rewrite p_eq Nonlinear_lemma.abc_quadratic.
  case: h => [[px0] dist_lt | disc0]; last first.
     apply: quadratic_root_plus_disc0_eq.
     by rewrite disc0 REAL_POS_NZ //= -p0 p_eq /= -REAL_POW_2.
  apply: quadratic_root_plus_gt_eq; exists `dist (x, v) * dist (x, v)`.
  rewrite a_gt0 disc_ge0 !andTb -REAL_POW_2.
  move: p0 px0; rewrite p_eq /= -!REAL_POW_2 => -> -> /=.
  by rewrite -REAL_LT_SQUARE_ABS !dist !REAL_ABS_NORM -!dist.
rewrite -p_def "GEN_ALL Mur.muRa" FUN_EQ_THM "GEN_ALL Collect_geom.LEMMA50".
set a := `ups_x _1 _2 _3`.
set b := `cayleytr _1 _2 _3 _4 _5 _6 _7 _8 _9 _10`.
set c := `cayleyR _1 _2 _3 _4 _5 _6 _7 _8 _9 _10`.
set y := `reflection (w cross w') u`.
exists a b c y => /=; split.
  rewrite -a_def.
  have := "(GEN_ALL o let_RULE) Trigonometry2.NOT_COLLINEAR_IMP_UPS_LT" `vec 0:real^3` w' w.
  rewrite !DIST_0 -!REAL_POW_2 (DIST_SYM w); apply.
  move/Planarity.notcoplanar_imp_notcollinear_fan: (fully_surrounded_not_coplanar fanV f_surr vw_in).
  by rewrite w'_def "GEN_ALL Collect_geom.PER_SET3" => ->.
split.
  rewrite -b_def -a_def -c_def "GEN_ALL Collect_geom.DISCRIMINANT_OF_CAY".
  rewrite !DELTA_EQ_DELTA_X -!Sphere.delta_y !(DIST_SYM w) !(DIST_SYM v) !(DIST_SYM u).
  rewrite !REAL_LE_MUL; last by arith.
  by rewrite !"let_RULE delta_y_pos".
case: (fully_surrounded_card_face_alt fanV f_surr vw_in) => card_f.
  right; rewrite -b_def -a_def -c_def "GEN_ALL Collect_geom.DISCRIMINANT_OF_CAY".
  rewrite !DELTA_EQ_DELTA_X -!Sphere.delta_y !(DIST_SYM w) !(DIST_SYM v) !(DIST_SYM u).
  have ->: `u = w`.
    by rewrite -u_def -w'_def ("let_RULE TRIANGULAR_FACE" fanV card_f) // -dart1_eq.
  rewrite !REAL_ENTIRE; right; right.
  rewrite Sphere.delta_y -DELTA_EQ_DELTA_X -!Trigonometry1.DIST_L_ZERO -!REAL_POW_2.
  rewrite -("let_RULE Collect_geom.POLFLZY") -Ckqowsa_3_points.coplanar_eq_coplanar_alt.
    by rewrite DIMINDEX_3; arith.
  rewrite "(GEN_ALL o CONJUNCT2) Collect_geom2.PER_SET4".
  rewrite COPLANAR_SMALL CARD_CLAUSES !(FINITE_INSERT, FINITE_EMPTY) // andTb.
  by rewrite !IN_INSERT /= Geomdetail.CARD3.
left; split.
  have := "let_RULE Collect_geom2.CAYLEYR_5POINTS" `vec 0:real^3` w' w v y.
  rewrite !(DIST_SYM `vec 0`) !DIST_0 -!REAL_POW_2 (DIST_SYM y) !(DIST_SYM w) !(DIST_SYM w').
  by rewrite -y_def norm_reflection !dist_reflection_special ?DOT_CROSS_SELF.
rewrite !dist -REAL_ABS_NORM -(REAL_ABS_NORM `u - v:real^3`) REAL_LT_SQUARE_ABS -!dist.
rewrite -y_def dist_reflection_lemma.
set n := `w cross w'`.
have n0: `~(n = vec 0)`.
  rewrite -n_def CROSS_EQ_0 -w'_def.
  by move/Planarity.notcoplanar_imp_notcollinear_fan: (fully_surrounded_not_coplanar fanV f_surr vw_in) => ->.
suff ineqs: `&0 < v dot n /\ u dot n < &0`.
  set t := `&4 * _`.
  suff: `t < &0` by arith.
  rewrite -t_def -REAL_LT_NEG REAL_NEG_0 REAL_NEG_RMUL REAL_LT_MUL.
  rewrite real_div REAL_NEG_LMUL REAL_NEG_RMUL; split; first by arith.
  rewrite !REAL_LT_MUL ?REAL_LT_INV ?DOT_POS_LT //.
  by move: ineqs; arith.
rewrite -n_def -{1}w'_def fully_surrounded_dot_cross // andTb.
have := fan_split_fan_face fanV f_surr vw_in.
rewrite split_fan_face w'_def.
move => [fan2] [dart2_eq] f_surr2.
suff: `&0 < w' dot (w cross u)`.
  by rewrite CROSS_SKEW DOT_RNEG DOT_SYM Trigonometry1.cross_triple DOT_SYM REAL_NEG_GT0.
suff ->: `u = sigma_fan (vec 0) V ({w,w'} INSERT E) w' w`.
  by rewrite (fully_surrounded_dot_cross fan2 f_surr2) dart2_eq IN_UNION IN_INSERT.
have [eq1 _] := "let_RULE n_split_fan_face" fanV f_surr vw_in card_f.
by move: eq1; rewrite split_fan_face w'_def !n_fan_pair u_def PAIR_EQ => [] [_] ->.
Qed.



Hypothesis subV: `V SUBSET ball_annulus`.


Lemma fully_surrounded_tau_eq_tauq v w: `v,w IN dart_of_fan (V,E)
	/\ CARD (face (hypermap_of_fan (V,E)) (v,w)) = 4
	==>	let w' = sigma_fan (vec 0) V E v w in
		let u = sigma_fan (vec 0) V E w' v in
		let y1 = norm v in
		let y2 = norm w in
		let y3 = norm w' in
		let y4 = dist(w,w') in
		let y5 = dist(v,w') in
		let y6 = dist(v,w) in
		let y7 = norm u in
		let y8 = dist(w',u) in
		let y9 = dist(w,u) in
	tauVEF (V,E,face (hypermap_of_fan (V,E)) (v,w)) = tauq y1 y2 y3 y4 y5 y6 y7 y8 y9`.
Proof.
move => [vw_in card4]; do 11!"CONV_TAC let_CONV".
have := fan_split_fan_face fanV f_surr vw_in.
have := "let_RULE face_split_fan_face_explicit" fanV f_surr vw_in.
rewrite card4 "ARITH_RULE `4 - 1 = 3`"; "ANTS_TAC"; first by arith.
rewrite split_fan_face.
set w' := `sigma_fan _ V E v w`.
set w2 := `sigma_fan _ V E w' v`.
set E2 := `_ INSERT E`.
set H2 := `hypermap_of_fan (V,E2)`.
move => [_] [cards_eq] [in_face] neqs [fan2] [dart2_eq] f_surr2.
have all_in2: `v,w IN dart_of_fan (V,E2) /\ w',w IN dart_of_fan (V,E2)`.
  by rewrite dart2_eq !IN_UNION -dart1_eq !IN_INSERT vw_in.
rewrite ("let_RULE tau_split_fan_face_add" fanV f_surr vw_in); first by rewrite card4; arith.
rewrite w'_def E2_def.
rewrite !"let_RULE fully_surrounded_tau_eq_taum" //.
set H := `hypermap_of_fan (V,E)`.
have split_eq: `split_fan_face (V,E) (v,w) = (V,E2)`.
  by rewrite split_fan_face w'_def E2_def.
have ->: `sigma_fan (vec 0) V E2 v w = w'`.
  suff: `n_fan_pair (V,E2) (v,w) = n_fan_pair (V,E) (v,w)`.
    by rewrite !n_fan_pair w'_def PAIR_EQ => [] [_] ->.
  rewrite -split_eq (n_split_fan_face_eq1 vw_in fanV f_surr) // !FST w'_def !neqs /=.
  by rewrite split_eq dart2_eq -dart1_eq IN_UNION.
have ->: `sigma_fan (vec 0) V E2 w' w = w2`.
  suff: `n_fan_pair (V,E2) (w',w) = n_fan_pair (V,E) (w',v)`.
    by rewrite !n_fan_pair w2_def PAIR_EQ => [] [_] ->.
  have := "let_RULE n_split_fan_face" fanV f_surr vw_in.
  by rewrite H_def card4 "ARITH_RULE `3 < 4`" /= split_eq w'_def => ->.
by rewrite Sphere.tauq (taum_sym `norm w2`) !(DIST_SYM w').
Qed.


End EnclosedTauq.



Section Contravening.

Variable V: `:real^3->bool`.
Hypothesis contrV: `contravening V`.

Let fanV: `FAN (vec 0,V,ESTD V)`.
Proof. exact: (CONTRAVENING_FAN contrV). Qed.

Let f_surr: `fully_surrounded (V,ESTD V)`.
Proof. exact: (CONTRAVENING_IMP_FULLY_SURROUNDED fanV). Qed.

Let dart1_eq: `dart_of_fan (V,ESTD V) = dart1_of_fan (V,ESTD V)`.
Proof. exact: (fully_surrounded_dart_of_fan_eq fanV). Qed.


Lemma contravening_non_triangular_face_dist v w: `v,w IN dart_of_fan (V,ESTD V)
	/\ 3 < CARD (face (hypermap_of_fan (V,ESTD V)) (v,w))
	==>	let w' = sigma_fan (vec 0) V (ESTD V) v w in
		let y1 = norm v in
		let y2 = norm w in
		let y3 = norm w' in
		let y4 = dist (w,w') in
		let y5 = dist (v,w') in
		let y6 = dist (v,w) in
			(&2 <= y1 /\ y1 <= &2 * h0) /\
			(&2 <= y2 /\ y2 <= &2 * h0) /\
			(&2 <= y3 /\ y3 <= &2 * h0) /\
			(&2 * h0 <= y4 /\ y4 <= &4 * h0) /\
			(&2 <= y5 /\ y5 <= &2 * h0) /\
			(&2 <= y6 /\ y6 <= &2 * h0)`.
Proof.
move => [vw_in card_gt3]; do !"CONV_TAC let_CONV".
rewrite h0 !"ARITH_RULE `&2 * #1.26 = #2.52 /\ &2 = #2.0`".
have vw_in1: `v,w IN dart1_of_fan (V,ESTD V)` by rewrite -dart1_eq.
have w'_in := sigma_fan_inV fanV vw_in1.
have vw'_in := sigma_in_dart1 fanV vw_in1.
have w'_neq_w := sigma_fan_not_fixed fanV vw_in f_surr.
have not_diag := fully_surrounded_diag_not_in_dart fanV f_surr vw_in card_gt3.
have [v_in w_in] := PAIR_IN_DART_OF_FAN fanV vw_in.
set w' := `sigma_fan _1 V _2 v w`.
rewrite -!Trigonometry1.DIST_L_ZERO !(CONTRAVENING_DIST contrV) // !andTb.
rewrite andbC 4?(CONTRAVENING_DART_DIST contrV) // !andTb; split.
  move: not_diag; rewrite dart1_eq dart1_of_fan IN_ELIM_THM /= !NOT_EXISTS_THM.
  by move/(_ w w'); rewrite /= IN_ESTD w_in w'_in -w'_neq_w /=; arith.
apply: (DIST_TRIANGLE_LE w w' v).
have ->: `&4 * #1.26 = #2.52 + #2.52` by arith.
by rewrite REAL_LE_ADD2 DIST_SYM (DIST_SYM w') !(CONTRAVENING_DART_DIST contrV).
Qed.


Section Ineqs.

Hypothesis ineqs: `kcblrqc_ineq_def`.

Lemma non_triangular_face_azim_dart_bound d: `d IN dart_of_fan (V,ESTD V)
	/\ 3 < CARD (face (hypermap_of_fan (V,ESTD V)) d)
	==> #1.15 < azim_dart (V,ESTD V) d`.
Proof.
case: d => v w [vw_in card_gt3].
rewrite "let_RULE CONTRAVENING_AZIM_DART_EQ_DIH_Y" // -real_gt.
set w' := `sigma_fan _1 V _2 v w`.
have := "GEN_ALL Tame_inequalities.DIH_Y_INEQ" 
	`norm v` `norm w` `norm w'` `dist (w,w')` `dist (v,w')` `dist (v,w)`; "ANTS_TAC".
  by move: ineqs; rewrite kcblrqc_ineq_def => ->.
rewrite INEQ_ALT !"GEN_ALL ALL" /=; "ANTS_TAC".
  have eqs: `#2.0 = &2 /\ #2.52 = &2 * h0 /\ #5.04 = &4 * h0` by rewrite h0; arith.
  by rewrite !eqs -w'_def 12!("let_RULE contravening_non_triangular_face_dist" vw_in card_gt3).
by rewrite -REAL_NOT_LT -w'_def ("let_RULE fully_surrounded_delta_pos" fanV f_surr).
Qed.


End Ineqs.


End Contravening.
