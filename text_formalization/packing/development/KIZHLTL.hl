(* ========================================================================= *)
(*                FLYSPECK - BOOK FORMALIZATION                              *)
(*                                                                           *)
(*      Authour   : VU KHAC KY                                               *)
(*      Book lemma: KIZHLTL                                                  *)
(*      Chaper    : Packing (Marchal cells)                                  *)
(*                                                                           *)
(* ========================================================================= *)
(* Lemmas KIZHLTL1, KIZHLTL2 are finished                                    *)
(* ========================================================================  *)

g KIZHLTL1_concl;;  
e (GEN_TAC);;
e (ASM_CASES_TAC `saturated V /\ packing (V:real^3->bool)`);;
e (UP_ASM_TAC THEN STRIP_TAC);;


e (NEW_GOAL `!r. &1 <= r
         ==> sum {X | X SUBSET ball (vec 0,r) /\ mcell_set V X} vol <= 
             vol (ball (vec 0, r))`);;
e (REPEAT STRIP_TAC);;
e (ABBREV_TAC `S = {X | X SUBSET ball (vec 0,r) /\ mcell_set V X}`);;
e (REWRITE_WITH `sum S vol = vol (UNIONS S)`);;
e (ONCE_REWRITE_TAC[EQ_SYM_EQ]);;
e (MATCH_MP_TAC MEASURE_NEGLIGIBLE_UNIONS);;
e (REPEAT STRIP_TAC);;
e (EXPAND_TAC "S");;
e (ASM_SIMP_TAC[FINITE_MCELL_SET_LEMMA]);;
e (REWRITE_TAC[GSYM HAS_MEASURE_MEASURE]);;
e (UP_ASM_TAC THEN EXPAND_TAC "S" THEN REWRITE_TAC[IN;IN_ELIM_THM]);;
e (REWRITE_TAC[mcell_set; IN_ELIM_THM] THEN REPEAT STRIP_TAC);;
e (ASM_REWRITE_TAC[]);;
e (MATCH_MP_TAC MEASURABLE_MCELL);;
e (ASM_REWRITE_TAC[]);;
e (UP_ASM_TAC THEN REWRITE_TAC[IN]);;

e (ASM_CASES_TAC `~NULLSET (s INTER t)`);;
e (NEW_GOAL `F`);;
e (UNDISCH_TAC `s:real^3->bool IN S` THEN UNDISCH_TAC `t:real^3->bool IN S`);;
e (EXPAND_TAC "S" THEN REWRITE_TAC[IN;IN_ELIM_THM]);;
e (REWRITE_TAC[mcell_set; IN_ELIM_THM] THEN REPEAT STRIP_TAC);;
e (NEW_GOAL `s = t:real^3->bool`);;
e (REWRITE_TAC[ASSUME `t = mcell i V ul`; ASSUME `s = mcell i' V ul'`]);;
e (ABBREV_TAC `j = if i <= 4 then i else 4`);;
e (ABBREV_TAC `j' = if i' <= 4 then i' else 4`);;
e (REWRITE_WITH `mcell i V ul = mcell j V ul`);;
e (EXPAND_TAC "j" THEN COND_CASES_TAC);;
e (REFL_TAC);;
e (ASM_SIMP_TAC[ARITH_RULE `~(i <= 4) ==> i >= 4`; 
   ARITH_RULE `4 >= 4`; MCELL_EXPLICIT]);;
e (REWRITE_WITH `mcell i' V ul' = mcell j' V ul'`);;
e (EXPAND_TAC "j'" THEN COND_CASES_TAC);;
e (REFL_TAC);;
e (ASM_SIMP_TAC[ARITH_RULE `~(i <= 4) ==> i >= 4`; 
   ARITH_RULE `4 >= 4`; MCELL_EXPLICIT]);;
e (REWRITE_WITH `j' = j /\ mcell j' V ul' = mcell j V ul`);;
e (MATCH_MP_TAC Ajripqn.AJRIPQN);;

e (REPEAT STRIP_TAC);;
e (ASM_REWRITE_TAC[]);;
e (ASM_REWRITE_TAC[]);;
e (ASM_MESON_TAC[IN]);;
e (ASM_MESON_TAC[IN]);;
e (EXPAND_TAC "j'" THEN COND_CASES_TAC);;
e (UP_ASM_TAC THEN REWRITE_TAC[ARITH_RULE `i <= 4 <=> i=0\/i=1\/i=2\/i=3\/i=4`]
   THEN SET_TAC[]);;
e (SET_TAC[]);;
e (EXPAND_TAC "j" THEN COND_CASES_TAC);;
e (UP_ASM_TAC THEN REWRITE_TAC[ARITH_RULE `i <= 4 <=> i=0\/i=1\/i=2\/i=3\/i=4`]
   THEN SET_TAC[]);;
e (SET_TAC[]);;
e (UP_ASM_TAC);;
e (REWRITE_WITH `mcell j V ul = mcell i V ul`);;
e (EXPAND_TAC "j" THEN COND_CASES_TAC);;
e (REFL_TAC);;
e (ASM_SIMP_TAC[ARITH_RULE `~(i <= 4) ==> i >= 4`; 
   ARITH_RULE `4 >= 4`; MCELL_EXPLICIT]);;
e (REWRITE_WITH `mcell j' V ul' = mcell i' V ul'`);;
e (EXPAND_TAC "j'" THEN COND_CASES_TAC);;
e (REFL_TAC);;
e (ASM_SIMP_TAC[ARITH_RULE `~(i <= 4) ==> i >= 4`; 
   ARITH_RULE `4 >= 4`; MCELL_EXPLICIT]);;
e (ASM_MESON_TAC[]);;
e (ASM_MESON_TAC[]);;
e (ASM_MESON_TAC[]);;
e (ASM_MESON_TAC[]);;

e (MATCH_MP_TAC MEASURE_SUBSET);;
e (REWRITE_TAC[MEASURABLE_BALL]);;
e (REPEAT STRIP_TAC);;
e (EXPAND_TAC "S" THEN MATCH_MP_TAC MEASURABLE_UNIONS);;
e (REPEAT STRIP_TAC);;
e (ASM_SIMP_TAC[FINITE_MCELL_SET_LEMMA]);;
e (UP_ASM_TAC THEN REWRITE_TAC[IN;IN_ELIM_THM; mcell_set]);;
e (REPEAT STRIP_TAC);;
e (ASM_SIMP_TAC[MEASURABLE_MCELL]);;
e (EXPAND_TAC "S" THEN SET_TAC[]);;

(* ----------------------------------------------------------------------- *)

e (NEW_GOAL `?c. !r. &1 <= r
         ==> vol (ball (vec 0, r)) + c * r pow 2 <= 
            sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_open V u))`);;
e (EXISTS_TAC `-- (&24 / &3) * pi`);;
e (REPEAT STRIP_TAC);;

e (ASM_CASES_TAC `r < &6`);;
e (NEW_GOAL `&0 <= sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_open V u))`);;
e (MATCH_MP_TAC SUM_POS_LE);;
e (ASM_SIMP_TAC[Packing3.KIUMVTC]);;
e (REPEAT STRIP_TAC);;
e (MATCH_MP_TAC MEASURE_POS_LE);;
e (ASM_SIMP_TAC[Pack1.measurable_voronoi]);;

e (NEW_GOAL `vol (ball ((vec 0):real^3,r)) + (--(&24 / &3) * pi) * r pow 2 <= &0`);;
e (REWRITE_TAC[REAL_ARITH `a + (--b * c) * d <= &0 <=> a <= b * c * d`]);;
e (ASM_SIMP_TAC [VOLUME_BALL; REAL_ARITH `&1 <= r ==> &0 <= r`]);;
e (REWRITE_TAC[REAL_ARITH `&4 / &3 * pi * r pow 3 <= &24 / &3 * pi * r pow 2
   <=> &0 <= &4 / &3 * pi * r pow 2 * (&6 - r)`]);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (STRIP_TAC);;
e (MATCH_MP_TAC REAL_LE_DIV THEN REAL_ARITH_TAC);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (REWRITE_TAC[PI_POS_LE]);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (REWRITE_TAC[REAL_LE_POW_2]);;
e (ASM_REAL_ARITH_TAC);;
e (ASM_REAL_ARITH_TAC);;


e (NEW_GOAL `vol (ball (vec 0,r - &2)) <=  
   sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_open V u))`);;
e (REWRITE_WITH 
   `sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_open V u)) = 
    sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_closed V u))`);;
e (MATCH_MP_TAC SUM_EQ);;
e (REPEAT STRIP_TAC);;
e (REWRITE_TAC[BETA_THM]);;
e (REWRITE_TAC[GSYM Pack2.MEASURE_VORONOI_CLOSED_OPEN]);;

e (ABBREV_TAC `S:real^3->bool = V INTER ball (vec 0, r)`);;
e (ABBREV_TAC `g = (\t:real^3. voronoi_closed V t)`);;

e (REWRITE_WITH `sum S (\u:real^3. vol (voronoi_closed V u)) = sum S (\t. vol (g t))`);;
e (EXPAND_TAC "g" THEN REWRITE_TAC[]);;
e (REWRITE_WITH `sum S (\t:real^3. vol (g t)) = measure (UNIONS (IMAGE g S))`);;
e (ONCE_REWRITE_TAC[EQ_SYM_EQ]);;
e (MATCH_MP_TAC MEASURE_NEGLIGIBLE_UNIONS_IMAGE);;
e (ASM_REWRITE_TAC[] THEN EXPAND_TAC "g");;
e (REPEAT STRIP_TAC);;
e (EXPAND_TAC "S");;
e (ASM_SIMP_TAC[Packing3.KIUMVTC]);;
e (MATCH_MP_TAC Pack2.MEASURABLE_VORONOI_CLOSED);;
e (ASM_REWRITE_TAC[]);;
e (MATCH_MP_TAC Pack2.NEGLIGIBLE_INTER_VORONOI_CLOSED);;
e (ASM_SET_TAC[]);;
e (EXPAND_TAC "g" THEN REWRITE_TAC[IMAGE]);;
e (MATCH_MP_TAC MEASURE_SUBSET);;
e (REWRITE_TAC[MEASURABLE_BALL]);;
e (REPEAT STRIP_TAC);;
e (MATCH_MP_TAC MEASURABLE_UNIONS);;
e (STRIP_TAC);;
e (MATCH_MP_TAC FINITE_IMAGE_EXPAND);;
e (EXPAND_TAC "S");;
e (ASM_SIMP_TAC[Packing3.KIUMVTC]);;

e (REWRITE_TAC[IN; IN_ELIM_THM] THEN REPEAT STRIP_TAC);;
e (ASM_REWRITE_TAC[]);;
e (MATCH_MP_TAC Pack2.MEASURABLE_VORONOI_CLOSED);;
e (ASM_REWRITE_TAC[]);;
e (REWRITE_TAC[SUBSET; IN_BALL; IN_UNIONS]);;
e (REPEAT STRIP_TAC);;
e (MP_TAC (ASSUME `saturated (V:real^3->bool)`));;
e (REWRITE_TAC[saturated] THEN STRIP_TAC);;
e (NEW_GOAL `?y. y IN V /\ dist (x:real^3,y) < &2`);;
e (ASM_MESON_TAC[]);;
e (UP_ASM_TAC THEN STRIP_TAC);;
e (NEW_GOAL `?v:real^3. v IN V /\ x IN voronoi_closed V v`);;
e (MATCH_MP_TAC Packing3.TIWWFYQ);;
e (ASM_REWRITE_TAC[]);;
e (UP_ASM_TAC THEN STRIP_TAC);;

e (EXISTS_TAC `voronoi_closed V (v:real^3)`);;
e (ASM_REWRITE_TAC[]);;

e (ONCE_REWRITE_TAC[IN] THEN REWRITE_TAC[IN_ELIM_THM]);;
e (EXISTS_TAC `v:real^3`);;
e (STRIP_TAC);;
e (EXPAND_TAC "S" THEN REWRITE_TAC[IN_INTER]);;
e (ASM_REWRITE_TAC[IN_BALL]);;

e (NEW_GOAL `dist (vec 0,v) <= dist (vec 0,x) + dist (x, v:real^3)`);;
e (REWRITE_TAC[DIST_TRIANGLE]);;
e (NEW_GOAL `dist (x, v:real^3) < &2`);;
e (NEW_GOAL `dist (x, v) <= dist (x, y:real^3)`);;
e (UNDISCH_TAC `x:real^3 IN voronoi_closed V v`);;
e (REWRITE_TAC[IN; voronoi_closed; IN_ELIM_THM]);;
e (STRIP_TAC);;
e (FIRST_ASSUM MATCH_MP_TAC);;
e (ASM_SET_TAC[]);;
e (ASM_REAL_ARITH_TAC);;
e (ASM_REAL_ARITH_TAC);;
e (REFL_TAC);;

e (NEW_GOAL `vol (ball (vec 0,r)) + (--(&24 / &3) * pi) * r pow 2 <= 
             vol (ball (vec 0,r - &2))`);;
e (ASM_SIMP_TAC[VOLUME_BALL; REAL_ARITH `~(r < &6) ==> &0 <= r`;
   REAL_ARITH `~(r < &6) ==> &0 <= (r - &2)` ]);;
e (REWRITE_TAC[REAL_ARITH 
 `&4 / &3 * pi * r pow 3 + (--(&24 / &3) * pi) * r pow 2 <=
  &4 / &3 * pi * (r - &2) pow 3 <=> 
  &0 <= &4 / &3 * pi * (&12 * r - &8)`]);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (STRIP_TAC);;
e (MATCH_MP_TAC REAL_LE_DIV THEN REAL_ARITH_TAC);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (STRIP_TAC);;
e (REWRITE_TAC[PI_POS_LE]);;
e (NEW_GOAL `&12 * r >= &72`);;
e (ASM_REAL_ARITH_TAC);;
e (ASM_REAL_ARITH_TAC);;

e (ASM_REAL_ARITH_TAC);;
e (UP_ASM_TAC THEN STRIP_TAC);;

e (EXISTS_TAC `c:real`);;
e (REPEAT STRIP_TAC);;

e (NEW_GOAL `sum {X | X SUBSET ball (vec 0,r) /\ mcell_set V X} vol <=
              vol (ball (vec 0,r))`);;
e (ASM_SIMP_TAC[]);;
e (NEW_GOAL `vol (ball (vec 0,r)) + c * r pow 2 <=
            sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_open V u))`);;
e (ASM_SIMP_TAC[]);;
e (ABBREV_TAC `a1 = sum {X | X SUBSET ball (vec 0,r) /\ mcell_set V X} vol`);;
e (ABBREV_TAC `a2 = vol (ball ((vec 0):real^3,r))`);;
e (ABBREV_TAC `a3 = sum (V INTER ball (vec 0,r)) (\u:real^3. vol (voronoi_open V u))`);;
e (ASM_REAL_ARITH_TAC);;

e (EXISTS_TAC `&0`);;
e (REPEAT STRIP_TAC);;
e (NEW_GOAL `F`);;
e (ASM_MESON_TAC[]);;
e (ASM_MESON_TAC[]);;

let KIZHLTL1 = top_thm();;

(* ------------------------------------------------------------------------ *)
(* ======================================================================== *)

g KIZHLTL2_concl;;
e (REPEAT STRIP_TAC);;
e (ASM_CASES_TAC `saturated V /\ packing V`);;
e (NEW_GOAL 
  `?C. !r. &1 <= r ==> 
   &(CARD (V INTER ball ((vec 0):real^3,r) DIFF V INTER ball (vec 0,r - &8))) <=
   C * r pow 2`);;
e (REWRITE_WITH `!r p. V INTER ball (p:real^3,r) DIFF V INTER ball (p,r - &8)
    = V INTER ball (p:real^3,r + &0) DIFF V INTER ball (p,r - &8)`);;
e (ASM_REWRITE_TAC[REAL_ARITH `a + &0 = a`]);;
e (ASM_SIMP_TAC[PACKING_BALL_BOUNDARY]);;
e (TAKE_TAC);;
e (EXISTS_TAC `(&2 * mm1 / pi) * (&4 * pi) * (--C)`);;
e (REPEAT STRIP_TAC);;
e (NEW_GOAL `&(CARD (V INTER ball ((vec 0):real^3,r) DIFF 
                     V INTER ball (vec 0,r - &8))) <= C * r pow 2`);;
e (ASM_SIMP_TAC[]);;
e (NEW_GOAL `total_solid V = (\X. total_solid V X)`);;
e (REWRITE_TAC[GSYM ETA_AX]);;
e (ONCE_ASM_REWRITE_TAC[] THEN DEL_TAC);;
e (REWRITE_TAC[total_solid]);;
e (ABBREV_TAC `B = {X | X SUBSET ball (vec 0,r) /\ mcell_set V X}`);;
e (NEW_GOAL `FINITE (B:(real^3->bool) ->bool)`);;
e (EXPAND_TAC "B" THEN MATCH_MP_TAC FINITE_MCELL_SET_LEMMA);;
e (ASM_REWRITE_TAC[]);;
e (ABBREV_TAC `A1:real^3->bool = V INTER ball (vec 0,r)`);;
e (ABBREV_TAC `A2:real^3->bool = V INTER ball (vec 0,r - &8)`);;
e (NEW_GOAL `FINITE (A1:real^3->bool)`);;
e (EXPAND_TAC "A1" THEN MATCH_MP_TAC FINITE_PACK_LEMMA);;
e (ASM_REWRITE_TAC[]);;
e (NEW_GOAL `FINITE (A2:real^3->bool)`);;
e (EXPAND_TAC "A2" THEN MATCH_MP_TAC FINITE_PACK_LEMMA);;
e (ASM_REWRITE_TAC[]);;

e (NEW_GOAL `sum B (\X. sum {u | u IN A2 /\ VX V X u} (\u. sol u X)) 
          <= sum B (\X. sum (VX V X) (\x. sol x X))`);;
e (MATCH_MP_TAC SUM_LE);;
e (ASM_REWRITE_TAC[BETA_THM]);;
e (REPEAT STRIP_TAC);;
e (MATCH_MP_TAC SUM_SUBSET_SIMPLE);;
e (REPEAT STRIP_TAC);;
e (REWRITE_TAC[VX] THEN COND_CASES_TAC);;
e (REWRITE_TAC[FINITE_EMPTY]);;
e (LET_TAC);;
e (COND_CASES_TAC);;
e (REWRITE_TAC[FINITE_EMPTY]);;
e (REWRITE_TAC[FINITE_SET_OF_LIST]);;
e (SET_TAC[]);;
e (REWRITE_TAC[BETA_THM]);;

e (UNDISCH_TAC `x:real^3->bool IN B`);;
e (EXPAND_TAC "B" THEN REWRITE_TAC[IN; IN_ELIM_THM; mcell_set_2]);;
e (REPEAT STRIP_TAC);;
e (ASM_REWRITE_TAC[]);;

e (NEW_GOAL `eventually_radial x' (mcell i V ul)`);;
e (MATCH_MP_TAC Urrphbz2.URRPHBZ2);;
e (ASM_REWRITE_TAC[]);;
e (SUBGOAL_THEN `x' IN (VX V x)` MP_TAC);;
e (ASM_SET_TAC[]);;
e (REWRITE_TAC[VX]);;
e (COND_CASES_TAC);;
e (SET_TAC[]);;
e (LET_TAC);;
e (COND_CASES_TAC);;
e (SET_TAC[]);;
e (STRIP_TAC);;

e (UNDISCH_TAC `cell_params V x = k,ul'`);;
e (ONCE_REWRITE_TAC[EQ_SYM_EQ]);;
e (REWRITE_TAC[cell_params]);;
e (ABBREV_TAC `P = (\(k,ul). k <= 4 /\ ul IN barV V 3 /\ x = mcell k V ul)`);;
e (DISCH_TAC);;
e (NEW_GOAL `(P:(num#(real^3)list->bool)) (k,ul')`);;
e (ASM_REWRITE_TAC[]);;
e (MATCH_MP_TAC SELECT_AX);;
e (EXISTS_TAC `(i:num,ul:(real^3)list)`);;
e (EXPAND_TAC "P");;
e (REWRITE_TAC[BETA_THM]);;
e (REWRITE_TAC[IN] THEN ASM_REWRITE_TAC[]);;
e (UP_ASM_TAC THEN EXPAND_TAC "P" THEN REWRITE_TAC[BETA_THM] THEN STRIP_TAC);;
e (NEW_GOAL `set_of_list (truncate_simplex (k - 1) ul') SUBSET V:real^3->bool`);;
e (MATCH_MP_TAC Packing3.BARV_SUBSET);;
e (EXISTS_TAC `k - 1`);;
e (MATCH_MP_TAC Packing3.TRUNCATE_SIMPLEX_BARV);;
e (EXISTS_TAC `3`);;
e (STRIP_TAC);;
e (ASM_SET_TAC[]);;
e (ASM_ARITH_TAC);;
e (ASM_SET_TAC[]);;


e (UP_ASM_TAC THEN REWRITE_TAC[eventually_radial]);;
e (REPEAT STRIP_TAC);;
e (REWRITE_WITH `sol x' (mcell i V ul) = 
  &3 * vol ((mcell i V ul) INTER normball x' r') / r' pow 3`);;
e (MATCH_MP_TAC sol);;
e (ASM_REWRITE_TAC[GSYM Marchal_cells_2_new.RADIAL_VS_RADIAL_NORM; NORMBALL_BALL]);;
e (MATCH_MP_TAC MEASURABLE_INTER);;
e (ASM_SIMP_TAC[MEASURABLE_BALL; MEASURABLE_MCELL]);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (REWRITE_TAC[REAL_ARITH `&0 <= &3`] THEN MATCH_MP_TAC REAL_LE_DIV);;
e (STRIP_TAC);;
e (MATCH_MP_TAC MEASURE_POS_LE);;
e (MATCH_MP_TAC MEASURABLE_INTER);;
e (ASM_SIMP_TAC[MEASURABLE_BALL; NORMBALL_BALL; MEASURABLE_MCELL]);;
e (MATCH_MP_TAC REAL_POW_LE THEN ASM_REAL_ARITH_TAC);;

(* ------------------------------------------------------------------------- *)

e (NEW_GOAL `sum B  (\X. sum {u | u IN A2 /\ VX V X u} (\u. sol u X)) = 
             sum A2 (\u. sum  {X | X IN B /\ VX V X u} (\X. sol u X))`);;
e (MATCH_MP_TAC SUM_SUM_RESTRICT);;
e (ASM_REWRITE_TAC[]);;

e (NEW_GOAL `sum A2 (\u. sum {X | X IN B /\ VX V X u} (\X. sol u X)) = 
             sum A2 (\u. sum {X | mcell_set V X /\ u IN VX V X} 
                    (\X. sol u X))`);;
e (MATCH_MP_TAC SUM_EQ);;
e (EXPAND_TAC "A2" THEN REWRITE_TAC[IN_INTER; IN_DIFF] THEN 
   REWRITE_TAC[IN_BALL; IN; IN_ELIM_THM] THEN REPEAT STRIP_TAC);;
e (REWRITE_WITH `{X | B X /\ VX V X x} = 
                 {X | mcell_set V X /\ VX V X x}`);;
e (REWRITE_TAC[SET_RULE `a = b <=> a SUBSET b /\ b SUBSET a`]);;
e (STRIP_TAC);;
e (EXPAND_TAC "B");;
e (SET_TAC[]);;
e (REWRITE_WITH `!x:real^3->bool. B x <=> x IN B`);;
e (REWRITE_TAC[IN]);;
e (EXPAND_TAC "B" THEN REWRITE_TAC[SUBSET; IN_INTER; IN_DIFF] THEN  
   REWRITE_TAC[IN_BALL; IN; IN_ELIM_THM]);;
e (REWRITE_TAC[MESON[] `A /\ X /\ Y ==> (B /\ A) /\ X /\ Y <=> A /\ X /\ Y ==> B`]);;
e (REPEAT STRIP_TAC);;
e (NEW_GOAL `x IN VX V x'`);;
e (ASM_REWRITE_TAC[IN]);;
e (NEW_GOAL `~NULLSET x'`);;
e (UNDISCH_TAC `x IN VX V x'` THEN REWRITE_TAC[VX] THEN COND_CASES_TAC);;
e (SET_TAC[]);;
e (MESON_TAC[]);;

e (NEW_GOAL `dist (vec 0, x'':real^3) <= dist (vec 0, x) + dist (x, x'')`);;
e (REWRITE_TAC[DIST_TRIANGLE]);;
e (NEW_GOAL `?p. x' SUBSET ball (p:real^3,&4)`);;
e (MATCH_MP_TAC MCELL_SUBSET_BALL_4);;
e (EXISTS_TAC `V:real^3->bool` THEN ASM_REWRITE_TAC[]);;
e (UP_ASM_TAC THEN REWRITE_TAC[SUBSET; IN_BALL] THEN STRIP_TAC);;
e (NEW_GOAL `dist (x, x'':real^3) <= dist (x, p) + dist (p, x'')`);;
e (REWRITE_TAC[DIST_TRIANGLE]);;
e (NEW_GOAL `dist (x, p:real^3) < &4`);;
e (ONCE_REWRITE_TAC[DIST_SYM]);;
e (FIRST_ASSUM MATCH_MP_TAC);;
e (NEW_GOAL `VX V x' = V INTER x'`);;
e (MATCH_MP_TAC Hdtfnfz.HDTFNFZ);;
e (UNDISCH_TAC `mcell_set V x'` THEN REWRITE_TAC[mcell_set; IN_ELIM_THM] THEN STRIP_TAC);;
e (EXISTS_TAC `ul:(real^3)list` THEN EXISTS_TAC `i:num`);;
e (ASM_REWRITE_TAC[]);;
e (ASM_SET_TAC[]);;
e (ASM_SET_TAC[]);;
e (NEW_GOAL `dist (p:real^3,x'') < &4`);;
e (FIRST_ASSUM MATCH_MP_TAC);;
e (ASM_REWRITE_TAC[IN]);;
e (ASM_REAL_ARITH_TAC);;
e (ASM_REWRITE_TAC[]);;
e (ASM_REWRITE_TAC[]);;

(* ------------------------------------------------------------------------ *)

e (UP_ASM_TAC);;
e (REWRITE_WITH `sum A2 (\u. sum {X | mcell_set V X /\ u IN VX V X} (\X. sol u X)) = sum A2 (\u. &4 * pi)`);;
e (MATCH_MP_TAC SUM_EQ);;
e (REWRITE_TAC[BETA_THM] THEN REPEAT STRIP_TAC);;
e (MATCH_MP_TAC Qzyzmjc.QZYZMJC);;
e (ASM_REWRITE_TAC[]);;
e (ASM_SET_TAC[]);;
e (ASM_SIMP_TAC[SUM_CONST]);;
e (STRIP_TAC);;

e (ABBREV_TAC `s1 = sum B (\X. sum (VX V X) (\x. sol x X))`);;
e (NEW_GOAL `&(CARD (A2:real^3->bool)) * &4 * pi <= s1`);;
e (ABBREV_TAC `s2 = sum B (\X. sum {u | u IN A2 /\ VX V X u} (\u. sol u X))`);;
e (ABBREV_TAC `s3 = sum A2 (\u. sum {X | X IN B /\ VX V X u} (\X. sol u X))`);;
e (ASM_REAL_ARITH_TAC);;
e (NEW_GOAL `(&2 * mm1 / pi) * &(CARD (A2:real^3->bool)) * &4 * pi <= (&2 * mm1 / pi) * s1`);;
e (REWRITE_TAC[REAL_ARITH `a * b <= a * c <=> &0 <= (c - b) * a`]);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (STRIP_TAC);;
e (ASM_REAL_ARITH_TAC);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (STRIP_TAC);;
e (REAL_ARITH_TAC);;
e (MATCH_MP_TAC REAL_LE_DIV);;
e (REWRITE_TAC[PI_POS_LE]);;
e (NEW_GOAL `#1.012080 < mm1`);;
e (REWRITE_TAC[Flyspeck_constants.bounds]);;
e (UP_ASM_TAC THEN REAL_ARITH_TAC);;
 
e (NEW_GOAL `&(CARD (A1:real^3->bool)) * &8 * mm1 + 
             ((&2 * mm1 / pi) * (&4 * pi) * --C) * r pow 2 <= 
             (&2 * mm1 / pi) * &(CARD (A2:real^3->bool)) * &4 * pi`);;
e (REWRITE_TAC[REAL_ARITH `((&2 * mm1 / pi) * (&4 * pi) * --C) * r pow 2 = 
  (--C * r pow 2) * (&8 * mm1) * (pi / pi)`]);;
e (REWRITE_TAC[REAL_ARITH `(&2 * mm1 / pi) * &(CARD A2) * &4 * pi = 
  &(CARD A2) * (&8 * mm1) * (pi / pi)`]);;
e (REWRITE_WITH `pi / pi = &1`);;
e (MATCH_MP_TAC REAL_DIV_REFL);;
e (MP_TAC PI_POS THEN REAL_ARITH_TAC);;
e (REWRITE_TAC[REAL_MUL_RID; REAL_ARITH `a * b + c * b <= d * b <=>
  &0 <= (d - a - c) * b `]);;
e (MATCH_MP_TAC REAL_LE_MUL);;
e (STRIP_TAC);;
e (REWRITE_TAC[REAL_ARITH `&0 <= a - b - (--c * x) <=> b - a <= c * x`]);;

e (NEW_GOAL `A2 SUBSET A1:real^3->bool`);;
e (EXPAND_TAC "A1" THEN EXPAND_TAC "A2");;
e (MATCH_MP_TAC (SET_RULE `A SUBSET B ==> V INTER A SUBSET V INTER B`));;
e (MATCH_MP_TAC SUBSET_BALL);;
e (REAL_ARITH_TAC);;
e (REWRITE_WITH `&(CARD (A1:real^3->bool)) - &(CARD (A2:real^3->bool)) = 
   &(CARD A1 - CARD A2)`);;
e (MATCH_MP_TAC REAL_OF_NUM_SUB);;
e (MATCH_MP_TAC CARD_SUBSET);;
e (ASM_REWRITE_TAC[]);;

e (REWRITE_WITH `CARD (A1:real^3->bool) - CARD (A2:real^3->bool) = 
   CARD (A1 DIFF A2)`);;
e (ONCE_REWRITE_TAC[EQ_SYM_EQ]);;
e (MATCH_MP_TAC CARD_DIFF);;
e (ASM_REWRITE_TAC[]);;
e (ASM_REWRITE_TAC[]);;

e (MATCH_MP_TAC REAL_LE_MUL);;
e (NEW_GOAL `#1.012080 < mm1`);;
e (REWRITE_TAC[Flyspeck_constants.bounds]);;
e (UP_ASM_TAC THEN REAL_ARITH_TAC);;


e (ASM_REAL_ARITH_TAC);;
e (EXISTS_TAC `&0`);;
e (REPEAT STRIP_TAC);;
e (NEW_GOAL `F`);;
e (ASM_MESON_TAC[]);;
e (ASM_MESON_TAC[]);;

let KIZHLTL2 = top_thm();;


