(* ========================================================================== *)
(* FLYSPECK - BOOK FORMALIZATION                                              *)
(* Section: Conclusions                                                       *)
(* Chapter: Local Fan                                                         *)
(* Author: Thomas C. Hales                                                    *)
(* Date: 2013-02-26                                                           *)
(* ========================================================================== *)

(*
remaining conclusions from appendix to Local Fan chapter
*)


module Appendix = struct


open Hales_tactic;;



let MHAEYJN_concl = 
`!a b V E FF f v w u.
     convex_local_fan (V,E,FF) /\
     lunar (v,w) V E /\
     deformation f V (a,b) /\
     interior_angle1 (vec 0) FF v < pi /\
     u IN V /\
     ~(u = v) /\
     ~(u = w) /\
     (!u' t. u IN V /\ ~(u = u') /\ t IN real_interval (a,b) ==> f u' t = u') /\
     (!t. t IN real_interval (a,b) ==> f u t IN affine hull {vec 0, v, w, u})
     ==> (?e. &0 < e /\
              (!t. --e < t /\ t < e
                   ==> convex_local_fan
                       (IMAGE (\v. f v t) V,
                        IMAGE (IMAGE (\v. f v t)) E,
                        IMAGE (\uv. f (FST uv) t,f (SND uv) t) FF) /\
                       lunar (v,w) (IMAGE (\v. f v t) V)
                       (IMAGE (IMAGE (\v. f v t)) E)))`;;

let ZLZTHIC_concl = 
`!a b V E FF f.
     convex_local_fan (V,E,FF) /\
     generic V E /\
     deformation f V (a,b) /\
     (!v t. v IN V /\ interior_angle1 (vec 0) FF v = pi ==>
	 interior_angle1 (vec 0) ( IMAGE (\uv. f (FST uv) t,f (SND uv) t) FF) (f v t) <= pi)
     ==> (?e. &0 < e /\
              (!t. --e < t /\ t < e
                   ==> convex_local_fan
                       (IMAGE (\v. f v t) V,
                        IMAGE (IMAGE (\v. f v t)) E,
                        IMAGE (\uv. f (FST uv) t,f (SND uv) t) FF) /\
                       generic (IMAGE (\v. f v t) V)
                       (IMAGE (IMAGE (\v. f v t)) E)))`;;

let cstab=new_definition ` cstab= #3.01`;;

let rho_fun = new_definition `rho_fun y =  &1 + (inv (&2 * h0 - &2)) * (inv pi) * sol0 * (y - &2)`;;

let rho_rho_fun = prove_by_refinement(
  `!y. rho_fun y = rho y`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Sphere.rho;rho_fun;Sphere.const1;Sphere.ly;Sphere.interp];
  REWRITE_TAC[GSYM Nonlinear_lemma.sol0_EQ_sol_y;Sphere.h0];
  REWRITE_TAC[arith `a + b = a + c <=> c = b`];
  GEN_TAC;
  MATCH_MP_TAC (arith `(sol0/pi)*(&1 - a) = (sol0/pi)*(c*e) ==> sol0/pi - sol0/pi * a = c * inv pi * sol0 * e`);
  AP_TERM_TAC;
  BY(REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let tau_fun = new_definition `tau_fun V E f =  sum (f) (\e. rho_fun(norm(FST e)) * (azim_in_fan e E)) - (pi + sol0) * &(CARD f -2)`;;

let tau3 = new_definition `tau3 (v1:real^3) v2 v3 = 
  rho (norm v1) * dihV (vec 0) v1 v2 v3 + rho(norm v2) * dihV (vec 0) v2 v3 v1 +
    rho(norm v3) * dihV (vec 0) v3 v1 v2 - (pi + sol0)`;;

(*
let standard = new_definition
` standard v w <=> &2 <= norm(v-w) /\  norm (v-w) <= &2 *h0 `;;

let protracted = new_definition
` protracted v w <=> &2 * h0 <= norm(v-w) /\  norm (v-w) <= sqrt (&8) `;;

let diagonal0 = new_definition
` (diagonal0 (E:(real^3->bool)->bool) (v:real^3) (w:real^3))  <=> ~(v = w) /\ ~({v,w} IN E) `;;

let diagonal1 = new_definition
` diagonal1 (V,E)  <=> !v w. ~(v = w) /\ v IN V /\ w IN V /\ ~({v,w} IN E)
==> &2 * h0 <= norm(v-w) `;;

let main_estimate= new_definition
` main_estimate(V,E,f) <=>
 convex_local_fan(V,E,f) /\
packing V /\
V SUBSET ball_annulus /\
diagonal1(V,E) /\
 CARD E= CARD f /\
3<= CARD E /\
CARD E <= 6`;;
*)

let torsor = new_definition
` torsor (s:A->bool) k (f:A->A) <=> (!x. x IN s ==> (f x) IN s) /\ (!x1 x2. x1 IN s /\ x2 IN s /\ f x1 = f x2 ==> x1=x2) /\ (!i x. 0 < i /\ i < k /\ x IN s ==> ~((f POWER i) x = x)) /\ (!x. x IN s==> (f POWER k) x = x)
/\ s HAS_SIZE k`;;

let tgt = new_definition `tgt = #1.541`;;

let d_tame = new_definition `d_tame n = 
     if n = 3 then &0 else 
     if n = 4 then #0.206 else
     if n = 5 then #0.4819 else 
     if n = 6 then #0.712 else tgt`;;  

(* rename d_tame2 -> tame_table_d, for compatibility with the rest of the project. *)

(*
let d_tame2 = new_definition `d_tame2 r s = 
  if (r + 2 * s <= 3) then &0
  else 
    #0.103 * (&2 - &s) + #0.2759 * (&r + &2 * &s - &4)`;;

let tame_table_d_tame2 = prove_by_refinement(
  `!r s. tame_table_d r s = if (r + 2 * s <= 3) then &0
  else 
    #0.103 * (&2 - &s) + #0.2759 * (&r + &2 * &s - &4)`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Sphere.tame_table_d];
  REPEAT WEAKER_STRIP_TAC;
  REWRITE_TAC[arith `r + 2 * s > 3 <=> ~(r + 2 * s <= 3)`];
  COND_CASES_TAC;
    BY(ASM_REWRITE_TAC[]);
  BY(REAL_ARITH_TAC)
  ]);;
  (* }}} *)
*)

let JEJTVGB_std_concl = 
  `!V E FF. 
    convex_local_fan (V,E,FF) /\
    packing V /\
    V SUBSET ball_annulus /\
    4 <= CARD V /\ CARD V <= 6 /\
    (!v w. ~(v = w) /\ v IN V /\ w IN V /\ ~({v,w} IN E) ==> &2 * h0 <= dist(v,w)) /\
    (!v w. {v,w} IN E ==> &2 <= dist(v,w) /\ dist(v,w) <= &2 * h0) ==>
    d_tame (CARD V) <= tau_fun V E FF`;;

let JEJTVGB_std3_concl = 
  `!v1 v2 v3. 
    &2 <= norm v1 /\
    &2 <= norm v2 /\
    &2 <= norm v3 /\
    norm v1 <= &2 * h0 /\
    norm v2 <= &2 * h0 /\
    norm v3 <= &2 * h0 /\
    &2 <= dist(v1,v2) /\
    &2 <= dist(v1,v3) /\
    &2 <= dist(v2,v3) /\
    dist(v1,v2) <= &2 * h0 /\
    dist(v1,v3) <= &2 * h0 /\
    dist(v2,v3) <= &2 * h0 ==>
    &0 <= tau3 v1 v2 v3`;;

let JEJTVGB_pent_diag_concl = 
  `!V E FF. 
    convex_local_fan (V,E,FF) /\
    packing V /\
    V SUBSET ball_annulus /\
    CARD V = 5 /\
    (!v w. ~(v = w) /\ v IN V /\ w IN V /\ ~({v,w} IN E) ==> sqrt8 <= dist(v,w)) /\
    (!v w. {v,w} IN E ==> &2 <= dist(v,w) /\ dist(v,w) <= &2 * h0) ==>
    #0.616 <= tau_fun V E FF`;;

let JEJTVGB_pent_pro_concl = 
  `!V E FF. 
    convex_local_fan (V,E,FF) /\
    packing V /\
    V SUBSET ball_annulus /\
    CARD V = 5 /\ 
    (!v w. ~(v = w) /\ v IN V /\ w IN V /\ ~({v,w} IN E) ==> &2 * h0 <= dist(v,w)) /\
    (?v0 w0.
       (!v w. {v,w} IN E /\ ~({v,w} = {v0,w0}) ==> &2 <= dist(v,w) /\ dist(v,w) <= &2 * h0) /\
       {v0,w0} IN E /\
       &2 *h0 <= dist(v0,w0) /\ dist(v0,w0) <= sqrt8)
     ==>
    #0.616 <= tau_fun V E FF`;;

let JEJTVGB_quad_pro_concl = 
  `!V E FF. 
    convex_local_fan (V,E,FF) /\
    packing V /\
    V SUBSET ball_annulus /\
    CARD V = 4 /\ 
    (!v w. ~(v = w) /\ v IN V /\ w IN V /\ ~({v,w} IN E) ==> sqrt8 <= dist(v,w)) /\
    (?v0 w0.
       (!v w. {v,w} IN E /\ ~({v,w} = {v0,w0}) ==> &2 <= dist(v,w) /\ dist(v,w) <= &2 * h0) /\
       {v0,w0} IN E /\
       &2 *h0 <= dist(v0,w0) /\ dist(v0,w0) <= sqrt8)
     ==>
    #0.477 <= tau_fun V E FF`;;

let JEJTVGB_quad_diag_concl = 
  `!V E FF. 
    convex_local_fan (V,E,FF) /\
    packing V /\
    V SUBSET ball_annulus /\
    CARD V = 4 /\
    (!v w. ~(v = w) /\ v IN V /\ w IN V /\ ~({v,w} IN E) ==> &3 <= dist(v,w)) /\
    (!v w. {v,w} IN E ==> &2 <= dist(v,w) /\ dist(v,w) <= &2 * h0) ==>
    #0.467 <= tau_fun V E FF`;;

let JEJTVGB_concl = 
  let co = [JEJTVGB_std_concl;JEJTVGB_std3_concl;JEJTVGB_pent_diag_concl;JEJTVGB_pent_pro_concl;
				      JEJTVGB_quad_pro_concl;JEJTVGB_quad_diag_concl] in
    list_mk_conj co;;

let JEJTVGB_assume_v34 =  new_definition (mk_eq (`JEJTVGB_assume_v34:bool`,JEJTVGB_concl));;

(* I'll use periodic functions to SCSs for now *)

let peropp = new_definition `peropp (f:num->A) k i = f (k - SUC(i MOD k))`;;

let peropp2 = new_definition `peropp2 (f:num->num->A) k i j = f (k - SUC(i MOD k)) (k - SUC(j MOD k))`;;

let peropp_periodic = prove_by_refinement(
  `!(f:num->A) k.      periodic (peropp f k) k`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  REWRITE_TAC[Oxl_def.periodic;peropp];
  ONCE_REWRITE_TAC[arith `i + k = 1 * k + i`];
  BY(REWRITE_TAC[MOD_MULT_ADD])
  ]);;
  (* }}} *)

let scs_data = `(k:num,d:real,a,alpha,beta,b,J,lo,hi,str)`;;

let scs_exists =  MESON[]   `?(x: ( 
				 (num) #
				 (real) #
				   (num->num -> real) #
				   (num->num -> real) #
				   (num-> num-> real) #
				   (num -> num -> real) #
				   (num -> num -> bool) #
				   (num -> bool) #
				   (num -> bool) #
				   (num -> bool))). T`;;

let scs_v34 = REWRITE_RULE[] (new_type_definition "scs_v34" ("scs_v34", "dest_scs_v34") scs_exists);;

let scs_k_v34 = new_definition `scs_k_v34 s = part0 (dest_scs_v34 s)`;;
let scs_d_v34 = new_definition `scs_d_v34 s = part1 (dest_scs_v34 s)`;;

let scs_a_v34 = new_definition `scs_a_v34 s = part2 (dest_scs_v34 s)`;;
let scs_am_v34 = new_definition `scs_am_v34 s = part3 (dest_scs_v34 s)`;;
let scs_bm_v34 = new_definition `scs_bm_v34 s = part4 (dest_scs_v34 s)`;;
let scs_b_v34 = new_definition `scs_b_v34 s = part5 (dest_scs_v34 s)`;;

let scs_J_v34 = new_definition `scs_J_v34 s = part6 (dest_scs_v34 s)`;;
let scs_lo_v34 = new_definition `scs_lo_v34 s = part7 (dest_scs_v34 s)`;;
let scs_hi_v34 = new_definition `scs_hi_v34 s = part8 (dest_scs_v34 s)`;;
let scs_str_v34 = new_definition `scs_str_v34 s = SND(drop3(drop3 (dest_scs_v34 s)))`;;

let scs_k_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_k_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str)) =  k`,
  (* {{{ proof *)
  [
    BY(REWRITE_TAC[scs_k_v34;scs_v34;Misc_defs_and_lemmas.part0])
  ]);;
  (* }}} *)

let scs_d_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_d_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str)) = d`,
  (* {{{ proof *)
  [
    BY(REWRITE_TAC[scs_d_v34;scs_v34;Misc_defs_and_lemmas.part1;Misc_defs_and_lemmas.drop0;FST])
  ]);;
  (* }}} *)

let scs_a_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_a_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str)) = a`,
  (* {{{ proof *)
  [
  BY((REWRITE_TAC[scs_a_v34;scs_v34;Misc_defs_and_lemmas.part2;Misc_defs_and_lemmas.part1;Misc_defs_and_lemmas.drop1;FST]))
  ]);;
  (* }}} *)

let scs_am_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_am_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = alpha`,
  (* {{{ proof *)
  [
  BY((REWRITE_TAC[scs_am_v34;scs_v34;Misc_defs_and_lemmas.part3;Misc_defs_and_lemmas.drop2;FST]))
  ]);;
  (* }}} *)

let scs_bm_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_bm_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = beta`,
  (* {{{ proof *)
  [
  ((REWRITE_TAC[scs_bm_v34;scs_v34;Misc_defs_and_lemmas.part4;Misc_defs_and_lemmas.drop3;FST]))
  ]);;
  (* }}} *)

let scs_b_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_b_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = b`,
  (* {{{ proof *)
  [
  ((REWRITE_TAC[scs_b_v34;scs_v34;Misc_defs_and_lemmas.part5;Misc_defs_and_lemmas.drop3;FST]))
  ]);;
  (* }}} *)

let scs_J_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_J_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = J`,
  (* {{{ proof *)
  [
  ((REWRITE_TAC[scs_J_v34;scs_v34;Misc_defs_and_lemmas.part6;Misc_defs_and_lemmas.drop3;FST]))
  ]);;
  (* }}} *)

let scs_lo_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_lo_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = lo`,
  (* {{{ proof *)
  [
  ((REWRITE_TAC[scs_lo_v34;scs_v34;Misc_defs_and_lemmas.part7;Misc_defs_and_lemmas.drop3;FST]))
  ]);;
  (* }}} *)

let scs_hi_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_hi_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = hi`,
  (* {{{ proof *)
  [
  ((REWRITE_TAC[scs_hi_v34;scs_v34;Misc_defs_and_lemmas.part8;Misc_defs_and_lemmas.drop3;FST]))
  ]);;
  (* }}} *)

let scs_str_v34_explicit = prove_by_refinement(
  `!k d a b alpha beta J lo str. scs_str_v34 (scs_v34 (k,d,a,alpha,beta,b,J,lo,hi,str))  = str`,
  (* {{{ proof *)
  [
  ((REWRITE_TAC[scs_str_v34;scs_v34;Misc_defs_and_lemmas.part7;Misc_defs_and_lemmas.drop3;FST]))
  ]);;
  (* }}} *)

let scs_v34_explicit = end_itlist CONJ 
  [scs_k_v34_explicit;scs_d_v34_explicit;
   scs_a_v34_explicit;scs_am_v34_explicit;
   scs_b_v34_explicit;scs_bm_v34_explicit;
   scs_J_v34_explicit;scs_lo_v34_explicit;
   scs_hi_v34_explicit;
   scs_str_v34_explicit];;

let periodic2 = new_definition `periodic2 (f:num->num->A) k = (!i j. f (i + k) j = f i j /\ f i (j + k) = f i j)`;;

(* This doesn't specify the form of : symmetric?
   Let's make it symmetric periodic.  is_ear_v... *)

let is_scs_v34 = new_definition `is_scs_v34 s = (
  scs_d_v34 s < #0.9 /\
    3 <= scs_k_v34 s /\
    scs_k_v34 s <= 6 /\
    periodic (scs_lo_v34 s) (scs_k_v34 s) /\
    periodic (scs_hi_v34 s) (scs_k_v34 s) /\
    periodic (scs_str_v34 s) (scs_k_v34 s) /\
    periodic (scs_str_v34 s) (scs_k_v34 s) /\
    periodic2 (scs_a_v34 s) (scs_k_v34 s) /\
    periodic2 (scs_am_v34 s) (scs_k_v34 s) /\
    periodic2 (scs_bm_v34 s) (scs_k_v34 s) /\
    periodic2 (scs_b_v34 s) (scs_k_v34 s) /\
    periodic2 (scs_J_v34 s) (scs_k_v34 s) /\
    (!i j. (scs_a_v34 s i j = scs_a_v34 s j i /\ scs_am_v34 s i j = scs_am_v34 s j i /\ 
	 scs_bm_v34 s i j = scs_bm_v34 s j i /\ scs_b_v34 s i j = scs_b_v34 s j i /\
	   scs_J_v34 s i j = scs_J_v34 s j i)) /\
    (!i j. scs_a_v34 s i j <= scs_am_v34 s i j /\ scs_am_v34 s i j <= scs_bm_v34 s i j /\ 
       scs_bm_v34 s i j <= scs_b_v34 s i j) /\
    (!i. scs_a_v34 s i i = &0) /\
    (!i j. (i < scs_k_v34 s) /\ (j < scs_k_v34 s) /\ ~(i = j) ==> &2 <= scs_a_v34 s i j) /\
    (!i. (scs_k_v34 s = 3) ==> (scs_b_v34 s i (SUC i) < &4)) /\
    (!i. (3 < scs_k_v34 s) ==> (scs_b_v34 s i (SUC i) <= cstab)) /\
    (!i j. scs_J_v34 s i j ==> ((j MOD scs_k_v34 s = (SUC i) MOD scs_k_v34 s) \/
			       ((i MOD scs_k_v34 s = (SUC j) MOD scs_k_v34 s)))) /\
    (!i j. scs_J_v34 s i j ==> scs_a_v34 s i j = sqrt8 /\ scs_b_v34 s i j = cstab) /\ 
    CARD { i | i < scs_k_v34 s /\ (&2 * h0 < scs_b_v34 s i (SUC i) \/ &2 < scs_a_v34 s i (SUC i)) } + scs_k_v34 s <= 6)`;;

(*
*)

let unadorned_v34 = new_definition `unadorned_v34 s = 
  (scs_lo_v34 s = {} /\ scs_hi_v34 s = {} /\ scs_str_v34 s = {} /\ scs_a_v34 s = scs_am_v34 s /\ scs_b_v34 s = scs_bm_v34 s)`;;

let periodic_empty = prove_by_refinement(
  `!n. periodic {} n`,
  (* {{{ proof *)
  [
  REWRITE_TAC[Oxl_def.periodic];
  BY(ASM_MESON_TAC[NOT_IN_EMPTY;IN])
  ]);;
  (* }}} *)

let scs_unadorned = prove_by_refinement(
  `!s. is_scs_v34 s ==>
    is_scs_v34 (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_a_v34 s,scs_b_v34 s,scs_b_v34 s,scs_J_v34 s,{},{},{}))`,
  (* {{{ proof *)
  [
  REWRITE_TAC[is_scs_v34;scs_v34_explicit];
  REPEAT WEAKER_STRIP_TAC;
  ASM_REWRITE_TAC[arith `x <= x`;periodic_empty];
  BY(ASM_MESON_TAC[arith `x <= y /\ y <= z ==> x <= z`])
  ]);;
  (* }}} *)

let is_ear_v34 = new_definition `is_ear_v34 s <=> 
  is_scs_v34 s /\
    unadorned_v34 s /\
    scs_k_v34 s = 3 /\
  scs_d_v34 s = #0.11 /\
  (!i. scs_b_v34 s i i = &0) /\
  (?i. {i | i < 3 /\ scs_J_v34 s i (SUC i)} = {i} /\
      scs_a_v34 s i (SUC i) = sqrt8 /\ scs_b_v34 s i (SUC i) = cstab /\
      (!j. j < 3 /\ ~(j = i) ==> scs_a_v34 s j (SUC j) = &2 /\ scs_b_v34 s j (SUC j) = &2 * h0))`;;
      
let BBs_v34 = new_definition `BBs_v34 s vv =
  (let V = IMAGE vv (:num) in
  let E = IMAGE (\i. {vv i, vv (SUC i)}) (:num) in
  let FF = IMAGE (\i. (vv i,vv (SUC i))) (:num) in
    (V SUBSET ball_annulus /\
       periodic vv (scs_k_v34 s) /\
       (!i j. scs_a_v34 s i j <= dist(vv i,vv j) /\ dist(vv i,vv j) <= scs_b_v34 s i j) /\ 
       (scs_k_v34 s <= 3  \/ (convex_local_fan (V,E,FF)))))`;;

let dsv_v34 = new_definition `dsv_v34 s (vv:num->real^3) = 
  scs_d_v34 s + #0.1 * (if (is_ear_v34 s) then &1 else -- &1) * 
    sum {i | i < scs_k_v34 s /\ scs_J_v34 s i (SUC i)} (\i. cstab - dist(vv i,vv (SUC i)))`;;

let dsv_J_empty = prove_by_refinement(
  `!s vv. scs_J_v34 s = (\i j. F) ==> dsv_v34 s vv = scs_d_v34 s`,
  (* {{{ proof *)
  [
  REPEAT WEAKER_STRIP_TAC;
  ASM_REWRITE_TAC[dsv_v34;EMPTY_GSPEC;SUM_CLAUSES];
  BY(REAL_ARITH_TAC)
  ]);;
  (* }}} *)

let taustar_v34 = new_definition `taustar_v34 s vv = 
  (let V = IMAGE vv (:num) in
  let E = IMAGE (\i. {vv i, vv (SUC i)}) (:num) in
  let FF = IMAGE (\i. (vv i,vv (SUC i))) (:num) in
    (if (scs_k_v34 s <= 3) then tau3 (vv 0) (vv 1) (vv 2) - dsv_v34 s vv
  else tau_fun V E FF - dsv_v34 s vv))`;;

let mk_unadorned_v34 = new_definition `mk_unadorned_v34 k d a b = 
  scs_v34 (k,d,a,a,b,b,(\i j. F),{},{},{})`;;

let cs_adj = new_definition `cs_adj k a1 a2 i j = 
  (if (i MOD k = j MOD k) then &0
			       else (if (j MOD k = SUC i MOD k) \/ (SUC j MOD k = i MOD k) then a1 else a2))`;;

let psort = new_definition `psort k (u:num#num) = 
  (let i = FST u MOD k in
  let j = SND u MOD k in
    if (i <= j) then (i,j) else (j,i))`;;

(*
let ASSOCD = new_definition` ASSOCD (a:A) hs (d:D) = 
  if hs = [] then d else ASSOC a hs`;;
*)

let ASSOCD_v34 = new_recursive_definition list_RECURSION
  `ASSOCD_v34 (a:A) [] (d:D) = d /\
   ASSOCD_v34 a (CONS h t) d = if (a = FST h) then SND h else ASSOCD_v34 a t d`;;

let funlist_v34 = new_definition `funlist_v34 data d k i j =
  (let data' = MAP (\ (u, d). (psort k u,d)) data in
    if (i MOD k = j MOD k) then (&0) 
    else ASSOCD_v34 (psort k (i,j)) data' d)`;;

let funlistA_v34 = new_definition `funlistA_v34 data (diag:A) default k i j =
  (let data' = MAP (\ (u, d). (psort k u,d)) data in
    if (i MOD k = j MOD k) then diag
    else ASSOCD_v34 (psort k (i,j)) data' default)`;;

let override = new_definition `override a k u d i j = 
     if psort k u = psort k (i,j) then (d:A) else a i j`;;
    
let s_init_list_v34 = new_definition `s_init_list_v34 = 
  let upperbd = &6 in
  let a_pro = (\k p a1 a2 i j. 
		 (if (i MOD k = j MOD k) then &0
		  else (if {(i MOD k),(j MOD k)}={0,1} then p
			else (if (j MOD k = SUC i MOD k) \/ (SUC j MOD k = i MOD k) then a1 else a2)))) in
    [mk_unadorned_v34 6 (d_tame 6) (cs_adj 6 (&2) (&2 * h0)) (cs_adj 6 (&2 * h0) upperbd);
     mk_unadorned_v34 5 (d_tame 5) (cs_adj 5 (&2) (&2 * h0)) (cs_adj 5 (&2 * h0) upperbd);
     mk_unadorned_v34 4 (d_tame 4) (cs_adj 4 (&2) (&2 * h0)) (cs_adj 4 (&2 * h0) upperbd);
     mk_unadorned_v34 3 (d_tame 3) (cs_adj 3 (&2) (&2 * h0)) (cs_adj 3 (&2 * h0) upperbd);
     mk_unadorned_v34 5 (#0.616) (cs_adj 5 (&2) (sqrt8)) (cs_adj 5 (&2 * h0) upperbd);     
     mk_unadorned_v34 4 (#0.616) (cs_adj 4 (&2) (&3)) (cs_adj 4 (&2 * h0) upperbd);     
     mk_unadorned_v34 5 (#0.616) (a_pro 5 (&2 * h0) (&2) (&2 * h0)) (a_pro 5 sqrt8 (&2 * h0) upperbd);
     mk_unadorned_v34 4 (#0.477) (a_pro 4 (&2 * h0) (&2) sqrt8) (a_pro 4 sqrt8 (&2 * h0) upperbd)
    ]`;;

let LENGTH_s_init_list = prove_by_refinement(
  `LENGTH s_init_list_v34 = 8`,
  (* {{{ proof *)
  [
  REWRITE_TAC[s_init_list_v34;LET_DEF;LET_END_DEF;LENGTH];
  BY(ARITH_TAC)
  ]);;
  (* }}} *)

let terminal_hex = 
  `mk_unadorned_v34 6 (d_tame 6) (cs_adj 6 (&2) (cstab)) (cs_adj 6 (&2) (&6))`;;

let terminal_pent = 
  `mk_unadorned_v34 5 (#0.616) (cs_adj 5 (&2) (cstab)) (cs_adj 5 (&2) (&6))`;;

let terminal_pent_lo2 = `scs_v34 (5, #0.616,
				   cs_adj 5 (&2) cstab,
				   cs_adj 5 (&2) cstab,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 (\i j.  F),(\i. (i MOD 5) IN {1,4}),{},{})`;;

let terminal_pent_hi2 = `scs_v34 (5, #0.616,
				   cs_adj 5 (&2) cstab,
				   cs_adj 5 (&2) cstab,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 (\i j.  F),{},(\i. (i MOD 5) IN {1,4}),{})`;;

let terminal_pent_str2 = `scs_v34 (5, #0.616,
				   cs_adj 5 (&2) cstab,
				   cs_adj 5 (&2) cstab,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 (\i j.  F),{},{},(\i. (i MOD 5) IN {1,4}))`;;

let terminal_pent_lo_hi = `scs_v34 (5, #0.616,
				   cs_adj 5 (&2) cstab,
				   cs_adj 5 (&2) cstab,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 (\i j.  F),(\i. 1 = (i MOD 5)),(\i. 4 = (i MOD 5)),{})`;;

let terminal_pent_lo_str = `scs_v34 (5, #0.616,
				   cs_adj 5 (&2) cstab,
				   cs_adj 5 (&2) cstab,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 (\i j.  F),(\i. 1 = (i MOD 5)),{},(\i. 4 = (i MOD 5)))`;;

let terminal_pent_hi_str = `scs_v34 (5, #0.616,
				   cs_adj 5 (&2) cstab,
				   cs_adj 5 (&2) cstab,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 funlist_v34 [(0,2),#3.23607;(0,3),#3.23607;(1,3),(&6);(1,4),(&6);(2,4),(&6)] (&2*h0) 5,
 (\i j.  F),{},(\i. 1 = (i MOD 5)),(\i. 4 = (i MOD 5)))`;;

let terminal_adhoc_quad_5691615370 = 
  `mk_unadorned_v34   4    #0.467    (cs_adj 4 (&2) (&3))
    (cs_adj 4 (&2) (&6))`;;

let terminal_adhoc_quad_9563139965B = 
  `mk_unadorned_v34 4  (#0.467) (cs_adj 4 (&2) (&3)) (cs_adj 4 (&2*h0) (&3))`;;

let terminal_adhoc_quad_4680581274 = 
  `mk_unadorned_v34  4  (#0.616 - #0.11)  (funlist_v34 [(0,1),cstab ; (0,2),cstab ; (1,3),cstab ] (&2) 4)
 (funlist_v34 [(0,1),cstab ; (0,2),(&6) ; (1,3),(&6) ] (&2) 4)`;;

let terminal_adhoc_quad_7697147739 = 
  `mk_unadorned_v34 4  (#0.616 - #0.11)  (funlist_v34 [(0,1),sqrt8 ; (0,2),cstab ; (1,3),cstab ] (&2) 4) 
 (funlist_v34 [(0,1),sqrt8 ; (0,2),(&6) ; (1,3),(&6) ] (&2) 4)`;;

let terminal_tri_7720405539 = `mk_unadorned_v34
 3
 (#0.5518 / #2.0 - #0.2)
 (funlist_v34 [(0,1),cstab; (0,2), (&2 * h0); (1,2),(&2)] (&2) 3)
 (funlist_v34 [(0,1),#3.41; (0,2), (&2 * h0); (1,2),(&2)] (&2) 3)`;;
 
let terminal_tri_2739661360 = `mk_unadorned_v34
 3
 (#0.5518 / #2.0 + #0.2)
 (funlist_v34 [(0,1),cstab; (0,2),cstab; (1,2),(&2)] (&2) 3)
 (funlist_v34 [(0,1), #3.41; (0,2),cstab; (1,2),(&2)] (&2) 3)`;;
 
let terminal_tri_9269152105 = `mk_unadorned_v34
 3
 (#0.5518 / #2.0)
 (funlist_v34 [(0,1),cstab; (0,2),cstab; (1,2),(&2)] (&2) 3)
 (funlist_v34 [(0,1),#3.62; (0,2),cstab; (1,2),(&2)] (&2) 3)`;;

let terminal_tri_4922521904 = `mk_unadorned_v34
 3
 (#0.5518 / #2.0)
 (funlist_v34 [(0,1),cstab; (0,2),(&2)*h0; (1,2),(&2)] (&2) 3)
 (funlist_v34 [(0,1),#3.339; (0,2),(&2)*h0; (1,2),(&2)] (&2) 3)`;;


let ear_cs = 
  `scs_v34 (3, #0.11,
 funlist_v34 [(0,1),sqrt8] (&2) 3,
 funlist_v34 [(0,1),sqrt8] (&2) 3,
 funlist_v34 [(0,1),cstab] ((&2)*h0) 3,
 funlist_v34 [(0,1),cstab] (&2*h0) 3,
 funlistA_v34 [(0,1),T] F F 3,{},{},{})`;;

let ear_jnull = `scs_v34 (3, #0.11,
 funlist_v34 [(0,1),sqrt8] (&2) 3,
 funlist_v34 [(0,1),sqrt8] (&2) 3,
 funlist_v34 [(0,1),cstab] (&2*h0) 3,
 funlist_v34 [(0,1),cstab] ((&2)*h0) 3,
 (\i j.  F),{},{},{})`;;

let ear_stab = `mk_unadorned_v34 
  3
//  (#0.11 + #0.1 *(cstab - sqrt8))  corrected 2013-4-20, see check_completeness djz.
  (#0.11)
  (funlist_v34 [(0,2),cstab] (&2) 3)
  (funlist_v34 [(0,2),cstab] (&2*h0) 3)`;;

let terminal_ear_3603097872 = ear_cs;;

let terminal_tri_5405130650 = `scs_v34(
 3,
  #0.477 -  #0.11,
 funlist_v34 [(0,1),sqrt8;(0,2),(&2*h0);(1,2),(&2)] (&2) 3,
 funlist_v34 [(0,1),sqrt8;(0,2),(&2*h0);(1,2),(&2)] (&2) 3,
 funlist_v34 [(0,1),cstab;(0,2),sqrt8;(1,2),(&2*h0)] (&2*h0) 3,
 funlist_v34 [(0,1),cstab;(0,2),sqrt8;(1,2),(&2*h0)] (&2*h0) 3,
 funlistA_v34 [(0,1),T] F F 3,{},{},{})`;;


let terminal_tri_5766053833 = `scs_v34(
 3,
  #0.696 - #2.0 * #0.11,
 funlist_v34 [(0,1),sqrt8;(1,2),sqrt8] (&2) 3,
 funlist_v34 [(0,1),sqrt8;(1,2),sqrt8] (&2) 3,
 funlist_v34 [(0,1),cstab;(1,2),cstab] (&2) 3,
 funlist_v34 [(0,1),cstab;(1,2),cstab] (&2) 3,
  funlistA_v34 [(0,1),T;(1,2),T] F F 3,{},{},{})`;;

let terminal_tri_5026777310a = `mk_unadorned_v34 
 3
  (#0.6548 - #2.0 * #0.11)
  (funlist_v34 [(0,1),sqrt8;(1,2),sqrt8] (&2) 3)
  (funlist_v34 [(0,1),cstab;(1,2),cstab] (&2*h0) 3)`;;

let terminal_tri_7881254908 = `mk_unadorned_v34 
 3
  (#0.696 - #2.0 * #0.11)
 (funlist_v34 [(0,1),sqrt8;(1,2),sqrt8] (&2*h0) 3)
 (funlist_v34 [(0,1),cstab;(1,2),cstab] (&2*h0) 3)`;;

let terminal_std_tri_OMKYNLT_2_1  = `mk_unadorned_v34 
 3
  (tame_table_d 2 1)
 (funlist_v34 [(0,1),(&2*h0)] (&2) 3)
 (funlist_v34 [(0,1),(&2*h0)] (&2) 3)`;;

let terminal_std_tri_7645170609 = `mk_unadorned_v34 
 3
  (tame_table_d 2 1)
 (funlist_v34 [(0,1),sqrt8] (&2) 3)
 (funlist_v34 [(0,1),sqrt8] (&2) 3)`;;

(* 1532755966 *)
let terminal_std_tri_OMKYNLT_1_2  = `mk_unadorned_v34 
 3
  (tame_table_d 1 2)
 (funlist_v34 [(0,1),(&2)] (&2*h0) 3)
 (funlist_v34 [(0,1),(&2)] (&2*h0) 3)`;;

let terminal_std_tri_7097350062a = `mk_unadorned_v34 
 3
  (tame_table_d 1 2)
 (funlist_v34 [(0,1),(&2*h0);(0,2),sqrt8] (&2) 3)
 (funlist_v34 [(0,1),(&2*h0);(0,2),sqrt8] (&2) 3)`;;

let terminal_std_tri_2900061606 = `mk_unadorned_v34 
 3
  (tame_table_d 1 2 + (tame_table_d 2 1 - #0.11))
 (funlist_v34 [(0,1),(&2*h0);(0,2),cstab] (&2) 3)
 (funlist_v34 [(0,1),(&2*h0);(0,2),cstab] (&2) 3)`;;

let terminal_std_tri_2200527225 = `mk_unadorned_v34 
 3
  (tame_table_d 1 2 + #2.0* (tame_table_d 2 1 - #0.11))
 (funlist_v34 [(0,1),(&2)] sqrt8 3)
 (funlist_v34 [(0,1),(&2)] sqrt8 3)`;;

let terminal_std_tri_3106201101 = `mk_unadorned_v34 
 3
  (tame_table_d 1 2 + #2.0* (tame_table_d 2 1 - #0.11))
 (funlist_v34 [(0,1),(&2);(0,2),cstab] sqrt8 3)
 (funlist_v34 [(0,1),(&2);(0,2),cstab] sqrt8 3)`;;

let terminal_std_tri_9816718044 = `mk_unadorned_v34 
 3
  (tame_table_d 1 2 + #2.0* (tame_table_d 2 1 - #0.11))
 (funlist_v34 [(0,1),(&2)] cstab 3)
 (funlist_v34 [(0,1),(&2)] cstab 3)`;;

let terminal_std_tri_1080462150 = `mk_unadorned_v34 
 3
  (tame_table_d 0 3 + #3.0 *(tame_table_d 2 1 - #0.11))
 (funlist_v34 [] (&2*h0) 3)
 (funlist_v34 [] (&2*h0) 3)`;;

let terminal_std_tri_4143829594 = `mk_unadorned_v34 
 3
  (tame_table_d 0 3 + #3.0 *(tame_table_d 2 1 - #0.11))
 (funlist_v34 [(0,1),cstab] (&2*h0) 3)
 (funlist_v34 [(0,1),cstab] (&2*h0) 3)`;;

let terminal_std_tri_7459553847 = `mk_unadorned_v34 
 3
  (tame_table_d 0 3 + #3.0 *(tame_table_d 2 1 - #0.11))
 (funlist_v34 [(0,1),(&2*h0)] cstab 3)
 (funlist_v34 [(0,1),(&2*h0)] cstab 3)`;;

let terminal_std_tri_4528012043 = `mk_unadorned_v34 
 3
  (tame_table_d 0 3 + #3.0 *(tame_table_d 2 1 - #0.11))
 (funlist_v34 [] cstab 3)
 (funlist_v34 [] cstab 3)`;;

let terminal_std_tri_OMKYNLT_3336871894 = `mk_unadorned_v34
 3
 (&0)
 (funlist_v34 [] (&2) 3)
 (funlist_v34 [] (&2) 3)`;;

let terminal_cs = [
 terminal_hex;
 terminal_pent;
 terminal_adhoc_quad_5691615370; 
 terminal_adhoc_quad_9563139965B; 
 terminal_adhoc_quad_4680581274; 
 terminal_adhoc_quad_7697147739; 
 terminal_tri_7720405539; 
 terminal_tri_2739661360; 
 terminal_tri_9269152105; 
 terminal_tri_4922521904; 
 terminal_ear_3603097872; 
 ear_jnull; 
 ear_stab;
 terminal_tri_5405130650; 
 terminal_tri_5766053833; 
 terminal_tri_5026777310a; 
 terminal_tri_7881254908; 
 terminal_std_tri_OMKYNLT_2_1 ; (* 1107929058 *)
 terminal_std_tri_7645170609; 
 terminal_std_tri_OMKYNLT_1_2 ; (* 1532755966 *)
 terminal_std_tri_7097350062a; 
 terminal_std_tri_2900061606; 
 terminal_std_tri_2200527225; 
 terminal_std_tri_3106201101; 
 terminal_std_tri_9816718044; 
 terminal_std_tri_1080462150; 
 terminal_std_tri_4143829594;
 terminal_std_tri_7459553847;
 terminal_std_tri_4528012043;
 terminal_std_tri_OMKYNLT_3336871894; 
];;

let scs_terminal_v34 = new_definition (mk_eq (`scs_terminal_v34:(scs_v34)list`,(mk_flist terminal_cs)));;

let ZITHLQN_concl =  `(!s vv. MEM s  s_init_list_v34 /\ vv IN
			 BBs_v34 s ==> 
			 &0 <= taustar_v34 s vv)  ==> JEJTVGB_assume_v34`;;

(*
let ZITHLQN = prove_by_refinement(
  `(!s vv. s IN set_of_list s_init_list_v34 /\ vv IN BBs_v34 s ==> &0 <= taustar_v34 s vv)  ==> JEJTVGB_assume_v34`,
  (* {{{ proof *)
  [
  rt[s_init_list_v34;set_of_list;JEJTVGB_assume_v34]
st/r
conj
st/r
rule (rr [taustar_v34])
typ `CARD V = 3 \/ CARD V = 4 \/ CARD V = 5 \/ CARD V = 6` (C gthen assume)
repeat (fxast `CARD` mp) then ARITH_TAC
  ]);;
  (* }}} *)
*)

let BBprime_v34 = new_definition `BBprime_v34 s vv = (BBs_v34 s vv /\ 
  (!ww. BBs_v34 s ww ==> taustar_v34 s vv <= taustar_v34 s ww) /\ taustar_v34 s vv < &0)`;;

let BBindex_v34 = new_definition `BBindex_v34 s (vv:num->real^3) = 
  CARD { i | i < scs_k_v34 s /\ scs_a_v34 s i (SUC i) = dist(vv i, vv (SUC i)) }`;;

let BBindex_min_v34 = new_definition `BBindex_min_v34 s = 
  min_num (IMAGE (BBindex_v34 s) (BBprime_v34 s))`;;

let BBprime2_v34 = new_definition `BBprime2_v34 s vv <=> 
  BBprime_v34 s vv /\ BBindex_v34 s vv = BBindex_min_v34 s`;;

let MMs_v34 = new_definition `MMs_v34 s vv <=>
  BBprime2_v34 s vv /\ 
  (!i. scs_str_v34 s i ==> &0 = azim (vec 0) (vv i) (vv (SUC i)) (vv (i + (scs_k_v34 s - 1))  )) /\
  (!i. scs_lo_v34 s i ==> norm (vv i) = &2) /\
  (!i. scs_hi_v34 s i ==> norm (vv i) = &2 * h0) /\
  (!i j. scs_am_v34 s i j <= dist(vv i,vv j)) /\
  (!i j. dist(vv i,vv j) <= scs_bm_v34 s i j)`;;

let unadorned_MMs_concl = `!s.
  unadorned_v34 s ==> (MMs_v34 s = BBprime2_v34 s)`;;
  
let XWITCCN_concl = `!s vv. MEM s s_init_list_v34 /\ vv IN BBs_v34 s /\
  taustar_v34 s vv < &0 ==> ~(BBprime_v34 s = {})`;;

let XWITCCN2_concl = `!s vv. MEM s s_init_list_v34 /\ vv IN BBs_v34 s /\
  taustar_v34 s vv < &0 ==> ~(BBprime2_v34 s = {})`;;

let AYQJTMD_concl = `!s vv. MEM s s_init_list_v34 /\ vv IN BBs_v34 s /\
  taustar_v34 s vv < &0 ==> ~(MMs_v34 s = {})`;;
  
let EAPGLE_concl =  `(!s. MEM s s_init_list_v34 ==> MMs_v34 s = {}) ==> JEJTVGB_assume_v34`;;

let JKQEWGV1_concl = `!s vv. is_scs_v34 s /\ BBs_v34 s vv /\ taustar_v34 s vv < &0 /\
  3 < scs_k_v34 s ==>
  (sol_local (IMAGE (\i. {vv i, vv (SUC i)}) (:num))
   (IMAGE (\i. (vv i,vv (SUC i))) (:num))  < pi)`;;

let JKQEWGV2_concl = `!s vv. is_scs_v34 s /\ BBs_v34 s vv /\ taustar_v34 s vv < &0 /\
  3 < scs_k_v34 s ==>
  (
    let V = IMAGE vv (:num) in
  let E = IMAGE (\i. {vv i, vv (SUC i)}) (:num) in
  ~circular V E)`;;

let JKQEWGV3_concl = `!s vv v w. 
  (let V = IMAGE vv (:num) in
   let E = IMAGE (\i. {vv i, vv (SUC i)}) (:num) in
   let FF = IMAGE (\i. (vv i,vv (SUC i))) (:num) in
     (is_scs_v34 s /\ BBs_v34 s vv /\ taustar_v34 s vv < &0 /\ lunar (v,w) V E /\
      3 < scs_k_v34 s ==>
	(interior_angle1 (vec 0) FF v  < pi / &2 )))`;;

let HFNXPZA_concl = `!s vv.
  is_scs_v34 s /\ BBs_v34 s vv /\ taustar_v34 s vv < &0 /\ scs_k_v34 s = 3 ==>
  dihV (vec 0) (vv 0) (vv 1) (vv 2) +
  dihV (vec 0) (vv 1) (vv 2) (vv 3) +
  dihV (vec 0) (vv 2) (vv 3) (vv 1) < &2 * pi`;;

let restriction_typ1_v34 = new_definition `restriction_typ1_v34 s = 
   (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,scs_bm_v34 s,scs_bm_v34 s,scs_J_v34 s,scs_lo_v34 s,scs_hi_v34 s,scs_str_v34 s))`;;

let restriction_typ2_v34 = new_definition `restriction_typ2_v34 s =
   (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_am_v34 s,scs_am_v34 s,
	    scs_am_v34 s,scs_am_v34 s,scs_J_v34 s,scs_lo_v34 s,scs_hi_v34 s,scs_str_v34 s))`;;

(* DEPRECATED 
let restriction_typ3_v34 = new_definition `restriction_typ3_v34 s i j a b =
  ((a <= scs_bm_v34 s i j  /\ scs_am_v34 s i j <= b),
   (let k = scs_k_v34 s in
   let i' =  i MOD k in
   let j' = j MOD k in
   let f = (\i'' j''. { i'' MOD k, j'' MOD k } = {i',j'}) in
   let a1 = (\i'' j''. if f i'' j'' then a else scs_a_v34 s i j) in
   let b1 = (\i'' j''. if f i'' j'' then b else scs_b_v34 s i j) in
   let am = (\i'' j''. if f i'' j'' then max a (scs_am_v34 s i j) else scs_am_v34 s i j) in
   let bm = (\i'' j''. if f i'' j'' then min b (scs_bm_v34 s i j) else scs_bm_v34 s i j)in
   (scs_v34 (scs_k_v34 s,scs_d_v34 s,a1,am,
	    bm,b1,scs_J_v34 s,scs_lo_v34 s,scs_hi_v34 s,scs_str_v34 s))))`;;

let subdivision_v34 = new_definition `subdivision_v34 c i j s =
  (let precond =   ((scs_a_v34 s i j <= c) /\  c <= scs_b_v34 s i j) in
  let r1 = restriction_typ3_v34 s i j (scs_a_v34 s i j) c in
  let r2 = restriction_typ3_v34 s i j c (scs_b_v34 s i j) in
  let cons_if = (\b r rs. if b then CONS r rs else rs) in
    if precond then (cons_if (FST r1) (SND r1) (cons_if (FST r2) (SND r2) []))  else [])`;;
*)

let restriction_cs1_v34 = new_definition `restriction_cs1_v34 s p q c =
  (let b1 = override (scs_b_v34 s) (scs_k_v34 s) (p,q) c in
   let bm = if (c < scs_bm_v34 s p q) then
     override (scs_bm_v34 s) (scs_k_v34 s) (p,q) c else scs_bm_v34 s in
   (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,
	    bm,b1,scs_J_v34 s,scs_lo_v34 s,scs_hi_v34 s,scs_str_v34 s)))`;;

let restriction_cs2_v34 = new_definition `restriction_cs2_v34 s p q c =
  (let a1 = override (scs_a_v34 s) (scs_k_v34 s) (p,q) c in
   let am = if (scs_am_v34 s p q < c) then
     override (scs_am_v34 s) (scs_k_v34 s) (p,q) c else scs_am_v34 s in
   (scs_v34 (scs_k_v34 s,scs_d_v34 s,a1,am,scs_bm_v34 s,scs_b_v34 s,
	    scs_J_v34 s,scs_lo_v34 s,scs_hi_v34 s,scs_str_v34 s)))`;;

let subdiv_v34 = new_definition `subdiv_v34 s p q c = 
  (if (c <= scs_a_v34 s p q) then [s]
  else (if (c <= scs_am_v34 s p q) then [restriction_cs2_v34 s p q c]
  else (if (c < scs_bm_v34 s p q) then [restriction_cs1_v34 s p q c;restriction_cs2_v34 s p q c]
  else (if (c < scs_b_v34 s p q) then [restriction_cs1_v34 s p q c]
  else [s]))))`  

let transfer_v34 = new_definition `transfer_v34 s s' <=>
  (is_ear_v34 s ==> (s = s')) /\
    (is_scs_v34 s') /\
    (unadorned_v34 s') /\
    scs_d_v34 s <= scs_d_v34 s' /\
    (scs_k_v34 s' = scs_k_v34 s) /\
    (!i j. scs_a_v34 s' i j <= scs_a_v34 s i j) /\
    (!i j. scs_b_v34 s i j <= scs_b_v34 s' i j) /\
    (!i j. scs_J_v34 s' i j ==>  scs_J_v34 s i j)
    `;;

let scs_prop_equ_v34 = new_definition `scs_prop_equ_v34 s i =
  (let shift1 = \ (f:num->bool) j. f (i + j) in
  let shift2 = \ (f:num->num-> real) j j'. (f (i + j)  (i + j')) in
  let shift2b = \ (f:num->num-> bool) j j'. (f (i + j)  (i + j')) in
    (scs_v34 (scs_k_v34 s,scs_d_v34 s,shift2 (scs_a_v34 s),shift2 (scs_am_v34 s),shift2 (scs_bm_v34 s),
	    shift2 (scs_b_v34 s), shift2b (scs_J_v34 s), shift1 (scs_lo_v34 s) , 
	      shift1 (scs_hi_v34 s), shift1 (scs_str_v34 s))))`;;

let scs_opp_v34 = new_definition `scs_opp_v34 s =
  (let k = scs_k_v34 s in
     scs_v34 (scs_k_v34 s,scs_d_v34 s, peropp2 (scs_a_v34 s) k, peropp2 (scs_am_v34 s) k,
	     peropp2 (scs_bm_v34 s) k,peropp2 (scs_b_v34 s) k,
	     peropp2 (scs_J_v34 s) k, peropp (scs_lo_v34 s) k,
	      peropp(scs_hi_v34 s) k, peropp (scs_str_v34 s) k))`;;

(* 0--(p-1) slice: *)

(*
let torsor_slice_v34 = new_definition `torsor_slice_v34 s p = 
  (
   let mod1 = \ (f:num->bool) j. f (j MOD p) in
   let mod2 = \ (f:num->num->real) j j'. f (j MOD p) (j' MOD p) in
   let mod2b = \ (f:num->num->bool) j j'. f (j MOD p) (j' MOD p) in
    (scs_v34 (scs_k_v34 s,scs_d_v34 s,mod2 (scs_a_v34 s),mod2 (scs_am_v34 s),mod2 (scs_bm_v34 s),
	    mod2 (scs_b_v34 s), mod2b (scs_J_v34 s), mod1 (scs_lo_v34 s) , mod1 (scs_str_v34 s))))`;;
*)

let scs_diag = new_definition `scs_diag k i j <=>
  ~(i MOD k = j MOD k) /\
  ~(SUC i MOD k = j MOD k) /\
  ~(i MOD k = SUC j MOD k)`;;


let scs_half_slice_v34 = new_definition `scs_half_slice_v34 s p q d' mkj = 
  (let p' = p MOD (scs_k_v34 s) in
   let k' = (q + 1 + (scs_k_v34 s - p')) MOD (scs_k_v34 s) in
   let mod2 = \ (f:num->num->real) j j'. f ((j MOD k')+ p') ((j' MOD k') + p') in
   let mod2b = \ (f:num->num->bool) j j'. f ((j MOD k')+ p') ((j' MOD k') + p') in
   let a1 = \ i'' j''. if {i'' MOD k',j'' MOD k'} = {0,k'-1} 
	     then scs_am_v34 s p q else mod2 (scs_a_v34 s) i'' j'' in
   let b1 = \ i'' j''. if {i'' MOD k',j'' MOD k'} = {0,k'-1} 
	     then scs_bm_v34 s p q else mod2 (scs_b_v34 s) i'' j'' in
   let J = \ i'' j''. if {i'' MOD k',j'' MOD k'} = {0,k'-1} then mkj else mod2b (scs_J_v34 s) i'' j'' in
     scs_v34 (k',d',a1,a1,b1,b1,J,{},{},{}))`;;

let scs_slice_v34 = new_definition `scs_slice_v34 s p q d' d'' mkj = 
  (scs_half_slice_v34 s p q d' mkj,
   scs_half_slice_v34 s q p d'' mkj)`;;

let is_scs_slice_v34 = new_definition `is_scs_slice_v34 s s' s'' p q <=>
  (let d' = scs_d_v34 s' in
   let d'' = scs_d_v34 s'' in
   let mkj = scs_J_v34 s' 0 (scs_k_v34 s' - 1) in
     (((s',s'') = scs_slice_v34 s p q d' d'' mkj) /\
	 d' < #0.9 /\
	 d'' < #0.9 /\
	 scs_d_v34 s <= d' + d'' /\
	 scs_bm_v34 s p q < &4 /\
	 ((scs_k_v34 s = 4) \/ scs_bm_v34 s p q <= cstab) /\
	 (mkj ==> (is_ear_v34 s' \/ is_ear_v34 s''))))`;;
       
let QKNVMLB1_concl = `!s p q d' mkj vv.
     (let s' = scs_half_slice_v34 s p q d' mkj in
      let vv'  = (\i. vv ((i+p) MOD (scs_k_v34 s'))) in
	MMs_v34 s vv /\
	  scs_bm_v34 s p q < &4 /\
	  ((scs_k_v34 s = 4) \/ scs_bm_v34 s p q <= cstab) /\
	is_scs_v34 s /\ d' < #0.9 /\ scs_diag (scs_k_v34 s) p q ==> 
	BBs_v34 s' vv')`;;
  
let QKNVMLB2_concl = `!s p q d' d'' mkj vv.
     (let s' = FST (scs_slice_v34 s p q d' d'' mkj) in
      let s'' = SND (scs_slice_v34 s p q d' d'' mkj) in
      let vv'  = (\i. vv ((i+p) MOD (scs_k_v34 s'))) in
      let vv'' = (\i. vv ((i+q) MOD (scs_k_v34 s''))) in
	MMs_v34 s vv /\
	is_scs_v34 s /\ scs_diag (scs_k_v34 s) p q /\
	  is_scs_slice_v34 s s' s'' p q /\
	scs_d_v34 s <= d' + d'' ==>
	  dsv_v34 s vv <= dsv_v34 s' vv' + dsv_v34 s'' vv'')`;;

let QKNVMLB3_concl = `!s p q d' d'' mkj vv.
     (let s' = FST (scs_slice_v34 s p q d' d'' mkj) in
      let s'' = SND (scs_slice_v34 s p q d' d'' mkj) in
      let vv'  = (\i. vv ((i+p) MOD (scs_k_v34 s'))) in
      let vv'' = (\i. vv ((i+q) MOD (scs_k_v34 s''))) in
	MMs_v34 s vv /\
	is_scs_v34 s /\ scs_diag (scs_k_v34 s) p q /\
 	  is_scs_slice_v34 s s' s'' p q /\
	scs_d_v34 s <= d' + d'' ==>
	  taustar_v34 s' vv' + taustar_v34 s'' vv'' <= taustar_v34 s vv)`;;

(* AZGJNZO *)

let scs_arrow_v34 = new_definition `scs_arrow_v34 S1 S2 <=> 
  (!s. s IN S2 ==> is_scs_v34 s) /\
  ((!s. s IN S1 ==> MMs_v34 s = {}) \/ 
     (?s. s IN S2 /\ ~(MMs_v34 s = {})))`;;
	  
let FZIOTEF_REFL = prove_by_refinement(
  `!S.  (!s. s IN S ==> is_scs_v34 s) ==> scs_arrow_v34 S S`,
  (* {{{ proof *)
  [
  REWRITE_TAC[scs_arrow_v34];
  BY(MESON_TAC[])
  ]);;
  (* }}} *)

let FZIOTEF_TRANS = prove_by_refinement(
  `!S1 S2 S3.  scs_arrow_v34 S1 S2 /\ scs_arrow_v34 S2 S3 ==>
  scs_arrow_v34 S1 S3`,
  (* {{{ proof *)
  [
  REWRITE_TAC[scs_arrow_v34];
  REPEAT WEAKER_STRIP_TAC;
  ASM_REWRITE_TAC[];
  BY(ASM_MESON_TAC[])
  ]);;
  (* }}} *)

let EQTTNZI1_concl = `!s. is_scs_v34 s /\ 
  (!i j. scs_J_v34 s i j ==> scs_b_v34 s i j = scs_bm_v34 s i j) /\
  (scs_J_v34 s = (\i j. F) \/ 3 < scs_k_v34 s)
  ==> scs_arrow_v34 {s} {restriction_typ1_v34 s}`;;

let EQTTNZI2_concl = `!s t. is_scs_v34 s /\ 
  (scs_am_v34 s = scs_bm_v34 s) /\
  (t = restriction_typ2_v34 s) /\ (!i j. ~scs_J_v34 s i j) /\
  (!i. scs_am_v34 s i i = &0) /\
  CARD { i | i < scs_k_v34 t /\ (&2 * h0 < scs_b_v34 t i (SUC i) \/ &2 < scs_a_v34 t i (SUC i)) } + scs_k_v34 t <= 6
  ==> scs_arrow_v34 {s} {t}`;;

let UAGHHBM_concl = `!s i j c .
    is_scs_v34 s /\
    scs_a_v34 s i j <= c /\  c <= scs_b_v34 s i j ==>
    scs_arrow_v34 {s} (set_of_list (subdiv_v34 c i j s))`;;

let YXIONXL1_concl = `!s t.
  is_scs_v34 s /\
  transfer_v34 s t ==> scs_arrow_v34 {s} {t}`;;

let YXIONXL2_concl = `!s.
  is_scs_v34 s ==> scs_arrow_v34 {s} {scs_opp_v34 s}`;;

let YXIONXL3_concl = `!s i.
  is_scs_v34 s ==> scs_arrow_v34 {s} {scs_prop_equ_v34 s i}`;;

let LKGRQUI_concl = `!s s' s'' p q d' d'' mkj.
  is_scs_v34 s /\ (s',s'') = scs_slice_v34 s p q d' d'' mkj ==> scs_arrow_v34 {s} {s',s''}`;;

let HXHYTIJ_concl = `!s vv ww.
  is_scs_v34 s /\
  BBprime2_v34 s vv /\
  BBs_v34 s ww ==>
  (taustar_v34 s vv < taustar_v34 s ww  \/
     BBindex_v34 s vv <= BBindex_v34 s ww)`;;

let ODXLSTCv2_concl = `!s k w l.
  is_scs_v34 s /\
  MMs_v34 s w /\
  k = scs_k_v34 s /\
  (!i. scs_diag k l i ==> &4 * h0 < scs_b_v34 s l i) /\
  ~(&2 = norm (w l)) /\
  (!i. ~(i MOD k = l MOD k) ==> scs_a_v34 s l i < dist(w l,w i)) /\
  (!i. ~(scs_J_v34 s l i)) /\
  (!V E v.
     V = IMAGE w (:num) /\
      E = IMAGE (\i. {w i, w (SUC i)}) (:num) /\
      ~(lunar (v,(w l)) V E )) ==> F`;;

let IMJXPHRv2_concl = `!s k w l.
  is_scs_v34 s /\
  MMs_v34 s w /\
  scs_str_v34 s l /\
  k = scs_k_v34 s /\
  (!i. scs_diag k l i ==> &4 * h0 < scs_b_v34 s l i) /\
  ~(&2 = norm (w l)) /\
  (!i. scs_diag k l i ==> scs_a_v34 s l i < dist(w l,w i)) /\
  (!i. ~(scs_J_v34 s l i)) /\
  (!V E v.
     V = IMAGE w (:num) /\
      E = IMAGE (\i. {w i, w (SUC i)}) (:num) /\
      ~(lunar (v,(w l)) V E )) ==> 
  (scs_a_v34 s l (SUC l) = dist (w l, w (SUC l)) /\
  (scs_a_v34 s l (l + (k-1)) = dist (w l, w (l + (k-1)))))`;;

let NUXCOEAv2_concl = `!s k w l j.
  is_scs_v34 s /\
  MMs_v34 s w /\
  scs_str_v34 s l /\
  k = scs_k_v34 s /\
  (j MOD k = SUC l MOD k \/ SUC j MOD k = l MOD k) /\
  (scs_a_v34 s j l = dist(w j,w l)) /\ 
  (!i. scs_diag k l i ==> &4 * h0 < scs_b_v34 s l i) /\
  (!i. scs_diag k l i ==> scs_a_v34 s l i < dist(w l,w i)) /\
  (!i. ~(scs_J_v34 s l i)) /\
  (!V E v.
     V = IMAGE w (:num) /\
      E = IMAGE (\i. {w i, w (SUC i)}) (:num) /\
      ~(lunar (v,(w l)) V E )) ==> 
  (scs_a_v34 s l (SUC l) = dist (w l, w (SUC l)) /\
  (scs_a_v34 s l (l + (k-1)) = dist (w l, w (l + (k-1)))))`;;

let deform_ODXLSTC_concl = `!s k l.
  is_scs_v34 s /\
  ~(MMs_v34 s = {}) /\
  k = scs_k_v34 s /\
  (!i. ~(scs_J_v34 s l i)) /\
  ~(scs_lo_v34 s l) /\
  (!j. ~(j MOD k = l MOD k) ==> scs_a_v34 s j l < scs_bm_v34 s j l) /\
  (!i. scs_diag k l i ==> &4 * h0 < scs_b_v34 s l i)
  ==>
  scs_arrow_v34 {s} 
   ({(scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,scs_bm_v34 s,
	    scs_b_v34 s, scs_J_v34 s, {j | j MOD k = l} UNION scs_lo_v34 s , 
	       scs_hi_v34 s, scs_str_v34 s))} UNION 
  (IMAGE (\j. (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,
	(\i i'. if {i MOD k,i' MOD k} = {j MOD k,l MOD k} then scs_a_v34 s j l
	 else scs_bm_v34 s i i'), scs_b_v34 s,scs_J_v34 s, scs_lo_v34 s , scs_hi_v34 s, scs_str_v34 s)))
   {j | ~(j = l) /\ j < scs_k_v34 s /\ (scs_a_v34 s l j  = scs_am_v34 s l j) }))
  `;;

let deform_IMJXPHR_concl = `!s k p0 p1 p2.
  is_scs_v34 s /\
  k = scs_k_v34 s /\
  p1 = p0 + 1 /\
  p2 = p0 + 2 /\
  scs_str_v34 s p1 /\
  ~(scs_J_v34 s p0 p1) /\
  ~(scs_J_v34 s p1 p2) /\
  ~(scs_lo_v34 s p1) /\
  (!i. scs_diag k p1 i ==> &4 * h0 < scs_b_v34 s p1 i) /\
  (!i. scs_diag k p1 i ==> ~(scs_a_v34 s p1 i = scs_bm_v34 s p1 i)) /\
  (?q0 q2. ({p0,p2} = {q0,q2}) /\  
     scs_a_v34 s p1 q0 = scs_bm_v34 s p1 q0 /\
     ~(scs_a_v34 s p1 q2 = scs_bm_v34 s p1 q2)) ==>
  scs_arrow_v34 {s}
   ({(scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,scs_bm_v34 s,
	    scs_b_v34 s, scs_J_v34 s, 
	       {j | j MOD k = p1} UNION scs_lo_v34 s , scs_hi_v34 s, scs_str_v34 s))} UNION 
  (IMAGE (\j. (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,
	(\i i'. if {i MOD k,i' MOD k} = {j MOD k,p1 MOD k} then scs_a_v34 s j p1
	 else scs_bm_v34 s i i'), scs_b_v34 s,scs_J_v34 s, scs_lo_v34 s , scs_hi_v34 s,scs_str_v34 s)))
   {j | ~(j = p1) /\ j < scs_k_v34 s /\ (scs_a_v34 s p1 j  = scs_am_v34 s p1 j) }))`;;

let deform_NUXCOEA_concl = `!s k p0 p1 p2 p'.
  is_scs_v34 s /\
  k = scs_k_v34 s /\
  p1 = p0 + 1 /\
  p2 = p0 + 2 /\
  scs_str_v34 s p1 /\
  ~(scs_J_v34 s p0 p1) /\
  ~(scs_J_v34 s p1 p2) /\
  (!i. scs_diag k p1 i ==> &4 * h0 < scs_b_v34 s p1 i) /\
  (!i. scs_diag k p1 i ==> ~(scs_a_v34 s p1 i = scs_bm_v34 s p1 i)) /\
  (?p''. ({p0,p2} = {p',p''}) /\  
     scs_a_v34 s p1 p' = scs_bm_v34 s p1 p' /\
     ~(scs_a_v34 s p1 p'' = scs_bm_v34 s p1 p'')) ==>
  scs_arrow_v34 {s}
  (IMAGE (\j. (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,
	(\i i'. if {i MOD k,i' MOD k} = {j MOD k,p1 MOD k} then scs_a_v34 s j p1
	 else scs_bm_v34 s i i'), scs_b_v34 s,scs_J_v34 s, scs_lo_v34 s , scs_hi_v34 s,scs_str_v34 s)))
   {j | ~(j = p1) /\ ~(j = p') /\ j < scs_k_v34 s /\ (scs_a_v34 s p1 j  = scs_am_v34 s p1 j) })`;;

let arcmax_v34 = new_definition `arcmax_v34 s (p1,p2) = 
  arclength (&2) (&2) (scs_bm_v34 s p1 p2)`;;

let arc1553 = new_definition `arc1553 = arclength (&2) (&2) (#15.53)`;;

let deform_2065952723_A1_single = `!s k p1 p2 p'.
  is_scs_v34 s /\
  k = scs_k_v34 s /\
  p2 = p1 + 1 /\
  arcmax_v34 s (p1,p2) < arc1553 /\
  ~(scs_J_v34 s p1 p2) /\
  ~(scs_str_v34 s p1) /\
  ~(scs_str_v34 s p2) /\
  (!i. scs_diag k p1 i ==> &4 * h0 < scs_b_v34 s p1 i) /\
  (!i. scs_diag k p1 i ==> ~(scs_a_v34 s p1 i = scs_bm_v34 s p1 i)) /\
  ~(scs_a_v34 s p1 p2 = scs_bm_v34 s p1 p2) /\
  ~(scs_b_v34 s p1 p2 = scs_am_v34 s p1 p2) /\
  (?p''. ({p0,p2} = {p',p''}) /\  
     scs_a_v34 s p1 p' = scs_bm_v34 s p1 p' /\
     ~(scs_a_v34 s p1 p'' = scs_bm_v34 s p1 p'')) ==>
  scs_arrow_v34 {s}
  ({(scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,scs_bm_v34 s,
	    scs_b_v34 s,scs_J_v34 s,
	      ({j | j MOD k = p1 MOD k} UNION scs_lo_v34 s) , scs_hi_v34 s,scs_str_v34 s)),
   (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,scs_bm_v34 s,
	    scs_b_v34 s,scs_J_v34 s,
	     ({j | j MOD k = p2 MOD k} UNION scs_lo_v34 s) , scs_hi_v34 s,scs_str_v34 s))}
  UNION
  (IMAGE (\j. (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,
	(\i i'. if {i MOD k,i' MOD k} = {j MOD k,p1 MOD k} then scs_a_v34 s j p1
	 else scs_bm_v34 s i i'), scs_b_v34 s,scs_J_v34 s, scs_lo_v34 s , scs_hi_v34 s, scs_str_v34 s)))
   {j | j < scs_k_v34 s /\ (scs_a_v34 s p1 j  = scs_am_v34 s p1 j) })
  UNION
  (IMAGE (\j. (scs_v34 (scs_k_v34 s,scs_d_v34 s,scs_a_v34 s,scs_am_v34 s,
	(\i i'. if {i MOD k,i' MOD k} = {j MOD k,p1 MOD k} then scs_a_v34 s j p1
	 else scs_bm_v34 s i i'), scs_b_v34 s,scs_J_v34 s, scs_lo_v34 s , scs_hi_v34 s,scs_str_v34 s)))
   {j | ~(j = p1) /\ ~(j = p') /\ j < scs_k_v34 s /\ (scs_a_v34 s p1 j  = scs_am_v34 s p1 j) }))`;;

(* insert these *)

let deform_2065952723_A1_double = `T `;;
let deform_4828966562 = `T `;;
let deform_4828966562A = `T `;;
let deform_4828966562B = `T `;;
let deform_4828966562_obtuse = `T `;;
let deform_4828966562A_obtuse = `T `;;
let deform_4828966562B_obtuse = `T `;;
let deform_6843920790 = `T `;;
let deform_6843920790_quad = `T `;;
let deform_6843920790_tri = `T `;;
let deform_684_quadA = `T `;;
let deform_684_quadB = `T `;;

let OCBICBY_concl = `(!s. MEM s scs_terminal_v34 ==> (MMs_v34 s = {}))`;;


let sterm_is_scs_concl = `!s. MEM s scs_terminal_v34 ==> is_scs_v34 s`;;

(* hexagons *)

let hex_assumptions = 
  `[HD (subdiv_v34 cstab 0 2  (EL 0 s_init_list_v34));
    HD (subdiv_v34 cstab 0 3  (EL 0 s_init_list_v34))]`;;

let hex_std_postslice = 
  Ineq.mk_tplate 
  `\hex_assumptions. (let s02 = EL 0 hex_assumptions in
  let d02' =  (#0.616) in
  let d02 = scs_d_v34 s02 - d02' in
  let vv02 = scs_slice_v34 s02 2 0 d02' d02 F in
  let s03 = EL 1 hex_assumptions in
  let d03' = scs_d_v34 s03 / &2 in
  let vv03 = (scs_slice_v34 s03 0 3 d03' d03' F) in
    [FST vv02; SND vv02; FST vv03])` [hex_assumptions];;

let check_completeness_hex_claimA_concl = ();;

let check_completeness_hex_claimB_concl = 
    Ineq.mk_tplate `\x. scs_arrow_v34 {EL 0 s_init_list_v34} 
      (set_of_list scs_terminal_v34 UNION (set_of_list x))` [hex_assumptions];;


end;;

(*
let check_completeness_claimA_concl = 
  Ineq.mk_tplate `\x. scs_arrow_v34 (set_of_list x) 
*)




